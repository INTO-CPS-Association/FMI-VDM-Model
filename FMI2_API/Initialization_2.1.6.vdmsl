/**
 * This file is part of the INTO-CPS toolchain.
 *
 * Copyright (c) 2017-2019, INTO-CPS Association,
 * c/o Professor Peter Gorm Larsen, Department of Engineering
 * Finlandsgade 22, 8200 Aarhus N.
 *
 * All rights reserved.
 *
 * THIS PROGRAM IS PROVIDED UNDER THE TERMS OF GPL VERSION 3 LICENSE OR
 * THIS INTO-CPS ASSOCIATION PUBLIC LICENSE VERSION 1.0.
 * ANY USE, REPRODUCTION OR DISTRIBUTION OF THIS PROGRAM CONSTITUTES
 * RECIPIENT'S ACCEPTANCE OF THE OSMC PUBLIC LICENSE OR THE GPL 
 * VERSION 3, ACCORDING TO RECIPIENTS CHOICE.
 *
 * The INTO-CPS toolchain  and the INTO-CPS Association Public License are
 * obtained from the INTO-CPS Association, either from the above address, from
 * the URLs: http://www.into-cps.org, and in the INTO-CPS toolchain distribution.
 * GNU version 3 is obtained from: http://www.gnu.org/copyleft/gpl.html.
 *
 * This program is distributed WITHOUT ANY WARRANTY; without
 * even the implied warranty of  MERCHANTABILITY or FITNESS FOR
 * A PARTICULAR PURPOSE, EXCEPT AS EXPRESSLY SET FORTH IN THE
 * BY RECIPIENT SELECTED SUBSIDIARY LICENSE CONDITIONS OF
 * THE INTO-CPS ASSOCIATION.
 *
 * See the full INTO-CPS Association Public License conditions for more details.
 */

/**
 * 2.1.6  Initialization, Termination, and Resetting an FMU
 */
functions
	fmi2SetupExperiment: fmi2Component * fmi2Boolean * fmi2Real * fmi2Real * fmi2Boolean * fmi2Real +> fmi2Status * fmi2Component
	fmi2SetupExperiment(c, toleranceDefined, tolerance, startTime, stopTimeDefined, stopTime) ==
		mk_(<fmi2OK>, c)
	pre
		c.fmuState = <INSTANTIATED>
		and (stopTimeDefined => stopTime > startTime)
	post
		c.fmuState = <INSTANTIATED>;

	fmi2EnterInitializationMode: fmi2Component +> fmi2Status * fmi2Component
	fmi2EnterInitializationMode(c) ==
		mk_(<fmi2OK>, mu(c, fmuState |-> <INITIALIZATION>))
	pre
		c.fmuState = <INSTANTIATED>
	post
		RESULT.#2.fmuState = <INITIALIZATION>;

	fmi2ExitInitializationMode: fmi2Component +> fmi2Status * fmi2Component
	fmi2ExitInitializationMode(c) ==
		mk_(<fmi2OK>, mu(c, fmuState |-> <EVENT_MODE>))
	pre
		c.fmuState = <INITIALIZATION>
	post
		RESULT.#2.fmuState = <EVENT_MODE>;

	fmi2Terminate: fmi2Component +> fmi2Status * fmi2Component
	fmi2Terminate(c) ==
		mk_(<fmi2OK>, mu(c, fmuState |-> <TERMINATED>))
	pre
		c.fmuState = <EVENT_MODE>
	post
		RESULT.#2.fmuState = <TERMINATED>;

	fmi2Reset: fmi2Component +> fmi2Status * fmi2Component
	fmi2Reset(c) ==
		mk_(<fmi2OK>, mu(c, fmuState |-> <START>))
	pre
		c.fmuState = <TERMINATED>
	post
		RESULT.#2.fmuState = <START>;

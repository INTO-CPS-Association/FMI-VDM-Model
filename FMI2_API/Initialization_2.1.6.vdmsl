/**
 * This file is part of the INTO-CPS toolchain.
 *
 * Copyright (c) 2017-2019, INTO-CPS Association,
 * c/o Professor Peter Gorm Larsen, Department of Engineering
 * Finlandsgade 22, 8200 Aarhus N.
 *
 * All rights reserved.
 *
 * THIS PROGRAM IS PROVIDED UNDER THE TERMS OF GPL VERSION 3 LICENSE OR
 * THIS INTO-CPS ASSOCIATION PUBLIC LICENSE VERSION 1.0.
 * ANY USE, REPRODUCTION OR DISTRIBUTION OF THIS PROGRAM CONSTITUTES
 * RECIPIENT'S ACCEPTANCE OF THE OSMC PUBLIC LICENSE OR THE GPL 
 * VERSION 3, ACCORDING TO RECIPIENTS CHOICE.
 *
 * The INTO-CPS toolchain  and the INTO-CPS Association Public License are
 * obtained from the INTO-CPS Association, either from the above address, from
 * the URLs: http://www.into-cps.org, and in the INTO-CPS toolchain distribution.
 * GNU version 3 is obtained from: http://www.gnu.org/copyleft/gpl.html.
 *
 * This program is distributed WITHOUT ANY WARRANTY; without
 * even the implied warranty of  MERCHANTABILITY or FITNESS FOR
 * A PARTICULAR PURPOSE, EXCEPT AS EXPRESSLY SET FORTH IN THE
 * BY RECIPIENT SELECTED SUBSIDIARY LICENSE CONDITIONS OF
 * THE INTO-CPS ASSOCIATION.
 *
 * See the full INTO-CPS Association Public License conditions for more details.
 */

/**
 * 2.1.6  Initialization, Termination, and Resetting an FMU
 */
functions
	/**
	 * Implementation and API for fmi2SetupExperiment.
	 */
	fmi2SetupExperiment_Impl: fmi2Component * fmi2Boolean * fmi2Real * fmi2Real * fmi2Boolean * fmi2Real +> fmi2Status * fmi2Component
	fmi2SetupExperiment_Impl(c, toleranceDefined, tolerance, startTime, stopTimeDefined, stopTime) ==
		mk_(<fmi2OK>, c)	-- TODO!
	pre
		c.fmuState = <INSTANTIATED>
		and (stopTimeDefined => stopTime > startTime)
	post
		RESULT.#2.fmuState = <INSTANTIATED>;

	fmi2SetupExperiment: fmi2Component * fmi2Boolean * fmi2Real * fmi2Real * fmi2Boolean * fmi2Real +> fmi2Status * fmi2Component
	fmi2SetupExperiment(c, toleranceDefined, tolerance, startTime, stopTimeDefined, stopTime) ==
		if c.fmuState <> <INSTANTIATED>
		then -- @Printf("Cannot call fmi2SetupExperiment in state %s\n", c.fmuState)
			mk_(<fmi2Error>, c)
		elseif stopTimeDefined and stopTime <= startTime
		then -- @Printf("stopTime is not > startTime")
			mk_(<fmi2Error>, c)
		else
			fmi2SetupExperiment_Impl(c, toleranceDefined, tolerance, startTime, stopTimeDefined, stopTime);


	/**
	 * Implementation and API for fmi2EnterInitializationMode.
	 */
	fmi2EnterInitializationMode_Impl: fmi2Component +> fmi2Status * fmi2Component
	fmi2EnterInitializationMode_Impl(c) ==
		mk_(<fmi2OK>, mu(c, fmuState |-> <INITIALIZATION>))
	pre
		c.fmuState = <INSTANTIATED>
	post
		RESULT.#2.fmuState = <INITIALIZATION>;

	fmi2EnterInitializationMode: fmi2Component +> fmi2Status * fmi2Component
	fmi2EnterInitializationMode(c) ==
		if c.fmuState <> <INSTANTIATED>
		then -- @Printf("Cannot call fmi2EnterInitializationMode in state %s\n", c.fmuState)
			mk_(<fmi2Error>, c)
		else
			fmi2EnterInitializationMode_Impl(c);


	/**
	 * Implementation and API for fmi2ExitInitializationMode.
	 */
	fmi2ExitInitializationMode_Impl: fmi2Component +> fmi2Status * fmi2Component
	fmi2ExitInitializationMode_Impl(c) ==
		mk_(<fmi2OK>, mu(c, fmuState |->
			if c.fmuType = <fmi2CoSimulation>
			then <STEP_COMPLETE>
			else <EVENT_MODE>))
	pre
		c.fmuState = <INITIALIZATION>
	post
		RESULT.#2.fmuState =
			if c.fmuType = <fmi2CoSimulation>
			then <STEP_COMPLETE>
			else <EVENT_MODE>;

	fmi2ExitInitializationMode: fmi2Component +> fmi2Status * fmi2Component
	fmi2ExitInitializationMode(c) ==
		if c.fmuState <> <INITIALIZATION>
		then -- @Printf("Cannot call fmi2ExitInitializationMode in state %s\n", c.fmuState)
			mk_(<fmi2Error>, c)
		else
			fmi2ExitInitializationMode_Impl(c);


	/**
	 * Implementation and API for fmi2Terminate.
	 */
	fmi2Terminate_Impl: fmi2Component +> fmi2Status * fmi2Component
	fmi2Terminate_Impl(c) ==
		mk_(<fmi2OK>, mu(c, fmuState |-> <TERMINATED>))
	pre
		c.fmuType = <fmi2CoSimulation> and c.fmuState in set {<STEP_COMPLETE>, <STEP_FAILED>}
		or
		c.fmuType = <fmi2ModelExchange> and c.fmuState in set {<EVENT_MODE>, <CT_MODE>}
	post
		RESULT.#2.fmuState = <TERMINATED>;

	fmi2Terminate: fmi2Component +> fmi2Status * fmi2Component
	fmi2Terminate(c) ==
		if c.fmuType = <fmi2CoSimulation> and c.fmuState not in set {<STEP_COMPLETE>, <STEP_FAILED>}
		then -- @Printf("Must be in state STEP_COMPLETE or STEP_FAILED")
			mk_(<fmi2Error>, c)
		elseif c.fmuType = <fmi2ModelExchange> and c.fmuState not in set {<EVENT_MODE>, <CT_MODE>}
		then -- @Printf("Must be in state EVENT_MODE or CT_MODE")
			mk_(<fmi2Error>, c)
		else
			fmi2Terminate_Impl(c);


	/**
	 * Implementation and API for fmi2Reset.
	 */
	fmi2Reset_Impl: fmi2Component +> fmi2Status * fmi2Component
	fmi2Reset_Impl(c) ==
		mk_(<fmi2OK>, mu(c, fmuState |-> <INSTANTIATED>))
	pre
		c.fmuState = <TERMINATED>
	post
		RESULT.#2.fmuState = <INSTANTIATED>;

	fmi2Reset: fmi2Component +> fmi2Status * fmi2Component
	fmi2Reset(c) ==
		if c.fmuState <> <TERMINATED>
		then -- @Printf("Cannot call fmi2Reset in state %s\n", c.fmuState)
			mk_(<fmi2Error>, c)
		else
			fmi2Reset_Impl(c);
	
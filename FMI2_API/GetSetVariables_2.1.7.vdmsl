/**
 * This file is part of the INTO-CPS toolchain.
 *
 * Copyright (c) 2017-2019, INTO-CPS Association,
 * c/o Professor Peter Gorm Larsen, Department of Engineering
 * Finlandsgade 22, 8200 Aarhus N.
 *
 * All rights reserved.
 *
 * THIS PROGRAM IS PROVIDED UNDER THE TERMS OF GPL VERSION 3 LICENSE OR
 * THIS INTO-CPS ASSOCIATION PUBLIC LICENSE VERSION 1.0.
 * ANY USE, REPRODUCTION OR DISTRIBUTION OF THIS PROGRAM CONSTITUTES
 * RECIPIENT'S ACCEPTANCE OF THE OSMC PUBLIC LICENSE OR THE GPL 
 * VERSION 3, ACCORDING TO RECIPIENTS CHOICE.
 *
 * The INTO-CPS toolchain  and the INTO-CPS Association Public License are
 * obtained from the INTO-CPS Association, either from the above address, from
 * the URLs: http://www.into-cps.org, and in the INTO-CPS toolchain distribution.
 * GNU version 3 is obtained from: http://www.gnu.org/copyleft/gpl.html.
 *
 * This program is distributed WITHOUT ANY WARRANTY; without
 * even the implied warranty of  MERCHANTABILITY or FITNESS FOR
 * A PARTICULAR PURPOSE, EXCEPT AS EXPRESSLY SET FORTH IN THE
 * BY RECIPIENT SELECTED SUBSIDIARY LICENSE CONDITIONS OF
 * THE INTO-CPS ASSOCIATION.
 *
 * See the full INTO-CPS Association Public License conditions for more details.
 */

/**
 * 2.1.7 Getting and Setting Variable Values
 */
functions
	/**
	 * The general contracts for calling fmi2GetXXX and fmi2SetXXX functions in a given state.
	 */
	generalGetPrecondition: fmi2Component * seq of fmi2ValueReference * Kind +> bool
	generalGetPrecondition(c, vrs, kind) ==
		cases c.fmuState:
			<INSTANTIATED>		-> false,
			<INITIALIZATION>	-> varsINIT(c, vrs, kind),
			<EVENT_MODE>		-> true,
			<CT_MODE>			-> true,
			<STEP_COMPLETE>		-> true,
			<STEP_IN_PROGRESS>	-> false,
			<STEP_FAILED>		-> true,
			<STEP_CANCELLED>	-> true,
			<TERMINATED>		-> true,
			<ERROR>				-> true,

			others -> undefined
		end;

	generalSetPrecondition: fmi2Component * seq of fmi2ValueReference * Kind +> bool
	generalSetPrecondition(c, vrs, kind) ==
		if c.fmuType = <fmi2ModelExchange>
		then
			cases c.fmuState:
				<INSTANTIATED>		-> varsSEA(c, vrs, kind),
				<INITIALIZATION>	-> varsSE(c, vrs, kind),
				<EVENT_MODE>		-> varsIN(c, vrs, kind) or varsINC(c, vrs, kind),
				<CT_MODE>			-> varsINC(c, vrs, kind),
				<TERMINATED>		-> false,
				<ERROR>				-> false,

				others -> undefined
			end
		else	-- fmi2CoSimulation
			cases c.fmuState:
				<INSTANTIATED>		-> varsINI(c, vrs, kind),
				<INITIALIZATION>	-> varsIN(c, vrs, kind) or varsINI(c, vrs, kind),
				<STEP_COMPLETE>		-> varsIN(c, vrs, kind),
				<STEP_IN_PROGRESS>	-> false,
				<STEP_FAILED>		-> false,
				<STEP_CANCELLED>	-> false,
				<TERMINATED>		-> false,
				<ERROR>				-> false,

				others -> undefined
			end;



	/**
	 * Implementation and API for fmi2GetReal.
	 */
	fmi2GetReal_Impl: fmi2Component * seq of fmi2ValueReference * nat +> fmi2Status * [seq of fmi2Real]
	fmi2GetReal_Impl(c, vrs, nvr) ==
		let mk_(s, c2) = if c.dirty then c.calculateValues(c) else mk_(<fmi2OK>, c) in
			if s <> <fmi2OK>
			then mk_(s, nil)
			else mk_(<fmi2OK>, [ c2.variables(c2.indexmap(mk_AliasTag(vr, <Real>))) | vr in seq vrs ])
	pre
		len vrs = nvr
		and generalGetPrecondition(c, vrs, <Real>)
		and forall vr in seq vrs & lookup(c.eModelVariables, vr, <Real>) <> nil
	post
		RESULT.#1 = <fmi2OK> => len RESULT.#2 = nvr;

	fmi2GetReal: fmi2Component * seq of fmi2ValueReference * nat +> fmi2Status * [seq of fmi2Real]
	fmi2GetReal(c, vrs, nvr) ==
		if len vrs <> nvr
		then -- @Printf("vrs and nvr do not match\n")
			mk_(<fmi2Error>, nil)
		elseif not generalGetPrecondition(c, vrs, <Real>)
		then -- @Printf("Get preconditions failed\n")
			mk_(<fmi2Error>, nil)
		elseif exists vr in seq vrs & lookup(c.eModelVariables, vr, <Real>) = nil
		then -- @Printf("Value references are not all Real variables\n")
			mk_(<fmi2Error>, nil)
		else
			fmi2GetReal_Impl(c, vrs, nvr)
	 post
	 	RESULT.#1 = <fmi2OK> <=> RESULT.#2 <> nil;


	/**
	 * Implementation and API for fmi2GetInteger.
	 */
	fmi2GetInteger_Impl: fmi2Component * seq of fmi2ValueReference * nat +> fmi2Status * [seq of fmi2Integer]
	fmi2GetInteger_Impl(c, vrs, nvr) ==
		let mk_(s, c2) = if c.dirty then c.calculateValues(c) else mk_(<fmi2OK>, c) in
			if s <> <fmi2OK>
			then mk_(s, nil)
			else mk_(<fmi2OK>, [ c2.variables(c2.indexmap(mk_AliasTag(vr, <Integer>))) | vr in seq vrs ])
	pre
		len vrs = nvr
		and generalGetPrecondition(c, vrs, <Integer>)
		and forall vr in seq vrs & lookup(c.eModelVariables, vr, <Integer>) <> nil
	post
		RESULT.#1 <> <fmi2Discard>
		and (RESULT.#1 = <fmi2OK> => len RESULT.#2 = nvr);

	fmi2GetInteger: fmi2Component * seq of fmi2ValueReference * nat +> fmi2Status * [seq of fmi2Integer]
	fmi2GetInteger(c, vrs, nvr) ==
		if len vrs <> nvr
		then -- @Printf("vrs and nvr do not match\n")
			mk_(<fmi2Error>, nil)
		elseif not generalGetPrecondition(c, vrs, <Integer>)
		then -- @Printf("Get preconditions failed\n")
			mk_(<fmi2Error>, nil)
		elseif exists vr in seq vrs & lookup(c.eModelVariables, vr, <Integer>) = nil
		then -- @Printf("Value references are not all Integer variables\n")
			mk_(<fmi2Error>, nil)
		else
			fmi2GetInteger_Impl(c, vrs, nvr)
	 post
	 	RESULT.#1 = <fmi2OK> <=> RESULT.#2 <> nil;


	/**
	 * Implementation and API for fmi2GetBoolean.
	 */
	fmi2GetBoolean_Impl: fmi2Component * seq of fmi2ValueReference * nat +> fmi2Status * [seq of fmi2Boolean]
	fmi2GetBoolean_Impl(c, vrs, nvr) ==
		let mk_(s, c2) = if c.dirty then c.calculateValues(c) else mk_(<fmi2OK>, c) in
			if s <> <fmi2OK>
			then mk_(s, nil)
			else mk_(<fmi2OK>, [ c2.variables(c2.indexmap(mk_AliasTag(vr, <Boolean>))) | vr in seq vrs ])
	pre
		len vrs = nvr
		and generalGetPrecondition(c, vrs, <Boolean>)
		and forall vr in seq vrs & lookup(c.eModelVariables, vr, <Boolean>) <> nil
	post
		RESULT.#1 <> <fmi2Discard>
		and (RESULT.#1 = <fmi2OK> => len RESULT.#2 = nvr);

	fmi2GetBoolean: fmi2Component * seq of fmi2ValueReference * nat +> fmi2Status * [seq of fmi2Boolean]
	fmi2GetBoolean(c, vrs, nvr) ==
		if len vrs <> nvr
		then -- @Printf("vrs and nvr do not match\n")
			mk_(<fmi2Error>, nil)
		elseif not generalGetPrecondition(c, vrs, <Boolean>)
		then -- @Printf("Get preconditions failed\n")
			mk_(<fmi2Error>, nil)
		elseif exists vr in seq vrs & lookup(c.eModelVariables, vr, <Boolean>) = nil
		then -- @Printf("Value references are not all Boolean variables\n")
			mk_(<fmi2Error>, nil)
		else
			fmi2GetBoolean_Impl(c, vrs, nvr)
	 post
	 	RESULT.#1 = <fmi2OK> <=> RESULT.#2 <> nil;


	/**
	 * Implementation and API for fmi2GetString.
	 */
	fmi2GetString_Impl: fmi2Component * seq of fmi2ValueReference * nat +> fmi2Status * [seq of fmi2String]
	fmi2GetString_Impl(c, vrs, nvr) ==
		let mk_(s, c2) = if c.dirty then c.calculateValues(c) else mk_(<fmi2OK>, c) in
			if s <> <fmi2OK>
			then mk_(s, nil)
			else mk_(<fmi2OK>, [ c2.variables(c2.indexmap(mk_AliasTag(vr, <String>))) | vr in seq vrs ])
	pre
		len vrs = nvr
		and generalGetPrecondition(c, vrs, <String>)
		and forall vr in seq vrs & lookup(c.eModelVariables, vr, <String>) <> nil
	post
		RESULT.#1 <> <fmi2Discard>
		and (RESULT.#1 = <fmi2OK> => len RESULT.#2 = nvr);

	fmi2GetString: fmi2Component * seq of fmi2ValueReference * nat +> fmi2Status * [seq of fmi2String]
	fmi2GetString(c, vrs, nvr) ==
		if len vrs <> nvr
		then -- @Printf("vrs and nvr do not match\n")
			mk_(<fmi2Error>, nil)
		elseif not generalGetPrecondition(c, vrs, <String>)
		then -- @Printf("Get preconditions failed\n")
			mk_(<fmi2Error>, nil)
		elseif exists vr in seq vrs & lookup(c.eModelVariables, vr, <String>) = nil
		then -- @Printf("Value references are not all String variables\n")
			mk_(<fmi2Error>, nil)
		else
			fmi2GetString_Impl(c, vrs, nvr)
	 post
	 	RESULT.#1 = <fmi2OK> <=> RESULT.#2 <> nil;


	/**
	 * Implementation and API for fmi2SetReal.
	 */
	fmi2SetReal_Impl: fmi2Component * seq1 of fmi2ValueReference * nat1 * seq1 of fmi2Real +> fmi2Status * fmi2Component
	fmi2SetReal_Impl(c, vrs, nvr, value) ==
		let updates = { c.indexmap(mk_AliasTag(vrs(i), <Real>)) |-> value(i) | i in set inds vrs } in
			mk_(<fmi2OK>, mu(c,
				variables |-> c.variables ++ updates,
				dirty     |-> true))
	pre
		len vrs = nvr and len vrs = len value
		and generalSetPrecondition(c, vrs, <Real>)
		and forall i in set inds vrs &
			let vr = vrs(i),
				sv = lookup(c.eModelVariables, vr, <Real>)
			in
				sv <> nil
				and sv.causality <> <independent>
				and let min = sv.variable.min,
						max = sv.variable.max
					in
						(min <> nil => value(i) >= min)	and (max <> nil => value(i) <= max);

	fmi2SetReal: fmi2Component * seq1 of fmi2ValueReference * nat1 * seq1 of fmi2Real +> fmi2Status * [fmi2Component]
	fmi2SetReal(c, vrs, nvr, value) ==
		if len vrs <> nvr
		then -- @Printf("vrs and nvr do not match\n")
			mk_(<fmi2Error>, nil)
		elseif len vrs <> len value
		then -- @Printf("vrs and value do not match\n")
			mk_(<fmi2Error>, nil)
		elseif not generalSetPrecondition(c, vrs, <Real>)
		then -- @Printf("Set preconditions failed\n")
			mk_(<fmi2Error>, nil)
		elseif exists vr in seq vrs & lookup(c.eModelVariables, vr, <Real>) = nil
		then -- @Printf("Value references are not all Real variables\n")
			mk_(<fmi2Error>, nil)
		elseif exists i in set inds vrs &
			let sv = lookup(c.eModelVariables, vrs(i), <Real>),
				min = sv.variable.min,
				max = sv.variable.max
			in
				(min <> nil and value(i) < min)	or (max <> nil and value(i) > max)
		then -- @Printf("Values not all within min/max for variables\n")
			mk_(<fmi2Error>, nil)
		elseif exists i in set inds vrs &
			let sv = lookup(c.eModelVariables, vrs(i), <Real>) in sv.causality = <independent>
		then -- @Printf("Cannot set value of independent variable\n")
			mk_(<fmi2Error>, nil)
		else
			fmi2SetReal_Impl(c, vrs, nvr, value)
	 post
	 	RESULT.#1 = <fmi2OK> <=> RESULT.#2 <> nil;


	/**
	 * Implementation and API for fmi2SetInteger.
	 */
	fmi2SetInteger_Impl: fmi2Component * seq1 of fmi2ValueReference * nat1 * seq1 of fmi2Integer +> fmi2Status * fmi2Component
	fmi2SetInteger_Impl(c, vrs, nvr, value) ==
		let updates = { c.indexmap(mk_AliasTag(vrs(i), <Integer>)) |-> value(i) | i in set inds vrs } in
			mk_(<fmi2OK>, mu(c,
				variables |-> c.variables ++ updates,
				dirty     |-> true))
	pre
		len vrs = nvr and len vrs = len value
		and generalSetPrecondition(c, vrs, <Integer>)
		and forall i in set inds vrs &
			let vr = vrs(i),
				sv = lookup(c.eModelVariables, vr, <Integer>)
			in
				sv <> nil
				and let min = sv.variable.min,
						max = sv.variable.max
					in
						(min <> nil => value(i) >= min)	and (max <> nil => value(i) <= max);

	fmi2SetInteger: fmi2Component * seq1 of fmi2ValueReference * nat1 * seq1 of fmi2Integer +> fmi2Status * [fmi2Component]
	fmi2SetInteger(c, vrs, nvr, value) ==
		if len vrs <> nvr
		then -- @Printf("vrs and nvr do not match\n")
			mk_(<fmi2Error>, nil)
		elseif len vrs <> len value
		then -- @Printf("vrs and value do not match\n")
			mk_(<fmi2Error>, nil)
		elseif not generalSetPrecondition(c, vrs, <Integer>)
		then -- @Printf("Set preconditions failed\n")
			mk_(<fmi2Error>, nil)
		elseif exists vr in seq vrs & lookup(c.eModelVariables, vr, <Integer>) = nil
		then -- @Printf("Value references are not all Integer variables\n")
			mk_(<fmi2Error>, nil)
		elseif exists i in set inds vrs &
			let sv = lookup(c.eModelVariables, vrs(i), <Integer>),
				min = sv.variable.min,
				max = sv.variable.max
			in
				(min <> nil and value(i) < min)	or (max <> nil and value(i) > max)
		then -- @Printf("Values not all within min/max for variables\n")
			mk_(<fmi2Error>, nil)
		else
			fmi2SetInteger_Impl(c, vrs, nvr, value)
	 post
	 	RESULT.#1 = <fmi2OK> <=> RESULT.#2 <> nil;


	/**
	 * Implementation and API for fmi2SetBoolean.
	 */
	fmi2SetBoolean_Impl: fmi2Component * seq1 of fmi2ValueReference * nat1 * seq1 of fmi2Boolean +> fmi2Status * [fmi2Component]
	fmi2SetBoolean_Impl(c, vrs, nvr, value) ==
		let updates = { c.indexmap(mk_AliasTag(vrs(i), <Boolean>)) |-> value(i) | i in set inds vrs } in
			mk_(<fmi2OK>, mu(c,
				variables |-> c.variables ++ updates,
				dirty     |-> true))
	pre
		len vrs = nvr and len vrs = len value
		and generalSetPrecondition(c, vrs, <Boolean>)
		and forall vr in seq vrs &
			lookup(c.eModelVariables, vr, <Boolean>) <> nil;

	fmi2SetBoolean: fmi2Component * seq1 of fmi2ValueReference * nat1 * seq1 of fmi2Boolean +> fmi2Status * [fmi2Component]
	fmi2SetBoolean(c, vrs, nvr, value) ==
		if len vrs <> nvr
		then -- @Printf("vrs and nvr do not match\n")
			mk_(<fmi2Error>, nil)
		elseif len vrs <> len value
		then -- @Printf("vrs and value do not match\n")
			mk_(<fmi2Error>, nil)
		elseif not generalSetPrecondition(c, vrs, <Boolean>)
		then -- @Printf("Set preconditions failed\n")
			mk_(<fmi2Error>, nil)
		elseif exists vr in seq vrs & lookup(c.eModelVariables, vr, <Boolean>) = nil
		then -- @Printf("Value references are not all Boolean variables\n")
			mk_(<fmi2Error>, nil)
		else
			fmi2SetBoolean_Impl(c, vrs, nvr, value)
	 post
	 	RESULT.#1 = <fmi2OK> <=> RESULT.#2 <> nil;


	/**
	 * Implementation and API for fmi2SetString.
	 */
	fmi2SetString_Impl: fmi2Component * seq1 of fmi2ValueReference * nat1 * seq1 of fmi2String +> fmi2Status * fmi2Component
	fmi2SetString_Impl(c, vrs, nvr, value) ==
		let updates = { c.indexmap(mk_AliasTag(vrs(i), <String>)) |-> value(i) | i in set inds vrs } in
			mk_(<fmi2OK>, mu(c,
				variables |-> c.variables ++ updates,
				dirty     |-> true))
	pre
		len vrs = nvr and len vrs = len value
		and generalSetPrecondition(c, vrs, <String>)
		and forall vr in seq vrs &
			lookup(c.eModelVariables, vr, <String>) <> nil;

	fmi2SetString: fmi2Component * seq1 of fmi2ValueReference * nat1 * seq1 of fmi2String +> fmi2Status * [fmi2Component]
	fmi2SetString(c, vrs, nvr, value) ==
		if len vrs <> nvr
		then -- @Printf("vrs and nvr do not match\n")
			mk_(<fmi2Error>, nil)
		elseif len vrs <> len value
		then -- @Printf("vrs and value do not match\n")
			mk_(<fmi2Error>, nil)
		elseif not generalSetPrecondition(c, vrs, <String>)
		then -- @Printf("Set preconditions failed\n")
			mk_(<fmi2Error>, nil)
		elseif exists vr in seq vrs & lookup(c.eModelVariables, vr, <String>) = nil
		then -- @Printf("Value references are not all String variables\n")
			mk_(<fmi2Error>, nil)
		else
			fmi2SetString_Impl(c, vrs, nvr, value)
	 post
	 	RESULT.#1 = <fmi2OK> <=> RESULT.#2 <> nil;

/**
 * 2.2.3 Definition of Types (TypeDefinitions)
 */
types
	RealType ::					-- XSD p39
		min			: [real]
		max			: [real]
		nominal		: [real]
		unbounded	: [bool]
	inv r ==
		-- @OnFail("RealType min not <= max")
		( r.max <> nil and r.min <> nil => r.max >= r.min );

	IntegerType ::				-- XSD p39
		min			: [int]
		max			: [int]
	inv i ==
		-- @OnFail("IntegerType min not <= max")
		( i.max <> nil and i.min <> nil => i.max >= i.min );

	BooleanType ::
		;

	StringType ::
		;

	-- Items of an enumeration as a sequence of "name" and "value" pairs. The
	-- values can be any integer number, but must be unique within the same
	-- enumeration (in order that the mapping between "name" and "value" is
	-- bijective). An Enumeration element must have at least one Item.

	Item ::
		name		: seq1 of char
		value		: int;

	EnumerationType ::			-- XSD p39
		items		: seq1 of Item
	inv e ==
		-- @OnFail("EnumerationType item values do not form a bijection")
		(
			card { i.name | i in seq e.items } = len e.items
			and	card { i.value | i in seq e.items } = len e.items
		);

	SimpleType ::		-- XSD p38
		name		: seq1 of char
		type		: RealType | IntegerType | BooleanType | StringType | EnumerationType;

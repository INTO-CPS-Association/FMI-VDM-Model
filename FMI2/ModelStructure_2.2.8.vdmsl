/**
 * This file is part of the INTO-CPS toolchain.
 *
 * Copyright (c) 2017-2019, INTO-CPS Association,
 * c/o Professor Peter Gorm Larsen, Department of Engineering
 * Finlandsgade 22, 8200 Aarhus N.
 *
 * All rights reserved.
 *
 * THIS PROGRAM IS PROVIDED UNDER THE TERMS OF GPL VERSION 3 LICENSE OR
 * THIS INTO-CPS ASSOCIATION PUBLIC LICENSE VERSION 1.0.
 * ANY USE, REPRODUCTION OR DISTRIBUTION OF THIS PROGRAM CONSTITUTES
 * RECIPIENT'S ACCEPTANCE OF THE OSMC PUBLIC LICENSE OR THE GPL 
 * VERSION 3, ACCORDING TO RECIPIENTS CHOICE.
 *
 * The INTO-CPS toolchain  and the INTO-CPS Association Public License are
 * obtained from the INTO-CPS Association, either from the above address, from
 * the URLs: http://www.into-cps.org, and in the INTO-CPS toolchain distribution.
 * GNU version 3 is obtained from: http://www.gnu.org/copyleft/gpl.html.
 *
 * This program is distributed WITHOUT ANY WARRANTY; without
 * even the implied warranty of  MERCHANTABILITY or FITNESS FOR
 * A PARTICULAR PURPOSE, EXCEPT AS EXPRESSLY SET FORTH IN THE
 * BY RECIPIENT SELECTED SUBSIDIARY LICENSE CONDITIONS OF
 * THE INTO-CPS ASSOCIATION.
 *
 * See the full INTO-CPS Association Public License conditions for more details.
 */

/**
 * 2.2.8 Definition of the Model Structure (ModelStructure)
 */
types
	-- XSD p57
	DependencyKind = <dependent> | <constant> | <fixed> | <tunable> | <discrete>;

	Unknown ::			-- XSD p57
		location			: Location
		index				: nat1
		dependencies		: [seq of nat1]
		dependenciesKind	: [seq of DependencyKind];

	ModelStructure ::	-- XSD p56
		location			: Location
		outputs				: [seq1 of Unknown]
		derivatives			: [seq1 of Unknown]
		initialUnknowns		: [seq1 of Unknown];

functions
	/**
	 * Validate an Unknown structure in isolation.
	 */
	isValidUnknown: Unknown +> bool
	isValidUnknown(u) ==
	-- @OnFail("2.2.8 Unknown %s has invalid dependencies/kinds at line %s", u.index, u.location)
	(
		if u.dependencies <> nil
		then u.dependenciesKind <> nil =>
			{
				-- @OnFail("2.2.8 Dependencies list does not match dependenciesKind at line %s", u.location)
				( len u.dependencies = len u.dependenciesKind ),

				/* @OnFail("2.2.8 Unknown has duplicate indexes %s at line %s",
					{ u.dependencies(a) | a, b in set inds u.dependencies &
						a <> b and u.dependencies(a) = u.dependencies(b) }, u.location ) */
				( len u.dependencies = card elems u.dependencies),

				-- @OnFail("2.2.8 Unknown cannot depend on itself at line %s", u.location)
				(true) -- CHECK THIS! ( u.index not in set elems u.dependencies )
			} = {true}
		else
			u.dependenciesKind = nil
	);

	/**
	 * Validation of a ModelStructure.
	 */
	isValidModelStructure: ModelStructure +> bool
	isValidModelStructure(ms) ==
	{
		-- @OnFail("2.2.8 ModelStructure has invalid Unknowns")
		(
			{
				list <> nil =>
					{ isValidUnknown(u) | u in seq list } = {true}

				| list in set {ms.outputs, ms.derivatives, ms.initialUnknowns}
			} = {true}
		),

		-- @OnFail("2.2.8 InitialUnknowns are not of kind dependent or constant")
		(
			ms.initialUnknowns <> nil =>
				forall iu in seq ms.initialUnknowns &
					iu.dependenciesKind <> nil =>
						forall dk in seq iu.dependenciesKind &
							 dk in set { <dependent>, <constant>, nil }
		)
	} = {true};


/**
 * 2.2.8 Definition of the Model Structure (ModelStructure)
 */
types
	-- XSD p57
	DependencyKind = <dependent> | <constant> | <fixed> | <tunable> | <discrete>;

	Unknown ::			-- XSD p57
		index				: nat1
		dependencies		: [seq of nat1]
		dependenciesKind	: [seq of DependencyKind];

	ModelStructure ::	-- XSD p56
		outputs				: [seq1 of Unknown]
		derivatives			: [seq1 of Unknown]
		initialUnknowns		: [seq1 of Unknown];

functions
	/**
	 * Validate an Unknown structure in isolation.
	 */
	isValidUnknown: Unknown +> bool
	isValidUnknown(u) ==
	-- @OnFail("Unknown %s has invalid dependencies/kinds", u.index)
	(
		if u.dependencies <> nil
		then u.dependenciesKind <> nil =>
			len u.dependencies = len u.dependenciesKind			-- Matching lists
			and len u.dependencies = card elems u.dependencies	-- No duplicates
		else u.dependenciesKind = nil
	);

	/**
	 * Validation of a ModelStructure.
	 */
	isValidModelStructure: ModelStructure +> bool
	isValidModelStructure(ms) ==
	{
		-- @OnFail("ModelStructure has invalid unknowns")
		(
			forall list in set {ms.outputs, ms.derivatives, ms.initialUnknowns} &
				list <> nil =>
					forall u in seq list &
						isValidUnknown(u)
		),

		-- @OnFail("ModelStructure has duplicate variables")
		(
			duplicateCheck(ms.outputs)
			and	duplicateCheck(ms.derivatives)
			and duplicateCheck(ms.initialUnknowns)
		),

		-- @OnFail("ModelStructure.InitialUnknowns are not of kind dependent or constant")
		(
			ms.initialUnknowns <> nil =>
				forall iu in seq ms.initialUnknowns &
					iu.dependenciesKind <> nil =>
						forall dk in seq iu.dependenciesKind &
							 dk in set { <dependent>, <constant>, nil }
		)
	} = {true};


	/**
	 * Check for duplicate indexes in a sequence of Unknowns.
	 */
	duplicateCheck: [seq1 of Unknown] +> bool
	duplicateCheck(su) ==
		su <> nil =>
			let indexes = [ u.index | u in seq su ] in
				len indexes = card elems indexes;

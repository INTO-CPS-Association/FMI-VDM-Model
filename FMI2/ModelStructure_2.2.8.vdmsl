/**
 * 2.2.8 Definition of the Model Structure (ModelStructure)
 */
types
	-- XSD p57
	DependencyKind = <dependent> | <constant> | <fixed> | <tunable> | <discrete>;

	Unknown ::			-- XSD p57
		index				: nat1
		dependencies		: [seq of nat1]
		dependenciesKind	: [seq of DependencyKind]
	inv u ==
		if u.dependencies <> nil
		then u.dependenciesKind <> nil
			and len u.dependencies = len u.dependenciesKind			-- Matching lists
			and len u.dependencies = card elems u.dependencies		-- No duplicates
		else u.dependenciesKind = nil;


	ModelStructure ::	-- XSD p56
		outputs				: [seq1 of Unknown]
		derivatives			: [seq1 of Unknown]
		initialUnknowns		: [seq1 of Unknown]
	inv ms ==
		duplicateCheck(ms.outputs)
		and	duplicateCheck(ms.derivatives)
		and duplicateCheck(ms.initialUnknowns)
		and (ms.initialUnknowns <> nil =>
			forall iu in seq ms.initialUnknowns &
				{ k | k in seq iu.dependenciesKind } subset { <dependent>, <constant> });

functions
	duplicateCheck: [seq1 of Unknown] +> bool
	duplicateCheck(su) ==
		su <> nil => let indexes = [ u.index | u in seq su ]
			in len indexes = card elems indexes;

/*********************************************************************************
 *
 *	Copyright (c) 2017-2022, INTO-CPS Association,
 *	c/o Professor Peter Gorm Larsen, Department of Engineering
 *	Finlandsgade 22, 8200 Aarhus N.
 *
 *	MIT Licence:
 *
 *	Permission is hereby granted, free of charge, to any person obtaining a copy of
 *	this software and associated documentation files (the "Software"), to deal in
 *	the Software without restriction, including without limitation the rights to use,
 *	copy, modify, merge, publish, distribute, sublicense, and/or sell copies of the
 *	Software, and to permit persons to whom the Software is furnished to do so,
 *	subject to the following conditions:
 *
 *	The above copyright notice and this permission notice shall be included in all
 *	copies or substantial portions of the Software.
 *
 *	THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED,
 *	INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A
 *	PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT
 *	HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION
 *	OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE
 *	SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
 *
 *	SPDX-License-Identifier: MIT
 *
 *********************************************************************************/

/**
 * 4.3.1 Definition of a ModelExchange section of an FMU.
 */
functions
	/**
	 * Apply defaults to return effective ModelExchange settings.
	 */
	effModelExchange: ModelExchange +> ModelExchange
	effModelExchange(me) ==
		mk_ModelExchange
		(
			me.location,
			me.modelIdentifier,
			nilBool(me.needsExecutionTool),
			nilBool(me.completedIntegratorStepNotNeeded),
			nilBool(me.canBeInstantiatedOnlyOncePerProcess),
			nilBool(me.canNotUseMemoryManagementFunctions),
			nilBool(me.canGetAndSetFMUstate),
			nilBool(me.canSerializeFMUstate),
			nilBool(me.providesDirectionalDerivative),
			nilSeq[File](me.sourceFiles)
		);

	/**
	 * Check ModelExchange values.
	 */
	isValidModelExchange: [ModelExchange] +> bool
	isValidModelExchange(me) ==
		me <> nil =>
		{
			me.sourceFiles <> nil =>
				/* @OnFail(1400, "3.3.1 ModelExchange source file names are not unique: %s",
					let files = me.sourceFiles in
						{ files(a) | a, b in set inds files &
							a <> b and files(a) = files(b) })

					The file names within the sequence of source files listed for the ModelExchange
					must be unique. This is determined by checking that the set of names is the same
					size as the length of the list of source files.
				 */
				( len me.sourceFiles = card { name | name in seq me.sourceFiles } ),

			/* @OnFail(1401, "3.3.1 ModelExchange modelIdentifer %s not valid C variable name at %s",
							me.modelIdentifier, loc2str(me.location)) */
			( me.modelIdentifier <> ""
			  and me.modelIdentifier(1) in set elems NONDIGITS )
		}
		= {true};


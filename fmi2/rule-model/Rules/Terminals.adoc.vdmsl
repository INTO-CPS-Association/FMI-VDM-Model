--
-- Document created from Terminals.adoc at Thu Jan 11 21:03:23 GMT 2024
--
functions
----
-- @DocLink("<FMI3_STANDARD>#_coordinatesystem")
validCoordinateSystem: CoordinateSystem +> bool
validCoordinateSystem(cs) == allOf
	([
		-- @OnFail("%NAME: Coordinate system has zero area at %s", loc2str(cs.location))
		( cs.x1 <> cs.x2 and cs.y1 <> cs.y2 ),

		-- @OnFail("%NAME: Coordinate system is not bottom left to top right at %s",
		-- loc2str(cs.location))
		( isBLtoTR(cs.x1, cs.y1, cs.x2, cs.y2) )
	]);
----
----
-- @DocLink("<FMI3_STANDARD>#_icon")
validIcon: Icon +> bool
validIcon(icon) ==
	-- @OnFail("%NAME: Icon has zero area at %s", loc2str(icon.location))
	( icon.x1 <> icon.x2 and icon.y1 <> icon.y2 );
----
----
-- @DocLink("<FMI3_STANDARD>#section-terminalvars")
validTerminalMemberVariables: Terminal * [FmiModelDescription] +> bool
validTerminalMemberVariables(terminal, fmd) ==
	let tmvs = terminal.terminalMemberVariable in
	tmvs <> nil and fmd <> nil => allOf
	([
		let var = lookupVariableName(tmv.variableName, fmd.modelVariables) in
			-- @OnFail("%NAME: Terminal member variable %s not declared at %s",
			-- tmv.variableName, loc2str(tmv.location))
			( var <> nil )

		| tmv in seq tmvs
	]);
----
----
-- @DocLink("<FMI3_STANDARD>#_terminal_stream_member_variable")
validTerminalStreamMemberVariables: Terminal * [FmiModelDescription] +> bool
validTerminalStreamMemberVariables(terminal, fmd) ==
	let tsmvs = terminal.terminalStreamMemberVariable in
	tsmvs <> nil and fmd <> nil =>allOf
	([
		let ivar = lookupVariableName(tsmv.inStreamVariableName, fmd.modelVariables),
			ovar = lookupVariableName(tsmv.outStreamVariableName, fmd.modelVariables)
		in
		[
			-- @OnFail("%NAME: Terminal inStreamVariableName %s not declared at %s",
			-- tsmv.inStreamVariableName, loc2str(tsmv.location))
			( ivar <> nil ),

			-- @OnFail("%NAME: Terminal outStreamVariableName %s not declared at %s",
			-- tsmv.outStreamVariableName, loc2str(tsmv.location))
			( ovar <> nil )
		]

		| tsmv in seq tsmvs
	]);
----
----
-- @DocLink("<FMI3_STANDARD>#_terminal_graphical_representation")
validTerminalGraphicalRepresentation: Terminal +> bool
validTerminalGraphicalRepresentation(terminal) ==
	let tgr = terminal.terminalGraphicalRepresentation in
	tgr <> nil => allOf
	([
		-- @OnFail("%NAME: Terminal area is zero at %s", loc2str(tgr.location))
		( tgr.x1 <> tgr.x2 and tgr.y1 <> tgr.y2 ),

		-- @OnFail("%NAME: Colour attributes must be octets (<=255) at %s",
		-- loc2str(tgr.location))
		( tgr.defaultConnectionColor <> nil =>
			forall c in seq tgr.defaultConnectionColor & c <= 255 ),

		-- @OnFail("%NAME: defaultConnectionStrokeSize must be >0 at %s",
		-- loc2str(tgr.location))
		( tgr.defaultConnectionStrokeSize <> nil =>
			tgr.defaultConnectionStrokeSize > 0 )
	]);
----

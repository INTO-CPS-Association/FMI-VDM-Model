/**
 * This file is part of the INTO-CPS toolchain.
 *
 * Copyright (c) 2017-2019, INTO-CPS Association,
 * c/o Professor Peter Gorm Larsen, Department of Engineering
 * Finlandsgade 22, 8200 Aarhus N.
 *
 * All rights reserved.
 *
 * THIS PROGRAM IS PROVIDED UNDER THE TERMS OF GPL VERSION 3 LICENSE OR
 * THIS INTO-CPS ASSOCIATION PUBLIC LICENSE VERSION 1.0.
 * ANY USE, REPRODUCTION OR DISTRIBUTION OF THIS PROGRAM CONSTITUTES
 * RECIPIENT'S ACCEPTANCE OF THE OSMC PUBLIC LICENSE OR THE GPL 
 * VERSION 3, ACCORDING TO RECIPIENTS CHOICE.
 *
 * The INTO-CPS toolchain  and the INTO-CPS Association Public License are
 * obtained from the INTO-CPS Association, either from the above address, from
 * the URLs: http://www.into-cps.org, and in the INTO-CPS toolchain distribution.
 * GNU version 3 is obtained from: http://www.gnu.org/copyleft/gpl.html.
 *
 * This program is distributed WITHOUT ANY WARRANTY; without
 * even the implied warranty of  MERCHANTABILITY or FITNESS FOR
 * A PARTICULAR PURPOSE, EXCEPT AS EXPRESSLY SET FORTH IN THE
 * BY RECIPIENT SELECTED SUBSIDIARY LICENSE CONDITIONS OF
 * THE INTO-CPS ASSOCIATION.
 *
 * See the full INTO-CPS Association Public License conditions for more details.
 */

/**
 * 2.1.5 Creation, Destruction and Logging of FMU Instances.
 */
functions
	/**
	 * Implementation of fmi3Instantiate. See API method below.
	 */
	fmi3Instantiate_Impl: fmi3String * fmi3InterfaceType * fmi3String * fmi3String *
					 fmi3CallbackFunctions * fmi3Boolean * fmi3Boolean * [fmi3CoSimulationConfiguration] +> fmi3Instance
	fmi3Instantiate_Impl(instanceName, fmuType, fmuInstantiationToken, -/*fmuResourceLocation*/,
					funcs, -/*visible*/, loggingOn, fmuCoSimulationConfiguration) ==
		let xmlModel = funcs.instanceEnvironment.xmlModel,
			eModelVariables = effectiveScalarVariables(xmlModel),
			allTags = setToSeq[fmi3ValueReference](dom getAliasRefMap(eModelVariables)),
			aliasMap : map fmi3ValueReference to nat1 = { allTags(i) |-> i | i in set inds allTags }
		in
			mk_fmi3Instance
			(
				-- The instance name
				instanceName,

				-- The FMU type
				fmuType,

				-- The initial state
				<INSTANTIATED>,

				-- The callbacks and environment passed
				funcs,
				funcs.instanceEnvironment,
				fmuCoSimulationConfiguration,

				-- The XML model
				xmlModel,

				-- The effective scalar variables, after all defaults and inherited values set
				eModelVariables,

				-- The continuous time state references
				[ u.valueReference | u in seq xmlModel.modelStructure.unknowns & u.kind = <Derivative> ],

				-- The initial unknown references, which may be an empty set
				{ u.valueReference | u in seq xmlModel.modelStructure.unknowns & u.kind = <InitialUnknown> },

				-- The solver derivative references
				[ sd | sd in seq funcs.instanceEnvironment.solverData.derivatives ],

				-- The initial setting of log categories (nil means debugging is disabled)
				if loggingOn and xmlModel.logCategories <> nil
				then { cat.name | cat in seq xmlModel.logCategories }
				else nil,

				-- The reference map of SV alias tags to variable state indexes (below)
				aliasMap,

				-- The state vector, indexed via the aliasmap, created from the effective SVs
				-- Initially either the start value given, or the default start value for the type.
				getStartValues(aliasMap, eModelVariables),

				-- The solver data vector, addressed by SolverData's vref
				[ 0.0 | - in seq funcs.instanceEnvironment.solverData.derivatives ],

				-- The eventIndicators
				[ 0.0 | - in set {1, ..., nilNat(xmlModel.attributes.numberOfEventIndicators)} ],

				-- Initially dirty, because "start" values above need a calculate call
				true,

				-- tolerance, set by fmi3SetupExperiment (defaults to xmlModel DefaultExperiment)
				nil,

				-- IsNewEventIteration, used in ME only
				false,

				-- EventInfo for ME and CS?
				mk_fmi3EventInfo(
					nil,	-- nextEventTime
					false,	-- newDiscreteStatesNeeded
					false,	-- terminateSimulation
					false,	-- nominalsOfContinuousStatesChanged
					false,	-- valuesOfContinuousStatesChanged
					false	-- nextEventTimeDefined
				),

				-- start time, set by fmi3SetupExperiment
				nil,

				-- stop time, optionally set by fmi3SetupExperiment
				nil,

				-- current time, set by fmi3SetupExperiment
				nil
			)
	pre
		instanceName <> ""
		and (fmuCoSimulationConfiguration <> nil => fmuType = <fmi3CoSimulation>)
		and let xmlModel = funcs.instanceEnvironment.xmlModel in
			fmuInstantiationToken = xmlModel.attributes.instantiationToken
			and (fmuType = <fmi3CoSimulation>  => xmlModel.coSimulation <> nil)
			and (fmuType = <fmi3ModelExchange> => xmlModel.modelExchange <> nil)
	post
		RESULT.fmuState = <INSTANTIATED>
		and RESULT.fmuType = fmuType;


	/**
	 * The fmi3Instantiate API. This performs sufficient checks to pass the precondition on the
	 * implementation function, returning nil if any checks fail.
	 */
	fmi3Instantiate: fmi3String * fmi3InterfaceType * fmi3String * fmi3String *
					 fmi3CallbackFunctions * fmi3Boolean * fmi3Boolean * [fmi3CoSimulationConfiguration] +> [fmi3Instance]
	fmi3Instantiate(instanceName, fmuType, fmuInstantiationToken, fmuResourceLocation,
					funcs, visible, loggingOn, fmuCoSimulationConfiguration) ==
		let ienv = funcs.instanceEnvironment,
			xmlModel = ienv.xmlModel
		in
			if instanceName = ""
			then let - = logInst(ienv, instanceName, "fmi3Instantiate instanceName cannot be empty") in
				nil
			elseif fmuCoSimulationConfiguration <> nil and fmuType <> <fmi3CoSimulation>
			then let - = logInst(ienv, instanceName, "fmi3Instantiate fmuCoSimulationConfiguration implies CoSimulation fmuType") in
				nil
			elseif fmuInstantiationToken <> xmlModel.attributes.instantiationToken
			then let - = logInst(ienv, instanceName, "fmi3Instantiate fmuInstantiationToken does not match XML configuration") in
				nil
			elseif fmuType = <fmi3CoSimulation> and xmlModel.coSimulation = nil
			then let - = logInst(ienv, instanceName, "CoSimulation FMU must define CoSimulation XML configuration") in
				nil
			elseif fmuType = <fmi3ModelExchange> and xmlModel.modelExchange = nil
			then let - = logInst(ienv, instanceName, "ModelExchange FMU must define ModelExchange XML configuration") in
				nil
			else
				fmi3Instantiate_Impl(instanceName, fmuType, fmuInstantiationToken, fmuResourceLocation,
									 funcs, visible, loggingOn, fmuCoSimulationConfiguration);

	/**
	 * Helper to log fmi3Instantiate errors. These cannot use the callback logger because the
	 * fmi3Instance that defines the enabled categories has not yet been created. It implicitly
	 * uses the raw implementation - ie. it is guaranteed to log.
	 */
	logInst: fmi3InstanceEnvironment * fmi3String * fmi3String +> <VOID>
	logInst(ienv, instanceName, message) ==
		fmi3Logger_Impl(ienv, instanceName, <fmi3Error>, "logAll", message);


	/**
	 * Calculate the initial vector of SV values, given their "start" attributes.
	 * Called by fmi3Instantiate and fmi3Reset.
	 */
	getStartValues: map fmi3ValueReference to nat1 * seq1 of Variable +> seq1 of fmi3Value
	getStartValues(aliasmap, eModelVariables) ==
	[
		let sv in seq eModelVariables be st sv.base.valueReference = tag in		-- Choose any alias
			if sv.start <> nil
			then sv.start	-- All aliases have the same start
			else
				-- Set default value for variable type
				if is_Real(sv) then 0.0
				else if is_Integer(sv) then 0
				else if is_Boolean(sv) then false
				else if is_String(sv) then ""
				else if is_Enumeration(sv) then 0
				else undefined

		| tag in set dom aliasmap
	];

	/**
	 * Implementation of fmi3FreeInstance. See API method below.
	 */
	fmi3FreeInstance_Impl: fmi3Instance +> <VOID>
	fmi3FreeInstance_Impl(c) ==
		<VOID>	-- Logically calls c.xmlModel.funcs.freeMemory()
	pre
		c.fmuState in set STATES_fmi3FreeInstance;

	/**
	 * The fmi3FreeInstance API. This performs sufficient checks to pass the precondition on the
	 * implementation function, returning <VOID> if any checks fail.
	 */
	fmi3FreeInstance: [fmi3Instance] +> <VOID>
	fmi3FreeInstance(c) ==
		if c = nil
		then -- The function ignores a "null" argument
			<VOID>
		elseif c.fmuState not in set STATES_fmi3FreeInstance
		then let - = logAPIError(c, "Cannot call fmi3FreeInstance in this state") in
			<VOID>
		else
			fmi3FreeInstance_Impl(c);


	/**
	 * Implementation of fmi3SetDebugLogging. See API method below.
	 */
	fmi3SetDebugLogging_Impl: fmi3Instance * fmi3Boolean * nat * seq of fmi3String +> fmi3Status * fmi3Instance
	fmi3SetDebugLogging_Impl(c, loggingOn, nCategories, categories) ==
		mk_(<fmi3OK>,
			mu(c, logCategories |->
				if loggingOn
				then
					if nCategories = 0
					then [ cat.name | cat in seq c.xmlModel.logCategories ]
					else elems categories
				else nil))
	pre
		c.fmuState in set STATES_fmi3SetDebugLogging
		and nCategories = len categories
		and card elems categories = nCategories
		and (nCategories > 0 =>
			let logcats = c.xmlModel.logCategories in
				logcats <> nil
				and forall cname in seq categories &
					exists lc in seq logcats &
						lc.name = cname)
	post
		RESULT.#2.logCategories =
			if loggingOn
			then if nCategories = 0
				then [ cat.name | cat in seq c.xmlModel.logCategories ]
				else elems categories
			else nil;

	/**
	 * The fmi3FreeInstance API. This performs sufficient checks to pass the precondition on the
	 * implementation function, returning <VOID> if any checks fail.
	 */
	fmi3SetDebugLogging: fmi3Instance * fmi3Boolean * nat * seq of fmi3String +> fmi3Status * [fmi3Instance]
	fmi3SetDebugLogging(c, loggingOn, nCategories, categories) ==
		if c.fmuState not in set STATES_fmi3SetDebugLogging
		then let - = logAPIError(c, "Cannot call fmi3SetDebugLogging in this state") in
			mk_(<fmi3Error>, nil)
		elseif nCategories <> len categories or card elems categories <> nCategories
		then let - = logAPIError(c, "nCategories does not match categories length") in
			mk_(<fmi3Error>, nil)
		else let logcats = c.xmlModel.logCategories in
			if logcats = nil
			then let - = logAPIError(c, "No log categories defined in XML configuration") in
				mk_(<fmi3Error>, nil)
			elseif exists cname in seq categories & not exists lc in seq logcats & lc.name = cname
			then let - = logAPIError(c, "Some log categories not found in XML configuration") in
				mk_(<fmi3Error>, nil)
			else
				fmi3SetDebugLogging_Impl(c, loggingOn, nCategories, categories)
	post
		RESULT.#1 <> <fmi3OK> => RESULT.#2 = nil;

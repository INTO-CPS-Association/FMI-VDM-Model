/*********************************************************************************
 *
 *	Copyright (c) 2017-2022, INTO-CPS Association,
 *	c/o Professor Peter Gorm Larsen, Department of Engineering
 *	Finlandsgade 22, 8200 Aarhus N.
 *
 *	MIT Licence:
 *
 *	Permission is hereby granted, free of charge, to any person obtaining a copy of
 *	this software and associated documentation files (the "Software"), to deal in
 *	the Software without restriction, including without limitation the rights to use,
 *	copy, modify, merge, publish, distribute, sublicense, and/or sell copies of the
 *	Software, and to permit persons to whom the Software is furnished to do so,
 *	subject to the following conditions:
 *
 *	The above copyright notice and this permission notice shall be included in all
 *	copies or substantial portions of the Software.
 *
 *	THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED,
 *	INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A
 *	PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT
 *	HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION
 *	OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE
 *	SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
 *
 *	SPDX-License-Identifier: MIT
 *
 *********************************************************************************/
types
	FmiBuildDescription' = FmiBuildDescription
	inv fbd == allOf
	([
		apply[BuildConfiguration](inv_BuildConfiguration', fbd.buildConfiguration),
		inv_Annotations'(fbd.annotations)
	]);

	BuildConfiguration' = BuildConfiguration
	inv bd == allOf
	([
		apply[SourceFileSet](inv_SourceFileSet', bd.sourceFileSet),
		apply[Library](inv_Library', bd.library),
		inv_Annotations'(bd.annotations)
	]);

	SourceFileSet' = SourceFileSet
	inv sfs == allOf
	([
		apply[SourceFile](inv_SourceFile', sfs.sourceFile),
		apply[PreprocessorDefinition](inv_PreprocessorDefinition', sfs.preprocessorDefinition),
		apply[IncludeDirectory](inv_IncludeDirectory', sfs.includeDirectory),
		inv_Annotations'(sfs.annotations),

		rule("validCompilerOptions", validCompilerOptions(sfs)),
		rule("validSourceFiles", validSourceFiles(sfs.sourceFile)),
		rule("validPreprocessorDefinitions", validPreprocessorDefinitions(sfs.preprocessorDefinition)),
		rule("validIncludeDirectories", validIncludeDirectories(sfs.includeDirectory))
	]);

	SourceFile' = SourceFile
	inv sf == allOf
	([
		inv_Annotations'(sf.annotations)
	]);

	PreprocessorDefinition' = PreprocessorDefinition
	inv pd == allOf
	([
		inv_Annotations'(pd.annotations)
	]);

	IncludeDirectory' = IncludeDirectory
	inv id == allOf
	([
		inv_Annotations'(id.annotations)
	]);

	Library' = Library
	inv lib == allOf
	([
		inv_Annotations'(lib.annotations)
	]);

functions
	validCompilerOptions: SourceFileSet +> bool
	validCompilerOptions(sfs) ==
		-- @OnFail("validCompilerOptions: Compiler options set without compiler at %s", loc2str(sfs.location))
		( sfs.compilerOptions <> nil => sfs.compiler <> nil );

	validSourceFiles: seq1 of SourceFile +> bool
	validSourceFiles(files) ==
		let names = [ sf.name | sf in seq files ] in
			/* @OnFail("validSourceFiles: SourceFileSet has duplicate names: %s",
				{ files(a) | a, b in set inds names & a <> b and files(a) = files(b) })	*/
			( card elems names = len names );

	validPreprocessorDefinitions: [seq1 of PreprocessorDefinition] +> bool
	validPreprocessorDefinitions(defs) ==
		defs <> nil =>
			let names = [ d.name | d in seq defs ] in
				/* @OnFail("validPreprocessorDefinitions: PreprocessorDefinitions has duplicate names: %s",
					{ names(a) | a, b in set inds names & a <> b and names(a) = names(b) }) */
				( card elems names = len names );

	validIncludeDirectories: [seq1 of IncludeDirectory] +> bool
	validIncludeDirectories(incs) ==
		incs <> nil =>
			let names = [ d.name | d in seq incs ] in
				/* @OnFail("validIncludeDirectories: IncludeDirectories has duplicate names: %s",
					{ names(a) | a, b in set inds names & a <> b and names(a) = names(b) }) */
				( card elems names = len names );

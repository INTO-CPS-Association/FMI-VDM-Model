/*********************************************************************************
 *
 *	Copyright (c) 2017-2022, INTO-CPS Association,
 *	c/o Professor Peter Gorm Larsen, Department of Engineering
 *	Finlandsgade 22, 8200 Aarhus N.
 *
 *	MIT Licence:
 *
 *	Permission is hereby granted, free of charge, to any person obtaining a copy of
 *	this software and associated documentation files (the "Software"), to deal in
 *	the Software without restriction, including without limitation the rights to use,
 *	copy, modify, merge, publish, distribute, sublicense, and/or sell copies of the
 *	Software, and to permit persons to whom the Software is furnished to do so,
 *	subject to the following conditions:
 *
 *	The above copyright notice and this permission notice shall be included in all
 *	copies or substantial portions of the Software.
 *
 *	THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED,
 *	INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A
 *	PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT
 *	HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION
 *	OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE
 *	SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
 *
 *	SPDX-License-Identifier: MIT
 *
 *********************************************************************************/
types
	UnitDefinitions' = [UnitDefinitions]
	inv units == units <> nil => allOf
	([
		apply[Unit](inv_Unit', units),

		rule("validUnitNames", validUnitNames(units))
	]);

	Unit' = Unit
	inv unit == allOf
	([
		apply[DisplayUnit](inv_DisplayUnit', unit.displayUnit),
		inv_BaseUnit'(unit.baseUnit),
		inv_Annotations'(unit.annotations),

		rule("validDisplayUnitNames", validDisplayUnitNames(unit))
	]);

	BaseUnit' = [BaseUnit]
	inv bunit == bunit <> nil => allOf
	(
		let - = effectiveBaseUnit(bunit) in
		[
			true	-- No rules for BaseUnit?
		]
	);

	DisplayUnit' = DisplayUnit
	inv dunit == allOf
	(
		let edu = effectiveDisplayUnit(dunit) in
		[
			inv_Annotations'(edu.annotations),

			rule("validInverseAndOffset", validInverseAndOffset(edu))
		]
	);

functions
	effectiveBaseUnit: BaseUnit +> BaseUnit
	effectiveBaseUnit(bunit) ==
		mk_BaseUnit
		(
			bunit.location,
			if bunit.kg <> nil then bunit.kg else 0,
			if bunit.m <> nil then bunit.m else 0,
			if bunit.s <> nil then bunit.s else 0,
			if bunit.a <> nil then bunit.a else 0,
			if bunit.k <> nil then bunit.k else 0,
			if bunit.mol <> nil then bunit.mol else 0,
			if bunit.cd <> nil then bunit.cd else 0,
			if bunit.rad <> nil then bunit.rad else 0,
			if bunit.factor <> nil then bunit.factor else 1,
			if bunit.offset <> nil then bunit.offset else 0
		);

	effectiveDisplayUnit: DisplayUnit +> DisplayUnit
	effectiveDisplayUnit(dunit) ==
		mk_DisplayUnit
		(
			dunit.location,
			dunit.name,
			if dunit.factor <> nil then dunit.factor else 1,
			if dunit.offset <> nil then dunit.offset else 0,
			dunit.$inverse = true,
			dunit.annotations
		);
	
	validUnitNames: UnitDefinitions +> bool
	validUnitNames(units) ==
		-- @OnFail("validUnitNames: Unit names must be unique")
		( let names = [ u.name | u in seq units ] in
			len names = card elems names );

	validDisplayUnitNames: Unit +> bool
	validDisplayUnitNames(unit) ==
		-- @OnFail("validDisplayUnitNames: DisplayUnit names must be unique within %s at %s", unit.name, loc2str(unit.location))
		( unit.displayUnit <> nil =>
			let names = [ u.name | u in seq unit.displayUnit ] in
				len names = card elems names );

	validInverseAndOffset: DisplayUnit +> bool
	validInverseAndOffset(dunit) ==
		-- @OnFail("validInverseAndOffset: inverse = true is only allowed if offset = 0 at %s", loc2str(dunit.location))
		( dunit.$inverse = true => dunit.offset = 0 )
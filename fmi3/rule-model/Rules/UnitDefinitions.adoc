// This adds the "functions" section header for VDM only
ifdef::hidden[]
// {vdm}
functions
// {vdm}
endif::[]

==== A.{section}.{counter:subsection} UnitDefinition Rules
:!typerule:
===== A.{section}.{subsection}.{counter:typerule} Rule: validUnitNames
[[validUnitNames]]
// {vdm}
----
validUnitNames: UnitDefinitions +> bool
validUnitNames(units) ==
	-- @OnFail("validUnitNames: Unit names must be unique")
	( let names = [ u.name | u in seq units ] in
		len names = card elems names );
----
// {vdm}
- See <<_physical_units>>.
- See <<uniqueNameAttribute>>.

===== A.{section}.{subsection}.{counter:typerule} Rule: validDisplayUnitNames
[[validDisplayUnitNames]]
// {vdm}
----
validDisplayUnitNames: Unit +> bool
validDisplayUnitNames(unit) ==
	-- @OnFail("validDisplayUnitNames: DisplayUnit names must be unique within %s at %s", unit.name, loc2str(unit.location))
	( unit.displayUnit <> nil =>
		let names = [ u.name | u in seq unit.displayUnit ] in
			len names = card elems names );
----
// {vdm}
- See <<figure-schema-DisplayUnit>>.
- See <<uniqueNameAttribute>>.

===== A.{section}.{subsection}.{counter:typerule} Rule: validInverseAndOffset
[[validInverseAndOffset]]
// {vdm}
----
validInverseAndOffset: DisplayUnit +> bool
validInverseAndOffset(dunit) ==
	-- @OnFail("validInverseAndOffset: inverse is only allowed if offset = 0 at %s", loc2str(dunit.location))
	( dunit.$inverse => dunit.offset = 0 )
----
// {vdm}
See <<figure-schema-DisplayUnit>>.

// This adds the docrefs for VDM only
ifdef::hidden[]
// {vdm}
values
	UnitDefinitions_refs : ReferenceMap =
	{
		"validUnitNames" |->
		[
			"fmi-standard/docs/index.html#_physical_units",
			"fmi-standard/docs/index.html#uniqueNameAttribute"
		],

		"validDisplayUnitNames" |->
		[
			"fmi-standard/docs/index.html#figure-schema-DisplayUnit",
			"fmi-standard/docs/index.html#uniqueNameAttribute"
		],

		"validInverseAndOffset" |->
		[
			"fmi-standard/docs/index.html#figure-schema-DisplayUnit"
		]
	};
// {vdm}
endif::[]






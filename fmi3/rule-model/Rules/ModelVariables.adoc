// This adds the "functions" section header for VDM only
ifdef::hidden[]
// {vdm}
functions
// {vdm}
endif::[]

==== A.{section}.{counter:subsection} ModelVariables Rules
:!typerule:
===== A.{section}.{subsection}.{counter:typerule} Rule: validValueReferences
[[validValueReferences]]
// {vdm}
----
validValueReferences: ModelVariables +> bool
validValueReferences(evs) ==
	let refs = [ ev.valueReference | ev in seq evs ] in
		/* @OnFail("validValueReferences: ModelVariable valueReferences must be unique: %s",
			{ vr | vr in seq refs & len [ v2 | v2 in seq refs & vr = v2 ] > 1 }) */
		( len refs = card elems refs );	
----
// {vdm}
See <<_platform_dependent_definitions>>.

===== A.{section}.{subsection}.{counter:typerule} Rule: validAliasNames
[[validAliasNames]]
// {vdm}
----
validAliasNames: ModelVariables +> bool
validAliasNames(evs) ==
	let aliases = conc [ [ a.name | a in seq ev.alias ] | ev in seq evs & ev.alias <> nil ],
		names = [ ev.name | ev in seq evs ] ^ aliases
	in
		-- @OnFail("validAliasNames: ModelVariable names and aliases must be unique")
		( len names = card elems names );
----
// {vdm}
See <<_alias_variables>>.

===== A.{section}.{subsection}.{counter:typerule} Rule: validIndependentVariable
[[validIndependentVariable]]
// {vdm}
----
validIndependentVariable: ModelVariables +> bool
validIndependentVariable(evs) ==
		/* @OnFail("validIndependentVariable: Variables define more than one independent variable: %s",
			{ sv.name | sv in seq evs & sv.causality = <independent> }) */
		( card { sv | sv in seq evs & sv.causality = <independent> } <= 1 );
----
// {vdm}
See <<ModelVariables>>.

===== A.{section}.{subsection}.{counter:typerule} Rule: validPreviousVariables
[[validPreviousVariables]]
// {vdm}
----
validPreviousVariables: ModelVariables +> bool
validPreviousVariables(evs) ==
	let plist =  [ sv.previous | sv in seq evs & not is_Clock(sv) and sv.previous <> nil ]
	in
		/* @OnFail("validPreviousVariables: Variable previous fields are not unique: %s",
			{ evs(a).name | a, b in set inds evs & not is_Clock(a) and not is_Clock(b) and
				a <> b and evs(a).previous <> nil and evs(a).previous = evs(b).previous } ) */
		( card elems plist = len plist );
----
// {vdm}
See <<ModelVariables>>.

===== A.{section}.{subsection}.{counter:typerule} Rule: validVariableDimensions
[[validVariableDimensions]]
// {vdm}
----
validVariableDimensions: ModelVariables +> bool
validVariableDimensions(evs) ==
	allOf(conc
	[
		[
			-- @OnFail("validVariableDimensions: Dimension of %s cannot reference itself", ev.name)
			( ev.dimension <> nil =>
				forall dim in seq ev.dimension &
					dim.valueReference <> nil =>
						dim.valueReference <> ev.valueReference ),

			-- @OnFail("validVariableDimensions: Dimension of %s, valueReference is not Integer, constant or structuralParameter, with start", ev.name)
			( ev.dimension <> nil =>
				forall dim in seq ev.dimension &
					dim.valueReference <> nil =>
						exists r in seq evs &
							r.valueReference = dim.valueReference
							and is_IntegerKind(kindOf(r))
							and ( r.causality = <structuralParameter> or r.variability = <constant> )
							and r.start <> nil and len r.start = 1 and r.start(1) >= 0 ),

			-- @OnFail("validVariableDimensions: Structural parameter %s cannot have Dimension", ev.name)
			( ev.dimension <> nil =>
				ev.causality <> <structuralParameter> )
		]

		| ev in seq evs & not is_Clock(ev)	-- Clocks have no dimensions
	]);
----
// {vdm}
See <<ModelVariables>>.

===== A.{section}.{subsection}.{counter:typerule} Rule: validStartAndVRef
[[validStartAndVRef]]
// {vdm}
----
validStartAndVRef: Dimension +> bool
validStartAndVRef(d) ==
	-- @OnFail("validStartAndVRef: Dimension cannot define start and valueReference at %s", loc2str(d.location))
	( d.start = nil <=> d.valueReference <> nil );
----
// {vdm}
See <<ModelVariables>>.

===== A.{section}.{subsection}.{counter:typerule} Rule: validAliasDisplayUnits
[[validAliasDisplayUnits]]
// {vdm}
----
validAliasDisplayUnits: ModelVariables +> bool
validAliasDisplayUnits(evs) ==
	allOf(conc
	[
		[
			-- @OnFail("validAliasDisplayUnits: Alias displayUnit requires Float unit definition at %s", loc2str(a.location))
			( a.displayUnit <> nil => isFloat(ev) and ev.unit <> nil )
			
			| a in seq ev.alias
		]

		| ev in seq evs & ev.alias <> nil	
	]);
----
// {vdm}
See <<ModelVariables>>.


// This adds the docrefs for VDM only
ifdef::hidden[]
// {vdm}
values
	ModelVariables_refs : ReferenceMap =
	{
		"validValueReferences" |->
		[
			"fmi-standard/docs/index.html#_platform_dependent_definitions"
		],

		"validAliasNames" |->
		[
			"fmi-standard/docs/index.html#_alias_variables"
		],

		"validIndependentVariable" |->
		[
			"fmi-standard/docs/index.html#ModelVariables"
		],

		"validPreviousVariables" |->
		[
			"fmi-standard/docs/index.html#ModelVariables"
		],

		"validVariableDimensions" |->
		[
			"fmi-standard/docs/index.html#ModelVariables"
		],

		"validStartAndVRef" |->
		[
			"fmi-standard/docs/index.html#ModelVariables"
		],

		"validAliasDisplayUnits" |->
		[
			"fmi-standard/docs/index.html#ModelVariables"
		]
	};
// {vdm}
endif::[]











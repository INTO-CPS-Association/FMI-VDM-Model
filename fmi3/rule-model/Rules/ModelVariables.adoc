// This adds the "functions" section header for VDM only
ifdef::hidden[]
// {vdm}
functions
// {vdm}
endif::[]

==== A.{section}.{counter:subsection} ModelVariables Rules
:!typerule:
===== A.{section}.{subsection}.{counter:typerule} Effective Model Variables
Note that the rules in this section are checking the "raw" ModelVariable values, before any field values are defaulted or inherited from a _declaredType_. This is in contrast to the ModelVariables passed to the rules in the <<FmiModelDescription>> section.

===== A.{section}.{subsection}.{counter:typerule} Rule: validValueReferences
[[validValueReferences]]
// {vdm}
----
validValueReferences: ModelVariables +> bool
validValueReferences(mvs) ==
	let refs = [ mv.valueReference | mv in seq mvs ] in
		-- @OnFail("%NAME: ModelVariable valueReferences must be unique: %s",
		--	{ vr | vr in seq refs & len [ v2 | v2 in seq refs & vr = v2 ] > 1 })
		( len refs = card elems refs );	
----
// {vdm}
See <<_platform_dependent_definitions>>.

===== A.{section}.{subsection}.{counter:typerule} Rule: validAliasNames
[[validAliasNames]]
// {vdm}
----
validAliasNames: ModelVariables +> bool
validAliasNames(mvs) ==
	let aliases = conc [ [ a.name | a in seq mv.alias ] | mv in seq mvs & mv.alias <> nil ],
		names = [ mv.name | mv in seq mvs ] ^ aliases
	in
		-- @OnFail("%NAME: ModelVariable names and aliases must be unique")
		( len names = card elems names );
----
// {vdm}
See <<_alias_variables>>.

===== A.{section}.{subsection}.{counter:typerule} Rule: validIndependentVariable
[[validIndependentVariable]]
// {vdm}
----
validIndependentVariable: ModelVariables +> bool
validIndependentVariable(mvs) ==
	-- @OnFail("%NAME: Variables define more than one independent variable: %s",
	-- { mv.name | mv in seq mvs & mv.causality = <independent> })
	( card { mv | mv in seq mvs & mv.causality = <independent> } <= 1 );
----
// {vdm}
See <<ModelVariables>>.

===== A.{section}.{subsection}.{counter:typerule} Rule: validPreviousVariables
[[validPreviousVariables]]
// {vdm}
----
validPreviousVariables: ModelVariables +> bool
validPreviousVariables(mvs) ==
	let plist =  [ mv.previous | mv in seq mvs & not is_Clock(mv) and mv.previous <> nil ]
	in
		-- @OnFail("%NAME: Variable previous fields are not unique: %s",
		-- { mvs(a).name | a, b in set inds mvs & not is_Clock(a) and not is_Clock(b) and
		--   a <> b and mvs(a).previous <> nil and mvs(a).previous = mvs(b).previous } )
		( card elems plist = len plist );
----
// {vdm}
See <<ModelVariables>>.

===== A.{section}.{subsection}.{counter:typerule} Rule: validVariableDimensions
[[validVariableDimensions]]
// {vdm}
----
validVariableDimensions: ModelVariables +> bool
validVariableDimensions(mvs) ==
	allOf(conc
	[
		[
			-- @OnFail("%NAME: Dimension of %s cannot reference itself", mv.name)
			( mv.dimension <> nil =>
				forall dim in seq mv.dimension &
					dim.valueReference <> nil =>
						dim.valueReference <> mv.valueReference ),

			-- @OnFail("%NAME: Dimension of %s, %s", mv.name,
			-- "valueReference is not Integer, constant or structuralParameter, with start")
			( mv.dimension <> nil =>
				forall dim in seq mv.dimension &
					dim.valueReference <> nil =>
						exists r in seq mvs &
							r.valueReference = dim.valueReference
							and is_IntegerKind(kindOf(r))
							and ( r.causality = <structuralParameter> or r.variability = <constant> )
							and r.start <> nil and len r.start = 1 and r.start(1) >= 0 ),

			-- @OnFail("%NAME: Structural parameter %s cannot have Dimension", mv.name)
			( mv.dimension <> nil =>
				mv.causality <> <structuralParameter> )
		]

		| mv in seq mvs & not is_Clock(mv)	-- Clocks have no dimensions
	]);
----
// {vdm}
See <<ModelVariables>>.

===== A.{section}.{subsection}.{counter:typerule} Rule: validStartAndVRef
[[validStartAndVRef]]
// {vdm}
----
validStartAndVRef: Dimension +> bool
validStartAndVRef(d) ==
	-- @OnFail("%NAME: Dimension cannot define start and valueReference at %s", loc2str(d.location))
	( d.start = nil <=> d.valueReference <> nil );
----
// {vdm}
See <<ModelVariables>>.

===== A.{section}.{subsection}.{counter:typerule} Rule: validAliasDisplayUnits
[[validAliasDisplayUnits]]
// {vdm}
----
validAliasDisplayUnits: ModelVariables +> bool
validAliasDisplayUnits(mvs) ==
	allOf
	([
		[
			-- @OnFail("%NAME: Alias displayUnit requires Float with unit definition at %s",
			-- loc2str(a.location))
			( a.displayUnit <> nil => isFloat(mv) and mv.unit <> nil )
			
			| a in seq mv.alias
		]

		| mv in seq mvs & mv.alias <> nil	
	]);
----
// {vdm}
See <<ModelVariables>>.


// This adds the docrefs for VDM only
ifdef::hidden[]
// {vdm}
values
	ModelVariables_refs : ReferenceMap =
	{
		"validValueReferences" |->
		[
			"<FMI3_STANDARD>#_platform_dependent_definitions"
		],

		"validAliasNames" |->
		[
			"<FMI3_STANDARD>#_alias_variables"
		],

		"validIndependentVariable" |->
		[
			"<FMI3_STANDARD>#ModelVariables"
		],

		"validPreviousVariables" |->
		[
			"<FMI3_STANDARD>#ModelVariables"
		],

		"validVariableDimensions" |->
		[
			"<FMI3_STANDARD>#ModelVariables"
		],

		"validStartAndVRef" |->
		[
			"<FMI3_STANDARD>#ModelVariables"
		],

		"validAliasDisplayUnits" |->
		[
			"<FMI3_STANDARD>#ModelVariables"
		]
	};
// {vdm}
endif::[]











/**
 * This file is part of the INTO-CPS toolchain.
 *
 * Copyright (c) 2017-2019, INTO-CPS Association,
 * c/o Professor Peter Gorm Larsen, Department of Engineering
 * Finlandsgade 22, 8200 Aarhus N.
 *
 * All rights reserved.
 *
 * THIS PROGRAM IS PROVIDED UNDER THE TERMS OF GPL VERSION 3 LICENSE OR
 * THIS INTO-CPS ASSOCIATION PUBLIC LICENSE VERSION 1.0.
 * ANY USE, REPRODUCTION OR DISTRIBUTION OF THIS PROGRAM CONSTITUTES
 * RECIPIENT'S ACCEPTANCE OF THE OSMC PUBLIC LICENSE OR THE GPL 
 * VERSION 3, ACCORDING TO RECIPIENTS CHOICE.
 *
 * The INTO-CPS toolchain  and the INTO-CPS Association Public License are
 * obtained from the INTO-CPS Association, either from the above address, from
 * the URLs: http://www.into-cps.org, and in the INTO-CPS toolchain distribution.
 * GNU version 3 is obtained from: http://www.gnu.org/copyleft/gpl.html.
 *
 * This program is distributed WITHOUT ANY WARRANTY; without
 * even the implied warranty of  MERCHANTABILITY or FITNESS FOR
 * A PARTICULAR PURPOSE, EXCEPT AS EXPRESSLY SET FORTH IN THE
 * BY RECIPIENT SELECTED SUBSIDIARY LICENSE CONDITIONS OF
 * THE INTO-CPS ASSOCIATION.
 *
 * See the full INTO-CPS Association Public License conditions for more details.
 */
 types
	/**
	 * A point in superdense time is represented by a real quantity and a whole
	 * number instant "within" that time.
	 */
	Time ::
		r	: real
		i	: nat
	inv t ==
		t.r >= 0	-- Times must be positive
	ord a < b ==
		(a.r < b.r) or (a.r = b.r and a.i < b.i);	-- NB using real equality!

	/**
	 * Possible "interval" values for a Clock variable
	 */
	Interval = <constant> | <fixed> | <calculated> | <tunable> | <changing> | <countdown> | <triggered>;

	/**
	 * The different types of actions
	 */
	ActionType = <get> | <set> | <step> | <getC> | <setC> ;  

	/*
		An FMU action has a type and some infomration describe the paramters of the actions
	*/
	Action ::
		actionType : ActionType
		fmu : FMU
		port : Ref;
	

	/**
	 * A variable or clock reference. These are unique within an FMU.
	 */
	Ref = nat;

	/**
	 * A convenient way to name FMUs.
	 */
	Name = seq1 of char;

	/**
	 * A reference to a connection point in a specific named FMU.
	 */
	FMURef ::
		name	: Name
		ref		: Ref;

	/**
	 * The current known FMU variable values. These are usually used by FMUs, but note that
	 * Clocks can produce temporary environments that are not committed until a step.
	 */
	Environment = map Ref to Value;

	/**
	 * The value that a clock or variable can have. 
	 * It contains a time and a value
	 */
	Value ::
		value : real | int | bool
		time : Time;

	/**
	 * An Equation is used to modify the values of an FMU in order to compute a new
	 * step in time. A clock defines a set of equations to activate.
	 */
	Equation = Environment -> Environment;

	/**
	 * A variable is identified by a Ref (within an FMU).
	 */
	Variable ::
		ref			: Ref
        time        : Time
        causality	: <input> | <output>
        type		: <discrete> | <continous>
		clocks		: set of Ref -- clocks that the variable can active
        dependsOn   : set of Ref -- feedthrough only relevant for outputs
        contract    : <none> | <reactive> | <delayed>   
	inv var ==
		(var.causality = <output> => var.contract = <none>) -- input contract only relevant for inputs
		and var.causality = <input> => var.dependsOn = {} ;


	/**
	 * A clock is identified by a Ref (within an FMU) and activates a set of equations.
	 */
	Clock ::
		ref			: Ref
		type		: <input> | <output>
		interval	: Interval
        timeToTick  : Time
		dependsOn 	: set of Ref -- The clocks that can trigger an clock
		equations	: set of Ref -- variables the clock activates
	inv clock ==
		(clock.type = <output> => clock.interval = <triggered>)	-- An output clock must be of type triggered 
		and (clock.type = <input> => clock.dependsOn = {}); -- Only output clocks are subject to feedthrough

	/**
	 * An FMU is comprised of a set of clocks and variables, plus an environment that holds
	 * the current values for both. The references must be unique and the references within
	 * the environment must all refer to known variables.
	 */
	FMU ::
		name				: Name				-- m
		clocks				: set of Clock		-- Ucm + Ycm
		inputs				: set of Variable	-- Um 
        outputs             : set of Variable   -- Ym
		mode				: <INIT> | <DONE> | <STEP> | <EVENT> -- Mode of the FMU
		time				: Time				-- time is per FMU
        maxStep             : real				-- How far can the FMU step
		env					: Environment
		activeClocks		: set of Ref		-- 
		activeEquations		: set of Equation	-- I think this needs to be a set of reference
	inv fmu ==
		let vars = fmu.inputs union fmu.outputs in
		let crefs = { c.ref | c in set fmu.clocks },
			vrefs = { v.ref | v in set vars},
			refs  = crefs union vrefs
		in
            -- The maximal step cannot be negative
            fmu.maxStep >= 0.0
			-- All clock and variable references are unique
			and card refs = card fmu.clocks + card vars
			-- All references in the environment are defined
			and (dom fmu.env subset refs)
			-- All active clocks are defined
			and (fmu.activeClocks subset crefs)
			-- A clock can only be active when the fmu is in event mode
			and (fmu.mode <> <EVENT> => fmu.activeClocks = {})
			-- An equation can only be active when the fmu is in event mode
			and (fmu.mode <> <EVENT> => fmu.activeEquations = {})
			-- All variables' clocks are defined
			and (forall var in set vars & var.clocks subset crefs)
			-- An active variable is active by a clock Todo
			--and (forall (cref |-> val) in set fmu.activeEquations 
			---		& exists c in set fmu.clocks & c.ref in set fmu.activeClocks and equation in set c.equations)
			-- All scheduled clocks are defined
			;

	/**
	 * A machine is a collection of FMUs, plus a connection map between them. The connection
	 * sources must be FMU outputs and the destinations must be FMU inputs.
	 */
	Machine ::
		fmus				: map Name to FMU
		connections			: map FMURef to FMURef
		clockConnections	: map FMURef to FMURef
	inv m ==
		(forall srcRef in set dom m.connections &
			-- Every connection's source is a known FMU
			srcRef.name in set dom m.fmus
			-- Every connection's source is an output from its FMU
			and 
			let srcFMU = m.fmus(srcRef.name),
				trgRef = m.connections(srcRef) in 
				-- Every connection's destination is an input of its FMU
				(exists v in set srcFMU.outputs & srcRef.ref = v.ref)
				-- Every connection's destination is a known FMU
				and trgRef.name in set dom m.fmus
				and let trgFMU = m.fmus(trgRef.name) in 
					(exists v in set trgFMU.inputs & trgRef.ref = v.ref)
				and 
				let output = derefOutput(srcFMU, srcRef.ref),
					input = derefInput(trgFMU, trgRef.ref) in
				output.type = input.type
		)
		and
		(forall fmuref in set dom m.clockConnections &
			-- Every connection's source is a known FMU
			fmuref.name in set dom m.fmus
			-- Every connection's source is an output from its FMU
			and let fmu = m.fmus(fmuref.name) in
				exists v in set fmu.clocks & fmuref.ref = v.ref and v.type = <output>)
		and
		(forall fmuref in set rng m.clockConnections &
			-- Every connection's destination is a known FMU
			fmuref.name in set dom m.fmus
			-- Every connection's destination is an input of its FMU
			and let fmu = m.fmus(fmuref.name) in
				exists v in set fmu.clocks & fmuref.ref = v.ref and v.type = <input>)
		and dom m.clockConnections inter dom m.connections = {}
		and rng m.clockConnections inter rng m.connections = {};
  
	/*
		The Importer runs the simulation
		It keeps track of the input and output to adapt to the behavior of the scenario
		It has a simulation time and values to exchange between the FMUs
	*/
	Importer :: 
		scenario : Machine
        schedule : map FMURef to real -- the time the clock should tick
		activeClocks : set of FMURef -- active clocks
		readyClocks : set of FMURef -- clocks that can be activated
		inactiveClocks : set of FMURef -- inactive clocks
		fmusWithEvent	: set of Name -- FMUs that triggered a state event
		relevantOutputClocks : set of FMURef -- All Output clocks the Importer must query since their state can have changed
		relevantInputClocks  : set of FMURef -- All Input clocks the Importer must update because their output changed 
		activeEquations : set of FMURef -- All active equations 
		calculatedEquations : set of FMURef -- The equations the Importer has calculated
		readyEquations : set of FMURef -- The equations that can be activated
		time : Time -- Simulation Time
		endtime : Time
		stepSize : real -- Planned step size
		valueMap : map FMURef to Value
	inv imp ==
		let fmus = imp.scenario.fmus in
		let inputclocks = dunion {createFMURefs(fmu, {clock.ref | clock in set fmu.clocks & clock.type = <input> }, {}) | fmu in set rng fmus } in
		let outputclocks = dunion {createFMURefs(fmu, {clock.ref | clock in set fmu.clocks & clock.type = <output> }, {}) | fmu in set rng fmus } in
		let clocks = inputclocks union outputclocks
		in
		imp.activeClocks union imp.inactiveClocks = clocks  -- a clock is either inactive or active
		and imp.activeClocks inter imp.inactiveClocks = {} -- a clock cannot be active and inactive
		and imp.activeClocks inter imp.readyClocks = {} -- an active clock can not be ready
		and imp.activeClocks = dunion {createFMURefs(fmu, fmu.activeClocks, {}) | fmu in set rng fmus }
		and imp.fmusWithEvent subset dom fmus -- all fmus that wishes to enter event mode is in the scenario
		and imp.relevantInputClocks subset inputclocks -- a relevant input clock needs to be an input clock
		and imp.relevantOutputClocks subset outputclocks -- a relevant output clock needs to be an output clock
		and imp.relevantInputClocks inter imp.relevantOutputClocks = {}
		--and imp.activeEquations = dunion {fmu.activeEquations | fmu in set rng fmus} -- Equations are actived by the clocks
		and dom imp.schedule = dunion {createFMURefs(fmu, {clock.ref | clock in set fmu.clocks & clock.type = <input> and clock.interval <> <triggered>}, {}) | fmu in set rng fmus }-- We only have time based clocks in the schedule
		---and imp.activeEquations = {c.equations | c in set clocks & c.ref in set imp.activeClocks} -- Invariant for the paper TYPE MISMATCH
		and imp.endtime >= imp.time -- The simulation cannot not proceed once the endtime is reached
		and imp.activeEquations inter imp.readyEquations = {}
		and imp.readyEquations inter imp.calculatedEquations = {}
		;

functions
	/**
	 * These functions are total, because the error conditions that you would expect
	 * on an API are modelled by preconditions or type invariants.
	 */

	set_m: FMU * Ref * Value +> FMU
	set_m(fmu, ref, val) ==
		mu(fmu, env |-> fmu.env ++ { ref |-> val })
		-- and calculate equations?
	pre preSet(fmu, ref);

	get_m: FMU * Ref +> FMU * Value
	get_m(fmu, ref) ==
		-- calculate equations?
		mk_(fmu, fmu.env(ref))
	pre preGet(fmu, ref);

	set_cm: FMU * Ref * bool +> FMU
	set_cm(fmu, ref, val) ==
		mu(fmu,
			env				|-> fmu.env ++ {ref |-> mk_Value(val, fmu.time)},
			activeClocks	|-> if val then fmu.activeClocks union {ref} else fmu.activeClocks \ {ref})
	pre preSetC(fmu, ref, val)
	post val <=> ref in set fmu.activeClocks;

	get_cm: FMU * Ref +> FMU * Value
	get_cm(fmu, ref) ==
		mk_(fmu, fmu.env(ref))
	pre preGetC(fmu, ref);

	step_tm: FMU * real +> FMU * real * bool
	step_tm(fmu, step) ==
        if step >= fmu.maxStep then mk_(fmu, fmu.maxStep, true) -- We should at it to the set of FMUs that wants to enter event mode
        else mk_(fmu, step, false)
	pre preStepT(fmu, step)
	post RESULT.#2 <= step -- We cannot take a step larger than what the Importer requested
        and RESULT.#2 <= fmu.maxStep; -- We cannot take a step larger than what the FMU is capable of
		
	step_e: FMU +> FMU * bool
	step_e(fmu) ==
		is not yet specified
	pre preStepE(fmu)
	post RESULT.#1.mode = <EVENT> 		-- FMU remains in event mode - it always does the Importer should bring it out
        and RESULT.#1.time.i = fmu.time.i + 1 -- The Integer component of the FMU should be increased
		and RESULT.#1.activeClocks = {} ; -- All clocks get disabled when we step the FMU

	/*
	 * 
	 */
	next_tm: FMU * Ref +> real | <NaN>
	next_tm(fmu, ref) ==
		is not yet specified
	pre exists v in set fmu.clocks & v.ref = ref
		and v.interval in set { <tunable>, <changing>, <countdown> };

	/**
	 * Update the discrete state of all FMUs.
	 */
	updateDiscreteState: Importer * set of Name -> Importer
	updateDiscreteState(I,fmus) ==
		let M = I.scenario in
		let res = {fmu |-> step_e(M.fmus(fmu)) | fmu in set dom M.fmus & fmu in set fmus} in 
		let fmuMap = { fmu |-> res(fmu).#1 | fmu in set dom res}, 
			eventFMUs = { fmu | fmu in set dom res & res(fmu).#2 = true} -- fmus that triggered an event
		in
		mu(I, 
			scenario |-> mu(I.scenario, fmus |-> fmuMap),
			fmusWithEvent |-> eventFMUs
		)
	pre (forall m' in set rng I.scenario.fmus & m'.name in set fmus and m'.mode = <EVENT>)
		and card {m'.time | m' in set rng I.scenario.fmus & m'.name in set fmus} = 1
	post (forall m' in set rng I.scenario.fmus & m'.name in set fmus and m'.mode = <EVENT>) 
		and card {m'.time | m' in set rng I.scenario.fmus & m'.name in set fmus} = 1;
	/**
	 * Dereference a clock in a given FMU.
	 */
	derefClock: FMU * Ref -> Clock
	derefClock(fmu, ref) ==
		iota c in set fmu.clocks & c.ref = ref
	pre exists c in set fmu.clocks & c.ref = ref;

	/**
	 * Dereference an input in a given FMU.
	 */
	derefInput: FMU * Ref -> Variable
	derefInput(fmu, ref) ==
		iota c in set fmu.inputs & c.ref = ref
	pre exists c in set fmu.inputs & c.ref = ref;

	/**
	 * Dereference an output in a given FMU.
	 */
	derefOutput: FMU * Ref -> Variable
	derefOutput(fmu, ref) ==
		iota c in set fmu.outputs & c.ref = ref
	pre exists c in set fmu.outputs & c.ref = ref;

	/**
	 * Move all relevant FMUs into event mode.
	 */
	enterEventMode: Importer * set of Name -> Importer
	enterEventMode(I, fmus) ==
		mu(I, scenario |-> mu(I.scenario, fmus |-> { name |-> mu(I.scenario.fmus(name), mode |-> <EVENT>) | name in set dom I.scenario.fmus & name in set fmus }))
	pre forall m' in set rng I.scenario.fmus & m'.time.i = 0 and m'.mode = <STEP>
	post forall m' in set rng I.scenario.fmus & m'.time.i = 0 and m'.mode = <STEP>;

    /**
	 * Move all FMUs in M into Step mode.
	 */
	enterStepMode: Importer * set of Name -> Importer
	enterStepMode(I, fmus) ==
		mu(I, scenario |-> mu(I.scenario, fmus |-> { name |-> mu(I.scenario.fmus(name), mode |-> <EVENT>) | name in set dom I.scenario.fmus & name in set fmus }))
	pre forall m' in set rng I.scenario.fmus & m'.time.i = 0 
		and m'.mode in set {<INIT>, <EVENT>}
	post forall m' in set rng I.scenario.fmus & m'.time.i = 0 
		and m'.mode = <STEP>;

    -- All connections shall be synchronized meaning their value should be identical
    variablesSynchronized: Machine -> bool
    variablesSynchronized(M) ==
        forall srcPort in set dom M.connections &
            let trgPort = M.connections(srcPort) in
            (M.fmus(srcPort.name)).env(srcPort.ref) = (M.fmus(trgPort.name)).env(trgPort.ref);

	/**
	 * Activate the clocks in all FMUs that are due to tick at this time.
	 * All clocks should be input clocks
	 */
	activateClocks: Importer * set of FMURef -> Importer 
	activateClocks(I, clocks)==
		if clocks = {} then I
		else 
		let clock in set clocks in
		let I1 = setClock(I, I.scenario.fmus(clock.name), clock.ref, true) in
		activateClocks(I1, clocks \ {clock})		
	pre clocks subset I.readyClocks inter I.relevantInputClocks
	measure card clocks;

	/*
	 * This function queries the clock to check its activation status
	 */
	queryClock: Importer * FMU * Ref -> Importer
	queryClock(I, fmu, clock)==
		let mk_(fmuUpdated, val) = get_cm(fmu, clock),
			fmuref = mk_FMURef(fmu.name, clock) in

		let connectedClock = I.scenario.clockConnections(fmuref) in
		let I1 = mu(I, 
			valueMap |-> I.valueMap ++ {connectedClock |-> val},
			relevantOutputClocks |-> I.relevantOutputClocks \ {fmuref},
			relevantInputClocks |-> I.relevantInputClocks union {connectedClock}
		) in
		updateEnvironmentClock(I1, fmuUpdated, clock, val.value)
	pre mk_FMURef(fmu.name,clock) in set I.relevantOutputClocks -- a clock should be relevant if we should query it
		and clock in set {c.ref | c in set fmu.clocks & c.type = <input>}; 


	/*
	 * This function sets the activation status of the clock
	 */
	setClock : Importer * FMU * Ref * bool -> Importer
	setClock(I, fmu, clock, val)==
		let fmuUpdated = set_cm(fmu,clock, val),
			fmuref = mk_FMURef(fmu.name, clock) in
		let feedthroughClocks = {mk_FMURef(fmu.name, c.ref) | c in set fmu.clocks & clock in set c.dependsOn} in
		let I1 = mu(I,
			valueMap |-> {fmuref} <-: I.valueMap,
			relevantInputClocks |-> I.relevantInputClocks \ {fmuref}, -- remain the same
			relevantOutputClocks |-> I.relevantOutputClocks union feedthroughClocks -- Other clocks might have been activated
		) in
		updateEnvironmentClock(I1, fmuUpdated, clock, val)
	pre mk_FMURef(fmu.name,clock) in set I.relevantInputClocks -- a clock should be relevant if we should query it
		and val <=> mk_FMURef(fmu.name,clock) in set I.inactiveClocks inter I.readyClocks;

	/*
	 * This function updates the Importer's environment according to observed clock value
	 */
	updateEnvironmentClock : Importer * FMU * Ref * bool -> Importer
	updateEnvironmentClock(I, fmu, clock, val)==
		let fmuref = mk_FMURef(fmu.name, clock),
			activatedVariables = createFMURefs(fmu, dunion {c.equations | c in set fmu.clocks & clock = c.ref}, {}) in
		mu(I,
			scenario |-> mu(I.scenario, fmus |-> I.scenario.fmus ++ {fmu.name |-> fmu}),
			activeClocks |-> if val then I.activeClocks union {fmuref} else I.activeClocks \ {fmuref},
			readyClocks |-> if val then I.readyClocks \ {fmuref} else I.readyClocks,
			inactiveClocks |-> if val then I.inactiveClocks \ {fmuref} else I.inactiveClocks union {fmuref},
			activeEquations |-> if val then I.activeEquations union activatedVariables else I.activeEquations \ activatedVariables,
			readyEquations |-> if val then I.readyEquations \ activatedVariables else I.readyEquations
		)
	pre let fmuRef = mk_FMURef(fmu.name,clock) in 
		fmuRef in set I.relevantInputClocks or fmuRef in set I.relevantOutputClocks;

	/*
	* Calculates an input equation that needs to be calculated
	* The port is calculated by the setting its value
	*/
	calculateInput : Importer * FMURef -> Importer
	calculateInput(I, equation)==
		let I1 = setPort(I, equation) in
			updateEnvironmentEquation(I1, I.scenario.fmus(equation.name), equation)
	pre 
		let fmu = I.scenario.fmus(equation.name) in
			equation in set I.activeEquations \ I.calculatedEquations -- The value should be active and not calculated before
			and equation in set dom I.valueMap -- A value to set should be accessible
			and fmu.mode = <EVENT>
			and (exists v in set fmu.inputs & v.ref = equation.ref and v.type = <discrete>); 

	/*
	* Calculates an output equation that needs to be calculated.
	* The port is calculated by the getting its value
	*/
	calculateOutput : Importer * FMURef -> Importer
	calculateOutput(I, equation)==
		let I1 = getPort(I, equation) in
			updateEnvironmentEquation(I1, I.scenario.fmus(equation.name), equation)
	pre 
		let fmu = I.scenario.fmus(equation.name) in
		equation in set I.activeEquations \ I.calculatedEquations
		and fmu.mode = <EVENT>
		and (exists v in set fmu.outputs & v.ref = equation.ref and v.type = <discrete>); 

	updateEnvironmentEquation : Importer * FMU * FMURef -> Importer
	updateEnvironmentEquation(I, fmu, equation)==
		let triggeredClocks = {clock | clock in set fmu.clocks & equation.ref in set clock.dependsOn} in
		-- We need to check if we triggered any clocks by setting the variable
		let triggeredInputs = {mk_FMURef(equation.name, inputClock.ref) | inputClock in set triggeredClocks & inputClock.type <> <input> },
			triggeredOutputs = {mk_FMURef(equation.name, outputClock.ref)  | outputClock in set triggeredClocks & outputClock.type <> <output> } in
		mu(I, 
			scenario |-> mu(I.scenario, fmus |-> I.scenario.fmus ++ {fmu.name |-> fmu}),
			calculatedEquations |-> I.calculatedEquations union {equation}, 
			relevantInputClocks |-> I.relevantInputClocks union triggeredInputs,
			relevantOutputClocks |-> I.relevantOutputClocks union triggeredOutputs
		)
	pre equation in set I.activeEquations \ I.calculatedEquations
	post equation in set RESULT.calculatedEquations;

    /*
		This function exchanges values between all connected Inputs and Outputs
		Currently we do not support algebraic loops
		It uses the runAlgorithm Function to execute the actions
	*/
    initializeData: Importer -> Importer
    initializeData(I) ==
	let connections = I.scenario.connections in
		let outputs = {mk_Action(<get>, I.scenario.fmus(output.name), output.ref) | output in set dom connections},
			inputs = {mk_Action(<set>, I.scenario.fmus(input.name), input.ref) | input in set rng connections} in
				runAlgorithm(I, outputs union inputs)
	pre forall fmu in set rng I.scenario.fmus & fmu.mode = <INIT>
    post variablesSynchronized(RESULT.scenario);

    /*
		This function runs a co-simulation step after the step negotiation
		Currently we do not support algebraic loops
		It uses the runAlgorithm-function to execute the actions
	*/
    cosimStep: Importer -> Importer
    cosimStep(I) ==
	let connections = I.scenario.connections in
		let fmus = rng I.scenario.fmus in
		-- Calculate all the actions that should be performed in the co-simulation step
		let outputs = {mk_Action(<get>, I.scenario.fmus(output.name), output.ref) | output in set dom connections & derefOutput(I.scenario.fmus(output.name), output.ref).type = <continous>},
			inputs = {mk_Action(<set>, I.scenario.fmus(input.name), input.ref) | input in set rng connections & derefInput(I.scenario.fmus(input.name), input.ref).type = <continous>},
			steps = {mk_Action(<step>, fmu, 1) | fmu in set fmus} in
		let actions = outputs union inputs union steps in
			runAlgorithm(I, actions)
	pre 
		let scenario = I.scenario in
			(forall fmu in set rng scenario.fmus & fmu.mode = <STEP>) and variablesSynchronized(scenario) and fmusSynchronized(scenario)
    post 
		let scenario = RESULT.scenario in
			(forall fmu in set rng scenario.fmus & fmu.mode = <STEP>) and variablesSynchronized(scenario) and fmusSynchronized(scenario);

	/*
		Function to run a set of actions (algorithm) until completion
		We pick an enabled action and perform it until the set of actions is empty
	*/
	runAlgorithm : Importer * set of Action -> Importer
	runAlgorithm(I, algorithm)==
		if algorithm = {} then I
		else
		let action in set  {a | a in set algorithm & activatedAction(I, a)} in
			runAlgorithm(runAction(I, action), algorithm \ {action})
	measure card algorithm;
	
	/*
		Runs a single action
	*/
	runAction: Importer * Action -> Importer
	runAction(I, A)==
	cases A:
		mk_Action(<get>, fmu, port) -> getPort(I, mk_FMURef(fmu.name, port)),
		mk_Action(<set>, fmu, port) -> setPort(I, mk_FMURef(fmu.name, port)),
		mk_Action(<step>, fmu, port) -> stepFMU(I, fmu), -- Nick can I sepcify that I don't care about the port here?
		mk_Action(<setC>, fmu, clock) -> setClock(I, fmu, clock, true),
		mk_Action(<getC>, fmu, clock) -> queryClock(I, fmu, clock)
	end;

	/*
		Performing the actions from the Importer's perspective
	*/

	-- This action obtains a value and set it on an input		
	setPort : Importer * FMURef -> Importer
	setPort(I, port)==
		let value = I.valueMap(port) in
			let fmu = set_m(I.scenario.fmus(port.name), port.ref, value) in 
				mu(I,
					scenario |-> mu(I.scenario, fmus |-> I.scenario.fmus ++ {fmu.name |-> fmu}), 
					valueMap |-> {port} <-: I.valueMap
				)
	pre port in set dom I.valueMap; -- A value to set should be accessible

	-- This action obtains a value from an output		
	getPort : Importer * FMURef -> Importer
	getPort(I, port)==
		let mk_(fmu, val) = get_m(I.scenario.fmus(port.name), port.ref), 
		 	connectedInput = I.scenario.connections(port) in
			mu(I, 
				scenario |-> mu(I.scenario, fmus |-> I.scenario.fmus ++ {fmu.name |-> fmu}),
				valueMap |-> I.valueMap ++ {connectedInput |-> val}
			)
	pre I.scenario.connections(port) not in set dom I.valueMap;

	/*
	 * This action progresses an FMU in time
	 * It also keeps track of the FMUs that triggers an event and adjust the step size dynamically
	 */
	stepFMU : Importer * FMU -> Importer
	stepFMU(I, fmu)==
		let mk_(fmuUpdated, step, event) = step_tm(fmu, I.stepSize) in
			mu(I, 
				scenario |-> mu(I.scenario, fmus |-> I.scenario.fmus ++ {fmuUpdated.name |-> fmuUpdated}),
				fmusWithEvent |-> if event then I.fmusWithEvent union {fmu.name} else I.fmusWithEvent,
				stepSize |-> step -- adjust the stepsize 
			);

	/*
	 * Is an Action activated?
	 * It is if its precondition is satisfied
	 */
	activatedAction : Importer * Action -> bool
	activatedAction(I, A) ==
		cases A:
		mk_Action(<get>, fmu, port) -> preGetI(I, fmu, port),
		mk_Action(<set>, fmu, port) -> preSetI(I, fmu, port),
		mk_Action(<step>, fmu, port) -> preStepI(I, fmu),
		mk_Action(<setC>, fmu, clock) -> preSetCI(I, fmu, clock),
		mk_Action(<getC>, fmu, clock) -> preGetCI(I, fmu, clock)
	end;

	/*
		The preconditions of the actions from the paper
	*/
	preSetI: Importer * FMU * Ref -> bool
	preSetI(I, fmu, port)==
		let input = derefInput(fmu, port) in
		(fmu.mode = <EVENT> <=> mk_FMURef(fmu.name, port) in set (I.activeEquations \ I.calculatedEquations) and input.type = <discrete>)
			and (fmu.mode = <STEP> <=> input.type = <continous>)
			and preSet(fmu, port);

	preGetI: Importer * FMU * Ref -> bool
	preGetI(I, fmu, port)==
		let output = derefOutput(fmu, port) in
		(fmu.mode = <EVENT> <=> mk_FMURef(fmu.name, port) in set (I.activeEquations \ I.calculatedEquations) and output.type = <discrete>)
			and (fmu.mode = <STEP> <=> output.type = <continous>)
			and preGet(fmu, port);

	preStepI: Importer * FMU -> bool
	preStepI(I, fmu)==
		preStepT(fmu, I.stepSize);

	preGetCI: Importer * FMU * Ref -> bool
	preGetCI(I, fmu, clock) ==
		let output = mk_FMURef(fmu.name, clock) in
			output in set I.relevantOutputClocks
				and preGetC(fmu, clock);
	
	preSetCI: Importer * FMU * Ref -> bool
	preSetCI(I, fmu, clock) ==
		let input = mk_FMURef(fmu.name, clock) in
		let val = I.valueMap(input) in
			preSetC(fmu, clock, val.value) and input in set I.relevantInputClocks;


	/*
		The preconditions of the actions from the paper - notice that the actions are only valid when the FMUs are not in the done state
	*/

	preSet: FMU * Ref -> bool
	preSet(fmu, input)==
		exists v in set fmu.inputs & v.ref = input and v.causality = <input>
			and fmu.mode <> <DONE>;
	
	/*
		An p
	 */
	preGet: FMU * Ref -> bool
	preGet(fmu, ref)==
		let output = derefOutput(fmu, ref) in
		exists v in set fmu.outputs & v.ref = ref and v.causality = <output>
			and fmu.mode <> <DONE>
			and feedthroughSatisfied(fmu, output);

	preStepT: FMU * real -> bool
	preStepT(fmu, stepSize)==
		forall i in set fmu.inputs &
			 		(i.contract = <reactive> => fmu.env(i.ref).time.r = fmu.time.r + stepSize)
					and (i.contract = <delayed> => fmu.env(i.ref).time = fmu.time)
			and stepSize >= 0.0 and fmu.mode = <STEP>;

	preStepE: FMU -> bool
	preStepE(fmu)==
		fmu.mode = <EVENT>;

	preSetC: FMU * Ref * bool -> bool
	preSetC(fmu, clock, val)==
	 	exists v in set fmu.clocks & v.ref = clock and v.type = <input>
			and val <=> not clock in set fmu.activeClocks; -- We can only activate a clock that 

	preGetC: FMU * Ref -> bool
	preGetC(fmu, clock)==
	 	exists v in set fmu.clocks & v.ref = clock and v.type = <output>
		 and fmu.mode = <EVENT>; -- We can only get an output clock

	feedthroughSatisfied : FMU * Variable -> bool
	feedthroughSatisfied(fmu, output)==
		forall i in set fmu.inputs & i.ref in set output.dependsOn 
			and (i.contract = <reactive> => fmu.env(i.ref).time >= fmu.time)
			and (i.contract = <delayed> => fmu.env(i.ref).time = fmu.time)
	pre forall i in set fmu.inputs & i.causality = <input>;

	/*
	 * This is the initialization procedure that makes the scenario ready for simulation
	 */
    initialization: Importer -> Importer 
    initialization(I) ==
        -- We don't care about parameters of the FMU, they are ignored
        /**
         * Exchange data between all connections in M
        */
        let I1 = initializeData(I) in
        /**
         * Calculate a schedule for all timebased clocks
        */
        let I2 = createSchedule(I1) in
        /*
            Move all FMUs to Step Mode
         */
        enterStepMode(I2, dom I2.scenario.fmus)
	pre forall m' in set rng I.scenario.fmus & m'.mode = <INIT> 
		and fmusSynchronized(I.scenario)
    post forall m' in set rng RESULT.scenario.fmus & m'.mode = <STEP> 
		and variablesSynchronized(RESULT.scenario) 
		and fmusSynchronized(RESULT.scenario);

	/*
		This function calculates the set of clocks that should be ticked
	 */
    tickingClocks: Importer -> Importer * set of FMURef
    tickingClocks(I) ==
		-- Clocks to tick at the current time
        let clocks = dom (I.schedule :> {I.time.r}) in
		-- Update the schedule for the ticked clocks to a new point in time
		--let updatedSchedule = clocks
		-- The clocks (input) are now relevant and their schedule is now updated
		let I1 = mu(I, relevantInputClocks |-> clocks) in --, schedule |-> updatedSchedule) in
		mk_(I1, clocks);


	/*
	 * This is the co-simulation step procedure that moves the scenario from time "t" to time "t+H"
	 */
    cosimulationStep: Importer -> Importer 
    cosimulationStep(I) ==
        /*
            Detect events at the initial time
        */
        let mk_(I1, clocksToTick) = tickingClocks(I) in
		-- We should enter event mode if any clocks have ticked or any fmu wants to enter Event mode
		let eventDetected = clocksToTick <> {} or I1.fmusWithEvent <> {} in
        /*
            If any events are present we solve them
        */
		let I2 = (if eventDetected then clockedSimulationAlgorithm(I1, clocksToTick) else I) in
        /*
            Select a step duration
         */
		let I3 = mu(I, stepSize |-> stepnegotiation(I2)) in
        /*
            Perform the rest of the simulation
        */
		let I4 = cosimStep(I3) in

		if I4.endtime >= I4.time 
			then cosimulationStep(I4) 
		else I4
	pre forall m' in set rng I.scenario.fmus & m'.mode = <STEP> 
		and variablesSynchronized(I.scenario) 
		and fmusSynchronized(I.scenario)
    post forall m' in set rng RESULT.scenario.fmus & m'.mode = <STEP> 
		and variablesSynchronized(RESULT.scenario) 
		and fmusSynchronized(RESULT.scenario)
	measure I.endtime.r - I.time.r;

	fmusSynchronized: Machine -> bool
	fmusSynchronized(M) ==
		 card { fmu.time | fmu in set rng M.fmus } = 1;


    -- Calculate an appropiate step duration that all FMUs can take
	-- This should be done dynamically
    stepnegotiation: Importer -> real
    stepnegotiation(M) ==
		is not yet specified
	post RESULT >= 0 
		and forall fmu in set rng M.scenario.fmus & fmu.maxStep >= RESULT;

	/*
		This is the recurrence relation from the paper - calculating the set of FMUs that must enter Event mode.
		We added the FMUs that has a ticking clock 
		And subsequently include all FMUs that has clock connection from an FMU in the event Mode 
	 */ 
	calculateRelevantFMUs: Machine * set of Name * set of Name * set of FMURef -> set of Name 
	calculateRelevantFMUs(M, relevantFMUs, notRelevantFMUs, clocks) == 
		let addedFMUs = {clock.name | clock in set clocks} union
			{M.clockConnections(con).name | con in set dom M.clockConnections & con.name in set relevantFMUs 
														and M.clockConnections(con).name in set notRelevantFMUs} in
		-- We terminate if we have reached a fixed point where no more relevant FMUs can be found
		if addedFMUs = {} then relevantFMUs
		else 
			calculateRelevantFMUs(M, relevantFMUs union addedFMUs, notRelevantFMUs \ addedFMUs, clocks)
		pre relevantFMUs inter notRelevantFMUs = {}
		post {clock.name | clock in set clocks} subset RESULT 
			and RESULT subset dom M.fmus
		measure card notRelevantFMUs;

	/**
	 * Let M denote the set of FMU instances participating in the simulation. We
	 * assume that one FMU instance m in set M or the importer has requested to enter
	 * Event mode. Therefore we assume that every other instance m' in set M and m' <> m
	 * has been stepped up to the same super-dense time (t.r, 0).
	 */
	clockedSimulationAlgorithm: Importer * set of FMURef -> Importer
	clockedSimulationAlgorithm(I, clocks) ==
		/**
		 * 1. Every m in set M enters Event mode (super-dense time instant is t.i = 0);
		 */
		let relevantFMUs = calculateRelevantFMUs(I.scenario, I.fmusWithEvent, (dom I.scenario.fmus) \ I.fmusWithEvent, clocks) in
		let I1 = enterEventMode(I, relevantFMUs) in

		/**
		 * 2. Activate any time-based clocks scheduled to tick at (t.r , 0), by
		 *    invoking set_cm(_, wcm0) for any input or output clock wcm0 in set
		 *    Ucm union Ycm and any instance m in set M;
		 */

		let I2 = activateClocks(I1, clocks) in
		/*
			The event Iteration solves the system of equations, updates the schedule and the discrete state of the FMUs
		 */
		let I3 = eventIteration(I2, relevantFMUs) in

		/**
		 * 6. If any m in set M wishes to repeat the event iteration, or if a clock returned a
		 *    zero interval, go to Step 3 (start iteration t.i + 1).
		 */

		if I3.fmusWithEvent <> {} 
			then enterStepMode(I3, relevantFMUs)
		else clockedSimulationAlgorithm(I, clocks)

	pre fmusSynchronized(I.scenario)
		and forall fmu in set rng I.scenario.fmus & fmu.mode = <STEP>
		and (I.fmusWithEvent <> {} or clocks <> {}) 
	post fmusSynchronized(I.scenario)
		and forall fmu in set rng I.scenario.fmus & fmu.mode = <STEP>
	-- I do not know the measure - it has something to do with the events that FMUs trigger
	measure is not yet specified;


eventIteration : Importer * set of Name -> Importer
eventIteration(I, relevantFMUs)==
		/**
		 * 3. Construct and solve system of equations for t.i :
		 * 		(a) For all ycm in set Ycm of any instance m in set M, forward activation state of
		 *		    triggered clocks:
		 * 			i. Invoke get_cm (_, ycm ), and set_cm(_, ucm0 ) or get_cm (_, ycm0 ),
		 *			   for any other clock ucm0 in set Ucm or ycm0 in set Ycm and instance
		 *			   m in set M that is transitively connected to ycm or has become active
		 *			   as a result of the clock activations;
		 *		(b) Invoke get_m(_, ym0) and set_m(_, um0, _) in the appropriate order,
		 *		    for any instance m in set M.
		 */

		let I1 = solveSystemOfEquations(I) in
		/**
		 * 4. Invoke step_e(_) for m in set M (signals end of Event iteration t.i).
		*/
		let I2 = updateDiscreteState(I1, relevantFMUs) in

		/**
			* 5. Schedule clocks by invoking next_tm on every relevant clock, for m in set M.
		*/
		updateSchedule(I2, relevantFMUs, { <tunable>, <changing>, <countdown> })
	pre fmusSynchronized(I.scenario)
		and forall fmu in set rng I.scenario.fmus & fmu.mode = <EVENT>
	post forall fmu in set rng I.scenario.fmus & fmu.mode = <EVENT>
		and I.activeEquations \ I.calculatedEquations = {};


/*
 * This function solves the active events by solving the system of equations
 * It terminates when all active variables have been calculated
 * Clock actions should also be performed before regular port actions.
 */
solveSystemOfEquations : Importer -> Importer
solveSystemOfEquations(I)==
	if I.activeEquations \ I.calculatedEquations = {} then I
	else
	let clockActions = { mk_Action(<setC>, I.scenario.fmus(clock.name), clock.ref) | clock in set I.relevantInputClocks }
						union { mk_Action(<getC>, I.scenario.fmus(clock.name), clock.ref) | clock in set I.relevantOutputClocks } in
		if clockActions <> {} and {a | a in set clockActions & activatedAction(I,a)} <> {} then
			let clockAction in set {a | a in set clockActions & activatedAction(I, a)} in
			solveSystemOfEquations(runAction(I, clockAction))
		else
			let portActions = {mk_Action(<set>, I.scenario.fmus(input.name), input.ref) | input in set I.activeEquations \ I.calculatedEquations }
						union {mk_Action(<get>, I.scenario.fmus(output.name), output.ref) | output in set I.activeEquations \ I.calculatedEquations } in
				let portAction in set {a | a in set portActions & activatedAction(I, a)} in
					solveSystemOfEquations(runAction(I, portAction))
	measure card I.readyEquations + card (I.activeEquations \ I.calculatedEquations);


/*
	This function updates the schedule
*/
updateSchedule: Importer * set of Name * set of Interval -> Importer
updateSchedule(I, fmus, intervalSet)==
	if fmus = {} then I 
	else
	let name in set fmus in
		let fmu = I.scenario.fmus(name) in
			let relevantClocks = {c.ref | c in set fmu.clocks & c.interval in set intervalSet} in  
				let scheduleEntries = calculateSchedule(fmu, relevantClocks, {|->}) in
					let I1 = mu(I, schedule |-> I.schedule ++ scheduleEntries) in
						updateSchedule(I1, fmus \ {name}, intervalSet)
measure card fmus;

/*
	Calculate a schedule for all time based clocks
*/
createSchedule : Importer -> Importer
createSchedule(I) == 
	let fmus = dom I.scenario.fmus in 
		updateSchedule(I, fmus, {<constant>, <fixed>, <calculated>, <tunable>, <changing>, <countdown>})
pre forall m' in set rng I.scenario.fmus & m'.mode = <INIT>
	and fmusSynchronized(I.scenario)
	and variablesSynchronized(I.scenario)
post 
	let timeBasedClocks = dunion {createFMURefs(fmu, {clock.ref | clock in set fmu.clocks & clock.type = <input> and clock.interval <> <triggered>}, {}) | fmu in set rng I.scenario.fmus}
	in 
	dom RESULT.schedule = timeBasedClocks
	  -- all time-based clocks must have a schedule
	and (forall m' in set rng I.scenario.fmus & m'.mode = <INIT>)
	and fmusSynchronized(I.scenario)
	and variablesSynchronized(I.scenario);

calculateSchedule: FMU * set of Ref * map FMURef to real -> map FMURef to real
calculateSchedule(fmu, clocks, schedule)== 
	if clocks = {} then schedule
	else
	let clock in set clocks in
		let fmuRef = mk_FMURef(fmu.name, clock) in
		let S1 = schedule ++ {fmuRef |-> next_tm(fmu, clock)} in
		calculateSchedule(fmu, clocks \ {clock}, S1)
pre 
let cref = {c.ref | c in set fmu.clocks & c.type = <input> and c.interval <> <triggered> } in 
	clocks subset cref
measure card clocks; 

createFMURefs: FMU * set of Ref * set of FMURef -> set of FMURef
createFMURefs(fmu, clocks, res)==
	if clocks = {} then res
	else let clock in set clocks in
	createFMURefs(fmu, clocks \ {clock}, res union {mk_FMURef(fmu.name, clock)})
measure card clocks;



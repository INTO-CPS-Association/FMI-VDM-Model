/**
 * This file is part of the INTO-CPS toolchain.
 *
 * Copyright (c) 2017-2019, INTO-CPS Association,
 * c/o Professor Peter Gorm Larsen, Department of Engineering
 * Finlandsgade 22, 8200 Aarhus N.
 *
 * All rights reserved.
 *
 * THIS PROGRAM IS PROVIDED UNDER THE TERMS OF GPL VERSION 3 LICENSE OR
 * THIS INTO-CPS ASSOCIATION PUBLIC LICENSE VERSION 1.0.
 * ANY USE, REPRODUCTION OR DISTRIBUTION OF THIS PROGRAM CONSTITUTES
 * RECIPIENT'S ACCEPTANCE OF THE OSMC PUBLIC LICENSE OR THE GPL 
 * VERSION 3, ACCORDING TO RECIPIENTS CHOICE.
 *
 * The INTO-CPS toolchain  and the INTO-CPS Association Public License are
 * obtained from the INTO-CPS Association, either from the above address, from
 * the URLs: http://www.into-cps.org, and in the INTO-CPS toolchain distribution.
 * GNU version 3 is obtained from: http://www.gnu.org/copyleft/gpl.html.
 *
 * This program is distributed WITHOUT ANY WARRANTY; without
 * even the implied warranty of  MERCHANTABILITY or FITNESS FOR
 * A PARTICULAR PURPOSE, EXCEPT AS EXPRESSLY SET FORTH IN THE
 * BY RECIPIENT SELECTED SUBSIDIARY LICENSE CONDITIONS OF
 * THE INTO-CPS ASSOCIATION.
 *
 * See the full INTO-CPS Association Public License conditions for more details.
 */
 types
	/**
	 * A point in superdense time is represented by a real quantity and a whole
	 * number instant "within" that time.
	 */
	Time ::
		r	: real
		i	: nat
	inv t ==
		t.r >= 0	-- Times must be positive
	ord a < b ==
		(a.r < b.r) or (a.r = b.r and a.i < b.i);	-- NB using real equality!

	/**
	 * Possible "interval" values for a Clock variable
	 */
	Interval = <constant> | <fixed> | <calculated> | <tunable> | <changing> | <countdown> | <triggered>;

	/**
	 * A variable or clock reference. These are unique within an FMU.
	 */
	Ref = nat;

	/**
	 * A convenient way to name FMUs.
	 */
	Name = seq1 of char;

	/**
	 * A reference to a connection point in a specific named FMU.
	 */
	FMURef ::
		name	: Name
		ref		: Ref;

	/**
	 * The current known FMU variable values. These are usually used by FMUs, but note that
	 * Clocks can produce temporary environments that are not committed until a step.
	 */
	Environment = map Ref to Value;

	/**
	 * The value that a clock or variable can have. Simplified for now.
	 */
	Value = real | int | bool;

	/**
	 * An Equation is used to modify the values of an FMU in order to compute a new
	 * step in time. A clock defines a set of equations to activate.
	 */
	Equation = Environment -> Environment;

	/**
	 * A variable is identified by a Ref (within an FMU).
	 */
	Variable ::
		ref			: Ref
        time        : Time
        causality	: <input> | <output>
        type		: <discrete> | <continous>
		clocks		: set of Ref -- clocks that the variable can active
        dependsOn   : set of Ref -- feedthrough only relevant for outputs
        contract    : <none> | <reactive> | <delayed>   
	inv var ==
		(var.causality = <output> => var.contract = <none>) -- input contract only relevant for inputs
		and var.causality = <input> => var.dependsOn = {} ;


	/**
	 * A clock is identified by a Ref (within an FMU) and activates a set of equations.
	 */
	Clock ::
		ref			: Ref
		type		: <input> | <output>
		interval	: Interval
        timeToTick  : Time
		dependsOn 	: set of Ref -- The clocks that can trigger an clock
		equations	: set of Ref -- variables the clock activates
	inv clock ==
		(clock.type = <output> => clock.interval = <triggered>)	-- An output clock must be of type triggered 
		and (clock.type = <input> => clock.dependsOn = {}); -- Only output clocks are subject to feedthrough

	/**
	 * An FMU is comprised of a set of clocks and variables, plus an environment that holds
	 * the current values for both. The references must be unique and the references within
	 * the environment must all refer to known variables.
	 */
	FMU ::
		name				: Name				-- m
		clocks				: set of Clock		-- Ucm + Ycm
		inputs				: set of Variable	-- Um 
        outputs             : set of Variable   -- Ym
		vars 				: set of Variable
		mode				: <INIT> | <DONE> | <STEP> | <EVENT>
		time				: Time				-- time is per FMU
        maxStep             : real				-- How far can the FMU step
		env					: Environment
		nextScheduledTick	: [Time]
		nextScheduledClocks	: set of Ref
		activeClocks		: set of Ref
		activeEquations		: set of Equation
	inv fmu ==
		let vars = fmu.inputs union fmu.outputs in
		let crefs = { c.ref | c in set fmu.clocks },
			vrefs = { v.ref | v in set vars},
			refs  = crefs union vrefs
		in
            -- The maximal step cannot be negative
            fmu.maxStep >= 0.0
			-- All clock and variable references are unique
			and card refs = card fmu.clocks + card fmu.vars
			-- All references in the environment are defined
			and (dom fmu.env subset refs)
			-- All active clocks are defined
			and (fmu.activeClocks subset crefs)
			-- A clock can only be active when the fmu is in event mode
			and (fmu.mode <> <EVENT> => fmu.activeClocks = {})
			-- All variables' clocks are defined
			and (forall var in set fmu.vars & var.clocks subset crefs)
			-- An active variable is active by a clock Todo
			--and (forall (cref |-> val) in set fmu.activeEquations 
			---		& exists c in set fmu.clocks & c.ref in set fmu.activeClocks and equation in set c.equations)
			-- All scheduled clocks are defined
			and (if fmu.nextScheduledTick = nil
				 then fmu.nextScheduledClocks = {}
				 else fmu.nextScheduledClocks subset crefs);

	/**
	 * A machine is a collection of FMUs, plus a connection map between them. The connection
	 * sources must be FMU outputs and the destinations must be FMU inputs.
	 */
	Machine ::
		fmus				: map Name to FMU
		connections			: map FMURef to FMURef
		clockConnections	: map FMURef to FMURef
	inv m ==
		(forall fmuref in set dom m.connections &
			-- Every connection's source is a known FMU
			fmuref.name in set dom m.fmus
			-- Every connection's source is an output from its FMU
			and let fmu = m.fmus(fmuref.name) in exists v in set fmu.outputs & fmuref.ref = v.ref)
		and
		(forall fmuref in set rng m.connections &
			-- Every connection's destination is a known FMU
			fmuref.name in set dom m.fmus
			-- Every connection's destination is an input of its FMU
			and let fmu = m.fmus(fmuref.name) in exists v in set fmu.inputs & fmuref.ref = v.ref)
		and
			(forall fmuref in set dom m.clockConnections &
			-- Every connection's source is a known FMU
			fmuref.name in set dom m.fmus
			-- Every connection's source is an output from its FMU
			and let fmu = m.fmus(fmuref.name) in
				exists v in set fmu.clocks & fmuref.ref = v.ref and v.type = <output>)
		and
		(forall fmuref in set rng m.clockConnections &
			-- Every connection's destination is a known FMU
			fmuref.name in set dom m.fmus
			-- Every connection's destination is an input of its FMU
			and let fmu = m.fmus(fmuref.name) in
				exists v in set fmu.clocks & fmuref.ref = v.ref and v.type = <input>)
		and dom m.clockConnections inter dom m.connections = {}
		and rng m.clockConnections inter rng m.connections = {};
  
	/*
		The Importer runs the simulation
		It keeps track of the input and output to adapt to the behavior of the scenario
		It has a simulation time and values to exchange between the FMUs
	*/
	Importer :: 
		scenario : Machine
        schedule : map FMURef to real -- the time the clock should tick
		activeClocks : set of FMURef -- active clocks
		readyClocks : set of FMURef -- clocks that can be activated
		inactiveClocks : set of FMURef -- inactive clocks
		fmusWithEvent	: set of Name -- FMUs that triggered a state event
		relevantOutputClocks : set of FMURef -- All Output clocks the Importer must query since their state can have changed
		relevantInputClocks  : set of FMURef -- All Input clocks the Importer must update because their output changed 
		activeEquations : set of FMURef -- All active equations 
		calculatedEquations : set of FMURef -- The equations the Importer has calculated
		time : Time -- Simulation Time
		stepSize : real -- Planned step size
		valueMap : map FMURef to Value
	inv imp ==
		let fmus = imp.scenario.fmus in
		let inputclocks = dunion {createFMURefs(fmu, {clock.ref | clock in set fmu.clocks & clock.type = <input> }, {}) | fmu in set rng fmus } in
		let outputclocks = dunion {createFMURefs(fmu, {clock.ref | clock in set fmu.clocks & clock.type = <output> }, {}) | fmu in set rng fmus } in
		let clocks = inputclocks union outputclocks
		in
		imp.activeClocks union imp.inactiveClocks = clocks  -- a clock is either inactive or active
		and imp.activeClocks inter imp.inactiveClocks = {} -- a clock cannot be active and inactive
		and imp.activeClocks inter imp.readyClocks = {} -- an active clock can not be active
		and imp.activeClocks = dunion {createFMURefs(fmu, fmu.activeClocks, {}) | fmu in set rng fmus }
		and imp.fmusWithEvent subset dom fmus -- all fmus that wishes to enter event mode is in the scenario
		and imp.relevantInputClocks subset inputclocks -- a relevant input clock needs to be an input clock
		and imp.relevantOutputClocks subset outputclocks -- a relevant output clock needs to be an output clock
		and imp.relevantInputClocks inter imp.relevantOutputClocks = {}
		--and imp.activeEquations = dunion {fmu.activeEquations | fmu in set rng fmus} -- Equations are actived by the clocks
		and dom imp.schedule = dunion {createFMURefs(fmu, {clock.ref | clock in set fmu.clocks & clock.type = <input> and clock.interval <> <triggered>}, {}) | fmu in set rng fmus }-- We only have time based clocks in the schedule
		---and imp.activeEquations = {c.equations | c in set clocks & c.ref in set imp.activeClocks} -- Invariant for the paper TYPE MISMATCH
		;

functions
	/**
	 * These functions are total, because the error conditions that you would expect
	 * on an API are modelled by preconditions or type invariants.
	 */

    -- We should never calculate the equations when we are not in event mode
	set_m: FMU * Ref * Value +> FMU
	set_m(fmu, ref, val) ==
		mu(fmu, env |-> fmu.env ++ { ref |-> val })
		-- and calculate equations?
	pre exists v in set fmu.inputs & v.ref = ref and v.causality = <input>;

	get_m: FMU * Ref +> FMU * Value
	get_m(fmu, ref) ==
		-- calculate equations?
		mk_(fmu, fmu.env(ref))
	pre exists v in set fmu.outputs & v.ref = ref and v.causality = <output>;

	set_cm: FMU * Ref * bool +> FMU
	set_cm(fmu, ref, val) ==
		mu(fmu,
			env				|-> fmu.env ++ { ref |-> val },
			activeClocks	|-> if val then fmu.activeClocks union {ref} else fmu.activeClocks \ {ref})
		-- and calculate equations?
	pre val <=> not ref in set fmu.activeClocks
		and exists v in set fmu.clocks & v.ref = ref and v.type = <output>
	post val <=> ref in set fmu.activeClocks;

	get_cm: FMU * Ref +> FMU * bool
	get_cm(fmu, ref) ==
		-- calculate equations?
		mk_(fmu, fmu.env(ref))
	pre exists v in set fmu.clocks & v.ref = ref and v.type = <input>;

	step_tm: FMU * real +> FMU * real * bool
	step_tm(fmu, step) ==
        if step >= fmu.maxStep then mk_(fmu, fmu.maxStep, true) -- We should at it to the set of FMUs that wants to enter event mode
        else mk_(fmu, step, false)
	pre fmu.mode = <STEP> and step >= 0.0 -- Step mode and positive step duration
        and inputsDefined(fmu)
	post RESULT.#2 <= step -- We cannot take a step larger than what the Importer requested
        and RESULT.#2 <= fmu.maxStep;-- We cannot take a step larger than what the FMU is capable of
            
	step_e: FMU +> FMU * bool
	step_e(fmu) ==
		is not yet specified
	pre fmu.mode = <EVENT>
	post RESULT.#1.mode = <EVENT> 		-- FMU remains in event mode - it always does the Importer should bring it out
        and RESULT.#1.time.i = fmu.time.i + 1 -- The Integer component of the FMU should be increased
		and RESULT.#1.activeClocks = {} ; -- All clocks get disabled when we step the FMU

	next_tm: FMU * Ref +> real | <NaN>
	next_tm(fmu, ref) ==
		is not yet specified
	pre exists v in set fmu.clocks & v.ref = ref
		and v.interval in set { <tunable>, <changing>, <countdown> };

	/**
	 * Update the discrete state of all FMUs.
	 */
	updateDiscreteState: Importer * set of Name -> Importer
	updateDiscreteState(I,fmus) ==
		let M = I.scenario in
		let res = {fmu |-> step_e(M.fmus(fmu)) | fmu in set dom M.fmus & fmu in set fmus} in 
		let fmuMap = { fmu |-> res(fmu).#1 | fmu in set dom res} in
		let eventFMUs = { fmu | fmu in set dom res & res(fmu).#2 = true} in
		-- All the FMUs should be used as the FMUs
		mu(I, 
			scenario |-> mu(I.scenario, fmus |-> fmuMap),
			fmusWithEvent |-> eventFMUs
		)
	pre forall m' in set rng I.scenario.fmus & m'.mode = <EVENT>;
 
    /* All inputs must have a value to be stepped */
    inputsDefined: FMU -> bool
    inputsDefined(fmu) ==
        forall input in set fmu.inputs & input.ref in set dom fmu.env;

	/**
	 * Dereference a clock in a given FMU.
	 */
	derefClock: FMU * Ref -> Clock
	derefClock(fmu, ref) ==
		iota c in set fmu.clocks & c.ref = ref
	pre exists c in set fmu.clocks & c.ref = ref;

	/**
	 * Move all relevant FMUs into event mode.
	 */
	enterEventMode: Importer * set of Name -> Importer
	enterEventMode(I, fmus) ==
		mu(I, scenario |-> mu(I.scenario, fmus |-> { name |-> mu(I.scenario.fmus(name), mode |-> <EVENT>) | name in set dom I.scenario.fmus & name in set fmus }))
	pre forall m' in set rng I.scenario.fmus & m'.time.i = 0 and m'.mode = <STEP>
	post forall m' in set rng I.scenario.fmus & m'.time.i = 0 and m'.mode = <STEP>;

    /**
	 * Move all FMUs in M into Step mode.
	 */
	enterStepMode: Importer * set of Name -> Importer
	enterStepMode(I, fmus) ==
		mu(I, scenario |-> mu(I.scenario, fmus |-> { name |-> mu(I.scenario.fmus(name), mode |-> <EVENT>) | name in set dom I.scenario.fmus & name in set fmus }))
	pre forall m' in set rng I.scenario.fmus & m'.time.i = 0 
		and m'.mode in set {<INIT>, <EVENT>}
	post forall m' in set rng I.scenario.fmus & m'.time.i = 0 
		and m'.mode = <STEP>;

    -- All connections shall be synchronized so meaning their value should be identical
    variablesSynchronzied: Machine -> bool
    variablesSynchronzied(M) ==
        forall srcPort in set dom M.connections &
            let trgPort = M.connections(srcPort) in
            (M.fmus(srcPort.name)).env(srcPort.ref) = (M.fmus(trgPort.name)).env(trgPort.ref);

	/**
	 * Activate the clocks in all FMUs that are due to tick at this time.
	 * All clocks should be input clocks
	 */
	activateClocks: Importer * set of FMURef -> Importer 
	activateClocks(I, clocks)==
		if clocks = {} then I
		else 
		let clock in set clocks in
		let I1 = setClock(I, I.scenario.fmus(clock.name), clock.ref, true) in
		activateClocks(I1, clocks \ {clock})		
	pre clocks subset I.readyClocks inter I.relevantInputClocks
	measure card clocks;

	queryClock: Importer * FMU * Ref -> Importer
	queryClock(I, fmu, clock)==
		let mk_(fmuUpdated, val) = get_cm(fmu, clock) in
		let fmuref = mk_FMURef(fmu.name, clock) in
		let connectedClock = I.scenario.clockConnections(fmuref) in
		let activatedVariables = createFMURefs(fmu, dunion {c.equations | c in set fmu.clocks & clock = c.ref}, {}) in
		mk_Importer(
			mk_Machine(
				I.scenario.fmus ++ {fmuUpdated.name |-> fmuUpdated},
				I.scenario.connections,
				I.scenario.clockConnections
			),
			I.schedule, -- Not affected
			if val then I.activeClocks union {fmuref} else I.activeClocks \ {fmuref},
			if val then I.readyClocks \ {fmuref} else I.readyClocks,
			if val then I.inactiveClocks \ {fmuref} else I.inactiveClocks union {fmuref},
			I.fmusWithEvent, -- Not affected
			I.relevantInputClocks union {connectedClock}, 
			I.relevantOutputClocks \ {fmuref}, -- Other clocks might have been activated?
			if val then I.activeEquations union activatedVariables else I.activeEquations \ activatedVariables, 
			I.calculatedEquations, -- Not affected
			I.time, -- Not affected
			I.stepSize, 
			I.valueMap ++ {connectedClock |-> val}
		)
	pre mk_FMURef(fmu.name,clock) in set I.relevantOutputClocks; -- a clock should be relevant if we should query it


	setClock : Importer * FMU * Ref * bool -> Importer
	setClock(I, fmu, clock, val)==
		let fmuUpdated = set_cm(fmu,clock, val) in
		let fmuref = mk_FMURef(fmu.name, clock) in
		let affectClocks = {mk_FMURef(fmu.name, c.ref) | c in set fmu.clocks & clock in set c.dependsOn} in
		let activatedVariables = createFMURefs(fmu, dunion {c.equations | c in set fmu.clocks & clock = c.ref}, {}) in
		mk_Importer(
			mk_Machine(
				I.scenario.fmus ++ {fmuUpdated.name |-> fmuUpdated},
				I.scenario.connections,
				I.scenario.clockConnections
			),
			I.schedule, -- Not affected
			if val then I.activeClocks union {fmuref} else I.activeClocks \ {fmuref},
			if val then I.readyClocks \ {fmuref} else I.readyClocks,
			if val then I.inactiveClocks \ {fmuref} else I.inactiveClocks union {fmuref},
			I.fmusWithEvent, -- Not affected
			I.relevantInputClocks, -- remain the same
			I.relevantOutputClocks union affectClocks, -- Other clocks might have been activated
			if val then I.activeEquations union activatedVariables else I.activeEquations \ activatedVariables, 
			I.calculatedEquations, -- Not affected
			I.time, -- Not affected
			I.stepSize, -- Not affected
			I.valueMap
		)
	pre mk_FMURef(fmu.name,clock) in set I.relevantInputClocks; -- a clock should be relevant if we should query it


	calculateEquationInput : Importer * FMURef -> Importer
	calculateEquationInput(I, equation)==
		let value = I.valueMap(equation) in
		let fmuUpdated = set_m(I.scenario.fmus(equation.name), equation.ref, value) in 
		let triggeredClocks = {clock | clock in set fmuUpdated.clocks & equation.ref in set clock.dependsOn} in
		-- We need to check if we triggered any clocks by setting the variable
		let triggeredInputs = {mk_FMURef(equation.name, inputClock.ref) | inputClock in set triggeredClocks & inputClock.type <> <input> } in
		let triggeredOutputs = {mk_FMURef(equation.name, outputClock.ref)  | outputClock in set triggeredClocks & outputClock.type <> <output> } in
		mu(I, 
			scenario |-> mu(I.scenario, fmus |-> I.scenario.fmus ++ {fmuUpdated.name |-> fmuUpdated}),
			valueMap |-> {equation} <-: I.valueMap,
			calculatedEquations |-> I.calculatedEquations union {equation}, 
			relevantInputClocks |-> I.relevantInputClocks union triggeredInputs,
			relevantOutputClocks |-> I.relevantOutputClocks union triggeredOutputs
		)
	pre equation in set I.activeEquations \ I.calculatedEquations -- The value should be active and not calculated before
		and I.valueMap(equation); -- A value to set should be accessible

	calculateEquationOutput : Importer * FMURef -> Importer
	calculateEquationOutput(I, equation)==
		let mk_(fmuUpdated, val) = get_m(I.scenario.fmus(equation.name), equation.ref) in
		let connectedInput = I.scenario.connections(equation) in
		-- We need to check if we triggered any clocks by getting the variable

		mu(I, 
			scenario |-> mu(I.scenario, fmus |-> I.scenario.fmus ++ {fmuUpdated.name |-> fmuUpdated}),
			valueMap |-> I.valueMap ++ {connectedInput |-> val},
			calculatedEquations |-> I.calculatedEquations union {equation}
		)
	pre equation in set I.activeEquations \ I.calculatedEquations;

	/**
	 * Propagate one FMU's output clock activations.
	 */
	propagateFMUActivation: Machine * Name -> Machine
	propagateFMUActivation(M, name) ==
		is not yet specified;

	/**
	 * Propagate activation from all output clocks to input clocks. The names passed
	 * are the FMU names to process (initially all of them).
	 */
	propagateActivations: Machine * set of Name -> Machine
	propagateActivations(M, names) ==
		if names = {}
		then M
		else let name in set names in
			let M2 = propagateFMUActivation(M, name) in
				propagateActivations(M2, names \ {name})
	measure card names;



    -- This method should exchange data between all inputs and outputs
	-- Currently we do not respect inputs contract and feedthrough - we need to use a graph based approach to do this
    exchangeData: Importer -> Importer
    exchangeData(I) ==
	let connections = I.scenario.connections in
		let outputs = {output | output in set dom connections} in
			mu(I, scenario |-> propagateValues(I.scenario, outputs))
    post variablesSynchronzied(RESULT.scenario);

	/*
		Propagate the output value to the connected input using set and get actions
	 */
	propagateValues: Machine * set of FMURef -> Machine
	propagateValues(M, outputs)==
	if outputs = {} then M
	else let output in set outputs in 
		let mk_(fmuUpdated, val) = get_m(M.fmus(output.name), output.ref) in
			let input = M.connections(output) in
				let fmuInput = set_m(M.fmus(input.name), input.ref, val) in
					let M1 = mu(M, fmus |-> M.fmus ++ { fmuUpdated.name |-> fmuUpdated, fmuInput.name |-> fmuInput}) in
						propagateValues(M1, outputs \ {output})
	post variablesSynchronzied(RESULT)
	measure card outputs;



    initialization: Importer -> Importer 
    initialization(I) ==
        -- We don't care about parameters of the FMU, they are ignored

        /**
         * Exchange data between all connections in M
        */
        let I1 = exchangeData(I) in
        /**
         * Calculate a schedule for all timebased clocks
        */
        let I2 = createSchedule(I1) in
        /*
            Move all FMUs to Step Mode
         */
        enterStepMode(I2, dom I2.scenario.fmus)
	pre forall m' in set rng I.scenario.fmus & m'.mode = <INIT> 
		and card { fmu.time | fmu in set rng I.scenario.fmus } = 1
    post forall m' in set rng RESULT.scenario.fmus & m'.mode = <STEP> 
		and variablesSynchronzied(RESULT.scenario) 
		and card { fmu.time | fmu in set rng I.scenario.fmus } = 1;


	/*
		This function calculates the set of clocks that should be ticked
	 */
    tickingClocks: Importer -> Importer * set of FMURef
    tickingClocks(I) ==
		-- Clocks to tick at the current time
        let clocks = dom (I.schedule :> {I.time.r}) in
		-- Update the schedule for the ticked clocks to a new point in time
		--let updatedSchedule = clocks
		-- The clocks (input) are now relevant and their schedule is now updated
		let I1 = mu(I, relevantInputClocks |-> clocks) in --, schedule |-> updatedSchedule) in
		mk_(I1, clocks);
    cosimulationStep: Importer -> Importer 
    cosimulationStep(I) ==
        /*
            Detect events at the initial time
        */
        let mk_(I1, clocksToTick) = tickingClocks(I) in
		-- We should enter event mode if any clocks have ticked or any fmu wants to enter Event mode
		let eventDetected = clocksToTick <> {} or I1.fmusWithEvent <> {} in
        /*
            If any events are present we solve them
        */
		let I2 = (if eventDetected then clockedSimulationAlgorithm(I1, clocksToTick) else I) in
        /*
            Select a step duration
         */
        let stepsize = stepnegotiation(I2) in
        /*
            Perform the rest of the simulation
        */
		--simulationStep(I1, stepsize)
		I1
	pre forall m' in set rng I.scenario.fmus & m'.mode = <STEP> 
		and variablesSynchronzied(I.scenario) 
		and card { fmu.time | fmu in set rng I.scenario.fmus } = 1
    post forall m' in set rng RESULT.scenario.fmus & m'.mode = <STEP> 
		and variablesSynchronzied(RESULT.scenario) 
		and card { fmu.time | fmu in set rng I.scenario.fmus } = 1;


    -- Calculate an appropiate step duration that all FMUs can take
	-- This should be done dynamically
    stepnegotiation: Importer -> real
    stepnegotiation(M) ==
		is not yet specified
	post RESULT >= 0 
		and forall fmu in set rng M.scenario.fmus & fmu.maxStep >= RESULT;

	/*
		This is the recurrence relation from the paper - calculating the set of FMUs that must enter Event mode.
		We added the FMUs that has a ticking clock 
		And subsequently include all FMUs that has clock connection from an FMU in the event Mode 
	 */ 
	calculateRelevantFMUs: Machine * set of Name * set of Name * set of FMURef -> set of Name 
	calculateRelevantFMUs(M, relevantFMUs, notRelevantFMUs, clocks) == 
		let addedFMUs = {clock.name | clock in set clocks} union
			{M.clockConnections(con).name | con in set dom M.clockConnections & con.name in set relevantFMUs 
														and M.clockConnections(con).name in set notRelevantFMUs} in
		if addedFMUs = {} then relevantFMUs
		else 
			calculateRelevantFMUs(M, relevantFMUs union addedFMUs, notRelevantFMUs \ addedFMUs, clocks)
		pre relevantFMUs inter notRelevantFMUs = {}
		post {clock.name | clock in set clocks} subset RESULT 
			and RESULT subset dom M.fmus
		measure card notRelevantFMUs;

	/**
	 * Let M denote the set of FMU instances participating in the simulation. We
	 * assume that one FMU instance m in set M or the importer has requested to enter
	 * Event mode. Therefore we assume that every other instance m' in set M and m' <> m
	 * has been stepped up to the same super-dense time (t.r, 0).
	 */
	clockedSimulationAlgorithm: Importer * set of FMURef -> Importer
	clockedSimulationAlgorithm(I, clocks) ==
		/**
		 * 1. Every m in set M enters Event mode (super-dense time instant is t.i = 0);
		 */
		let relevantFMUs = calculateRelevantFMUs(I.scenario, I.fmusWithEvent, (dom I.scenario.fmus) \ I.fmusWithEvent, clocks) in
		let I1 = enterEventMode(I, relevantFMUs) in

		/**
		 * 2. Activate any time-based clocks scheduled to tick at (t.r , 0), by
		 *    invoking set_cm(_, wcm0) for any input or output clock wcm0 in set
		 *    Ucm union Ycm and any instance m in set M;
		 */

		let M2 = activateClocks(I1, clocks) in

		/**
		 * 3. Construct and solve system of equations for t.i :
		 * 		(a) For all ycm in set Ycm of any instance m in set M, forward activation state of
		 *		    triggered clocks:
		 * 			i. Invoke get_cm (_, ycm ), and set_cm(_, ucm0 ) or get_cm (_, ycm0 ),
		 *			   for any other clock ucm0 in set Ucm or ycm0 in set Ycm and instance
		 *			   m in set M that is transitively connected to ycm or has become active
		 *			   as a result of the clock activations;
		 *		(b) Invoke get_m(_, ym0) and set_m(_, um0, _) in the appropriate order,
		 *		    for any instance m in set M.
		 */

		let M3 = solveSystemOfEquations(M2) in
		--let M3 = propagateActivations(M2, dom M2.fmus) in

		/**
		 * 4. Invoke step_e(_) for m in set M (signals end of Event iteration t.i).
		*/
		let I4 = updateDiscreteState(M3, relevantFMUs) in

		/**
			* 5. Schedule clocks by invoking next_tm on every relevant clock, for m in set M.
		*/
		let I5 = updateSchedule(I4, relevantFMUs, { <tunable>, <changing>, <countdown> }) in

		/**
		 * 6. If any m in set M wishes to repeat the event iteration, or if a clock returned a
		 *    zero interval, go to Step 3 (start iteration t.i + 1).
			 if(M4.fmusWithEvent <> {} )

		 */
		enterStepMode(I5, relevantFMUs)

	pre card { fmu.time | fmu in set rng I.scenario.fmus } = 1;

solveSystemOfEquations : Importer -> Importer
solveSystemOfEquations(I)==
	is not yet specified;

updateSchedule: Importer * set of Name * set of Interval -> Importer
updateSchedule(I, fmus, intervalSet)==
	if fmus = {} then I 
	else
	let name in set fmus in
		let fmu = I.scenario.fmus(name) in
			let relevantClocks = {c.ref | c in set fmu.clocks & c.interval in set intervalSet} in  
				let scheduleEntries = calculateSchedule(fmu, relevantClocks, {|->}) in
					let I1 = mu(I, schedule |-> I.schedule ++ scheduleEntries) in
						updateSchedule(I1, fmus \ {name}, intervalSet)
measure card fmus;
-- All 

/*
	Calculate a schedule for all time based clocks
*/
createSchedule : Importer -> Importer
createSchedule(I) == 
	let fmus = dom I.scenario.fmus in 
		updateSchedule(I, fmus, {<constant>, <fixed>, <calculated>, <tunable>, <changing>, <countdown>})
pre forall m' in set rng I.scenario.fmus & m'.mode = <INIT>
post dom RESULT.schedule = 
	dunion {createFMURefs(fmu, {clock.ref | clock in set fmu.clocks & clock.type = <input> and clock.interval <> <triggered>}, {}) | fmu in set rng I.scenario.fmus };  -- all time-based clocks must have a schedule

calculateSchedule: FMU * set of Ref * map FMURef to real -> map FMURef to real
calculateSchedule(fmu, clocks, schedule)== 
	if clocks = {} then schedule
	else
	let clock in set clocks in
		let S1 = schedule ++ {mk_FMURef(fmu.name, clock) |-> next_tm(fmu, clock)} in
		calculateSchedule(fmu, clocks \ {clock}, S1)
measure card clocks; 



createFMURefs: FMU * set of Ref * set of FMURef -> set of FMURef
createFMURefs(fmu, clocks, res)==
	if clocks = {} then res
	else let clock in set clocks in
	createFMURefs(fmu, clocks \ {clock}, res union {mk_FMURef(fmu.name, clock)})
measure card clocks;

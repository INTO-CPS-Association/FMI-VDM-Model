/**
 * This file is part of the INTO-CPS toolchain.
 *
 * Copyright (c) 2017-2019, INTO-CPS Association,
 * c/o Professor Peter Gorm Larsen, Department of Engineering
 * Finlandsgade 22, 8200 Aarhus N.
 *
 * All rights reserved.
 *
 * THIS PROGRAM IS PROVIDED UNDER THE TERMS OF GPL VERSION 3 LICENSE OR
 * THIS INTO-CPS ASSOCIATION PUBLIC LICENSE VERSION 1.0.
 * ANY USE, REPRODUCTION OR DISTRIBUTION OF THIS PROGRAM CONSTITUTES
 * RECIPIENT'S ACCEPTANCE OF THE OSMC PUBLIC LICENSE OR THE GPL 
 * VERSION 3, ACCORDING TO RECIPIENTS CHOICE.
 *
 * The INTO-CPS toolchain  and the INTO-CPS Association Public License are
 * obtained from the INTO-CPS Association, either from the above address, from
 * the URLs: http://www.into-cps.org, and in the INTO-CPS toolchain distribution.
 * GNU version 3 is obtained from: http://www.gnu.org/copyleft/gpl.html.
 *
 * This program is distributed WITHOUT ANY WARRANTY; without
 * even the implied warranty of  MERCHANTABILITY or FITNESS FOR
 * A PARTICULAR PURPOSE, EXCEPT AS EXPRESSLY SET FORTH IN THE
 * BY RECIPIENT SELECTED SUBSIDIARY LICENSE CONDITIONS OF
 * THE INTO-CPS ASSOCIATION.
 *
 * See the full INTO-CPS Association Public License conditions for more details.
 */
 types
 /*
		The Importer runs the simulation
		It keeps track of the input and output to adapt to the behavior of the scenario
		It has a simulation time and values to exchange between the FMUs
	*/
	Importer :: 
		scenario : Machine
        schedule : map Name to real -- the time the clock should tick
		activeClocks : set of FMURef -- active clocks
		readyClocks : set of FMURef -- clocks that can be activated
		inactiveClocks : set of FMURef -- inactive clocks
		fmusWithEvent	: set of Name -- FMUs that triggered a state event
		relevantOutputClocks : set of FMURef -- All Output clocks the Importer must query since their state can have changed
		relevantInputClocks  : set of FMURef -- All Input clocks the Importer must update because their output changed 
		activeEquations : set of FMURef -- All active equations 
		calculatedEquations : set of FMURef -- The equations the Importer has calculated
		readyEquations : set of FMURef -- The equations that can be activated
		time : Time -- Simulation Time
		endtime : Time
		stepSize : real -- Planned step size
		valueMap : map FMURef to Value
	inv imp ==
		let fmus = imp.scenario.fmus in
		let inputclocks = dunion {createFMURefs(fmu, {clock.ref | clock in set fmu.clocks & clock.type = <input> }) | fmu in set rng fmus } in
		let outputclocks = dunion {createFMURefs(fmu, {clock.ref | clock in set fmu.clocks & clock.type = <output> }) | fmu in set rng fmus } in
		let clocks = inputclocks union outputclocks
		in
		--imp.activeClocks union imp.inactiveClocks = clocks  -- a clock is either inactive or active
		card (imp.activeClocks union imp.inactiveClocks) = card clocks
		and imp.activeClocks inter imp.inactiveClocks = {} -- a clock cannot be active and inactive
		and imp.activeClocks inter imp.readyClocks = {} -- an active clock can not be ready
		and imp.activeClocks = dunion {createFMURefs(fmu, fmu.activeClocks) | fmu in set rng fmus }
		and imp.fmusWithEvent subset dom fmus -- all fmus that wishes to enter event mode is in the scenario
		and imp.relevantInputClocks subset inputclocks -- a relevant input clock needs to be an input clock
		and imp.relevantOutputClocks subset outputclocks -- a relevant output clock needs to be an output clock
		and imp.relevantInputClocks inter imp.relevantOutputClocks = {}
		--and imp.activeEquations = dunion {fmu.activeEquations | fmu in set rng fmus} -- Equations are actived by the clocks
		--and dom imp.schedule = dunion {createFMURefs(fmu, {clock.ref | clock in set fmu.clocks & clock.type = <input> and clock.interval <> <triggered>}) | fmu in set rng fmus }-- We only have time based clocks in the schedule
		---and imp.activeEquations = {c.equations | c in set clocks & c.ref in set imp.activeClocks} -- Invariant for the paper TYPE MISMATCH
		and imp.endtime >= imp.time -- The simulation cannot not proceed once the endtime is reached
		and imp.activeEquations inter imp.readyEquations = {}
		and imp.readyEquations inter imp.calculatedEquations = {}
		;

functions
	createImporter : Machine * Time * real -> Importer
	createImporter(M, endtime, stepSize) ==
	let clocks = dunion {createFMURefs(fmu, {c.ref | c in set fmu.clocks}) | fmu in set rng M.fmus},  
		discreteVariables = dunion {createFMURefs(fmu, {v.ref | v in set (fmu.inputs union fmu.outputs) & v.type = <discrete>}) | fmu in set rng M.fmus}
	in
		mk_Importer(
			M,
			{|->},
			{},
			clocks,
			clocks,
			{},
			{},
			{},
			{},
			{},
			discreteVariables,
			mk_Time(0,0),
			endtime,
			stepSize,
			{|->}
		);

	runScenario: () -> Importer
	runScenario() ==
		let scenario = createScenario() in
		let I = createImporter(scenario, mk_Time(10,0), 5) in
		let I1 = initialization(I) in
		let I2 = cosimulationStep(I1) in
		terminate(I2);


	
	/*
		Function to run a set of actions (algorithm) until completion
		We pick an enabled action and perform it until the set of actions is empty
	*/
	runAlgorithm : Importer * set of Action -> Importer
	runAlgorithm(I, algorithm)==
		if algorithm = {} then I
		else
		let action in set  {action | action in set algorithm & isActionEnabled(I, action)} in
			--@Printf("Action to execute: %s\n", action)
			runAlgorithm(runAction(I, action), algorithm \ {action})
	measure card algorithm;
	
	/*
		Runs a single action
	*/
	runAction: Importer * Action -> Importer
	runAction(I, A)==
	let fmus = I.scenario.fmus in
	cases A:
		mk_Action(<get>, fmu, port) -> if fmus(fmu).mode = <EVENT> then calculateOutput(I, mk_FMURef(fmu, port)) else getPort(I, mk_FMURef(fmu, port)),
		mk_Action(<set>, fmu, port) -> if fmus(fmu).mode = <EVENT> then calculateInput(I, mk_FMURef(fmu, port)) else setPort(I, mk_FMURef(fmu, port)),
		mk_Action(<step>, fmu, -) -> stepFMU(I, I.scenario.fmus(fmu)),
		mk_Action(<setC>, fmu, clock) -> setClock(I, I.scenario.fmus(fmu), clock, true),
		mk_Action(<getC>, fmu, clock) -> queryClock(I, I.scenario.fmus(fmu), clock),
		others -> I -- Default match
	end;

	/**
	 * Update the discrete state of all FMUs.
	 */
	updateDiscreteState: Importer * set of Name -> Importer
	updateDiscreteState(I,fmus) ==
		let M = I.scenario in
		let res = {fmu |-> step_e(M.fmus(fmu)) | fmu in set fmus} in 
		let fmuMap = I.scenario.fmus ++ { fmu |-> res(fmu).#1 | fmu in set dom res}, 
			eventFMUs = { fmu | fmu in set dom res & res(fmu).#2 = true} -- fmus that triggered an event
		in
		mu(I, 
			scenario |-> mu(I.scenario, fmus |-> fmuMap),
			fmusWithEvent |-> eventFMUs,
			activeClocks |-> {},
			inactiveClocks |-> I.inactiveClocks union I.activeClocks,
			activeEquations |-> {},
			readyClocks |-> I.inactiveClocks union I.activeClocks,
			readyEquations |->  I.readyEquations union I.calculatedEquations union I.activeEquations,
			calculatedEquations |-> {},
			activeEquations |-> {}
		)
	pre
	let relevantFMUs = rng (fmus  <: I.scenario.fmus) in
		assertFMUMode(relevantFMUs, {<EVENT>})
		and fmusSynchronized(relevantFMUs)
	post 
	let newFmus = rng (fmus  <: RESULT.scenario.fmus),
		oldFMUs = rng (fmus  <: I.scenario.fmus)
	in
		assertFMUMode(newFmus, {<EVENT>})
		and fmusSynchronized(newFmus)
		and timeAdvanced(newFmus, oldFMUs, true);

    /**
	 * Move all relevant FMUs into event mode.
	 */
	enterEventMode: Importer * set of Name -> Importer
	enterEventMode(I, fmus) ==
		mu(I, scenario |-> mu(I.scenario, 
		fmus |-> I.scenario.fmus ++
			{ name |->  mu(I.scenario.fmus(name), mode |-> <EVENT>) | name in set dom I.scenario.fmus & name in set fmus }))
	pre assertFMUMode(rng (fmus <: I.scenario.fmus), {<STEP>})
		and I.activeClocks = {}
	post
		let fmusAffected = rng (fmus <: RESULT.scenario.fmus) in
		ImporterNotAffected(I, RESULT)
		and assertFMUMode(fmusAffected, {<EVENT>})
	 	and forall m' in set fmusAffected & m'.time.i = 0;

    /**
	 * Move all FMUs in M into Step mode.
	 */
	enterStepMode: Importer * set of Name -> Importer
	enterStepMode(I, fmus) ==
		mu(I, 
			scenario |-> mu(
							I.scenario, 
							fmus |-> I.scenario.fmus ++ { name |-> mu(I.scenario.fmus(name), mode |-> <STEP>, time |-> mk_Time(I.scenario.fmus(name).time.r, 0)) | name in set fmus }),
							time |-> mk_Time(I.time.r, 0)
			)
	pre 
		let relevantFMUs = rng (fmus <: I.scenario.fmus) in
		assertFMUMode(relevantFMUs, {<INIT>, <EVENT>})
		and I.activeClocks = {}
	post 
		ImporterNotAffected(I, RESULT) and
		let relevantFMUs = rng (fmus <: RESULT.scenario.fmus) in
			assertFMUMode(relevantFMUs, {<STEP>})
			and forall m' in set relevantFMUs & m'.time.i = 0;

	/**
	 * Activate the clocks in all FMUs that are due to tick at this time.
	 * All clocks should be input clocks
	 */
	activateClocks: Importer * set of FMURef -> Importer 
	activateClocks(I, clocks) ==
		if clocks = {} then I
		else 
		let clock in set clocks in
		let I1 = setClock(I, I.scenario.fmus(clock.name), clock.ref, true) in
		activateClocks(I1, clocks \ {clock})		
	pre clocks subset I.readyClocks inter I.relevantInputClocks
		and let fmusOfClocks = {I.scenario.fmus(clock.name) | clock in set clocks} in
		assertFMUMode(fmusOfClocks, {<EVENT>})
	post clocks subset RESULT.activeClocks
		and (clocks <> {} => not clocks  subset RESULT.readyClocks)
		and (clocks <> {} => not clocks  subset RESULT.relevantInputClocks)
		and let fmusOfClocks = {RESULT.scenario.fmus(clock.name) | clock in set clocks} in
		assertFMUMode(fmusOfClocks, {<EVENT>})
	measure card clocks;

	/*
	 * This is the initialization procedure that makes the scenario ready for simulation
	 */
    initialization: Importer -> Importer 
    initialization(I) ==
        -- We don't care about parameters of the FMU, they are ignored
        /**
         * Exchange data between all connections in M
        */
        let I1 = initializeData(I) in
        /**
         * Calculate a schedule for all timebased clocks
        */
        let I2 = createSchedule(I1) in
        /*
            Move all FMUs to Step Mode
         */
		-- @Printf("Initialization done\n")
        enterStepMode(I2, dom I2.scenario.fmus)
	pre 
		let fmus = rng I.scenario.fmus in
		assertFMUMode(fmus, {<INIT>})
		and fmusSynchronizedAtTime(fmus, I.time)
		and I.time = mk_Time(0,0)
    post 
		preCoSimulationStep(RESULT.scenario, RESULT.time)
		and RESULT.time = mk_Time(0,0);

    /*
		This function runs a co-simulation step after the step negotiation
		Currently we do not support algebraic loops
		It uses the runAlgorithm-function to execute the actions
	*/
    cosimStep: Importer -> Importer
    cosimStep(I) ==
	let connections = I.scenario.connections in
		let fmus = rng I.scenario.fmus in
		-- Calculate all the actions that should be performed in the co-simulation step
		let outputs = {mk_Action(<get>, output.name, output.ref) | output in set dom connections & derefOutput(I.scenario.fmus(output.name), output.ref).type = <continous>},
			inputs = {mk_Action(<set>, input.name, input.ref) | input in set rng connections & derefInput(I.scenario.fmus(input.name), input.ref).type = <continous>},
			steps = {mk_Action(<step>, fmu.name, 1) | fmu in set fmus} in
		let actions = outputs union inputs union steps in
			runAlgorithm(I, actions)
	pre 
		let scenario = I.scenario, fmus = rng scenario.fmus in
			assertFMUMode(fmus, {<STEP>}) 
                and variablesSynchronized(scenario, {<continous>}) 
                and fmusSynchronized(fmus)
    post 
		let scenario = RESULT.scenario, fmus = rng scenario.fmus in
			assertFMUMode(fmus, {<STEP>}) 
                and variablesSynchronized(scenario, {<continous>}) 
                and fmusSynchronized(fmus)
                and timeAdvanced(fmus, rng I.scenario.fmus, false);


	terminate : Importer -> Importer
	terminate(I) ==
	let fmus = dom I.scenario.fmus in
		mu(I, scenario |-> mu(I.scenario, fmus |-> { name |-> mu(I.scenario.fmus(name), mode |-> <DONE>) | name in set fmus }))
	pre preCoSimulationStep(I.scenario, I.endtime)
		and I.time.r = I.endtime.r
	post assertFMUMode(rng RESULT.scenario.fmus, {<DONE>})
		and fmusSynchronized(rng RESULT.scenario.fmus)
		and ImporterNotAffected(I,RESULT); 

    	/*
	 * This is the co-simulation step procedure that moves the scenario from time "t" to time "t+H"
	 */
    cosimulationStep: Importer -> Importer 
    cosimulationStep(I) ==
        /*
            Detect events at the initial time
        */
        let mk_(I1, clocksToTick) = tickingClocks(I) in
		-- We should enter event mode if any clocks have ticked or any fmu wants to enter Event mode
		let eventDetected = clocksToTick <> {} or I1.fmusWithEvent <> {} in
        /*
            If any events are present we solve them
        */
		let I2 = (if eventDetected then clockedSimulationAlgorithm(I1, clocksToTick) else I) in
        /*
            Select a step duration using step negotiation
         */
		let I3 = mu(I2, stepSize |-> stepnegotiation(I2)) in
        /*
            Perform the rest of the simulation
        */
		let I4 = cosimStep(I3) in
		-- @Printf("Cosimulation Step performed\n")
		let I5 = mu(I4, 
						scenario |-> mu(I4.scenario, fmus |-> { name |-> mu(I4.scenario.fmus(name), stepped |-> false) | name in set dom I4.scenario.fmus }),
						time |-> mk_Time(I4.time.r + I4.stepSize, I4.time.i)
					) in

		if I5.endtime > I5.time 
			then 
			-- @Printf("We run an extra iteration of the co-simulation step\n")
			cosimulationStep(I5) 
		else I5
	pre preCoSimulationStep(I.scenario, I.time)
    post preCoSimulationStep(RESULT.scenario, RESULT.time) 
		and I.time.r <= RESULT.time.r
		and	ImporterNotAffected(I, RESULT)
		and 
		let M' = rng RESULT.scenario.fmus,
			M = rng I.scenario.fmus in
         	timeAdvanced(M', M, false)
	measure I.endtime.r - I.time.r;

    -- Calculate an appropiate step duration that all FMUs can take
	-- This should be done dynamically
    stepnegotiation: Importer -> real
    stepnegotiation(I) ==
		let maxSteps = {fmu.maxStep | fmu in set rng I.scenario.fmus},
		 	scheduling = {t - I.time.r | t in set rng I.schedule},
			timeLeft = I.endtime.r - I.time.r in
		selectMinStep(maxSteps union scheduling union {timeLeft})
	post RESULT >= 0 
		and forall fmu in set rng I.scenario.fmus & fmu.maxStep >= RESULT;

    
	/*
		Performing the actions from the Importer's perspective
	*/
    	/*
	 * This function queries the clock to check its activation status
	 */
	queryClock: Importer * FMU * Ref -> Importer
	queryClock(I, fmu, clock)==
		let mk_(fmuUpdated, val) = get_cm(fmu, clock),
			fmuref = mk_FMURef(fmu.name, clock) in

		let connectedClock = I.scenario.clockConnections(fmuref) in
		let I1 = mu(I, 
			valueMap |-> I.valueMap ++ {connectedClock |-> val},
			relevantOutputClocks |-> I.relevantOutputClocks \ {fmuref},
			relevantInputClocks |-> I.relevantInputClocks union {connectedClock}
		) in
		updateEnvironmentClock(I1, fmuUpdated, clock, val.value)
	pre mk_FMURef(fmu.name,clock) in set I.relevantOutputClocks -- a clock should be relevant if we should query it
		and clock in set {c.ref | c in set fmu.clocks & c.type = <output>}
		and	preGetCI(I, fmu, clock)
	post mk_FMURef(fmu.name,clock) not in set RESULT.relevantOutputClocks;

	/*
	 * This function sets the activation status of the clock
	 */
	setClock : Importer * FMU * Ref * bool -> Importer
	setClock(I, fmu, clock, val)==
		let fmuUpdated = set_cm(fmu,clock, val),
			fmuref = mk_FMURef(fmu.name, clock) in
		let feedthroughClocks = {mk_FMURef(fmu.name, c.ref) | c in set fmu.clocks & clock in set c.dependsOn} in
		let I1 = mu(I,
			valueMap |-> {fmuref} <-: I.valueMap,
			relevantInputClocks |-> I.relevantInputClocks \ {fmuref}, -- remain the same
			relevantOutputClocks |-> I.relevantOutputClocks union feedthroughClocks -- Other clocks might have been activated
		) in
		updateEnvironmentClock(I1, fmuUpdated, clock, val)
	pre 
	let fmuRef = mk_FMURef(fmu.name,clock) in 
		fmuRef in set I.relevantInputClocks -- a clock should be relevant if we should query it
		and (val <=> fmuRef in set I.inactiveClocks inter I.readyClocks)
		and fmu.mode = <EVENT>
	post 
	let fmuRef = mk_FMURef(fmu.name,clock) in 
		fmuRef not in set RESULT.relevantInputClocks
		and card RESULT.relevantInputClocks < card I.relevantInputClocks
		and RESULT.scenario.fmus(fmu.name).mode = <EVENT>
		and (val <=> (fmuRef in set RESULT.activeClocks) and fmuRef not in set (RESULT.readyClocks union RESULT.inactiveClocks));

	-- This action obtains a value and set it on an input		
	setPort : Importer * FMURef -> Importer
	setPort(I, port)==
		let value = I.valueMap(port) in
			let fmu = set_m(I.scenario.fmus(port.name), port.ref, value) in 
				mu(I,
					scenario |-> mu(I.scenario, fmus |-> I.scenario.fmus ++ {fmu.name |-> fmu}), 
					valueMap |-> {port} <-: I.valueMap
				)
	pre preSetI(I, I.scenario.fmus(port.name), port.ref)
	post fmusNotAffected(rng ({port.name} <-: I.scenario.fmus), rng ({port.name} <-: RESULT.scenario.fmus));

	--post All Other FMUs are not affect and time is not progressed

	-- This action obtains a value from an output		
	getPort : Importer * FMURef -> Importer
	getPort(I, port)==
		let mk_(fmu, val) = get_m(I.scenario.fmus(port.name), port.ref), 
		 	connectedInput = I.scenario.connections(port) in
			mu(I, 
				scenario |-> mu(I.scenario, fmus |-> I.scenario.fmus ++ {fmu.name |-> fmu}),
				valueMap |-> I.valueMap ++ {connectedInput |-> val}
			)
	pre preGetI(I, I.scenario.fmus(port.name), port.ref)
	post fmusNotAffected(rng ({port.name} <-: I.scenario.fmus), rng ({port.name} <-: RESULT.scenario.fmus));

	/*
	 * This action progresses an FMU in time
	 * It also keeps track of the FMUs that triggers an event and adjust the step size dynamically
	 */
	stepFMU : Importer * FMU -> Importer
	stepFMU(I, fmu)==
		let mk_(fmuUpdated, step, event) = step_tm(fmu, I.stepSize) in
			mu(I, 
				scenario |-> mu(I.scenario, fmus |-> I.scenario.fmus ++ {fmuUpdated.name |-> fmuUpdated}),
				fmusWithEvent |-> if event then I.fmusWithEvent union {fmu.name} else I.fmusWithEvent,
				stepSize |-> step -- adjust the stepsize 
			)
	pre preStepI(I, fmu)
	post fmusNotAffected(rng ({fmu.name} <-: I.scenario.fmus), rng ({fmu.name} <-: RESULT.scenario.fmus));
/*********************************************************************************
 *
 *	Copyright (c) 2017-2022, INTO-CPS Association,
 *	c/o Professor Peter Gorm Larsen, Department of Engineering
 *	Finlandsgade 22, 8200 Aarhus N.
 *
 *	MIT Licence:
 *
 *	Permission is hereby granted, free of charge, to any person obtaining a copy of
 *	this software and associated documentation files (the "Software"), to deal in
 *	the Software without restriction, including without limitation the rights to use,
 *	copy, modify, merge, publish, distribute, sublicense, and/or sell copies of the
 *	Software, and to permit persons to whom the Software is furnished to do so,
 *	subject to the following conditions:
 *
 *	The above copyright notice and this permission notice shall be included in all
 *	copies or substantial portions of the Software.
 *
 *	THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED,
 *	INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A
 *	PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT
 *	HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION
 *	OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE
 *	SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
 *
 *	SPDX-License-Identifier: MIT
 *
 *********************************************************************************/

/**
 * 3.2.2 Evaluation of Model Equations
 */
functions
	/**
	 * Implementation and API for fmi3EnterEventMode.
	 */
	fmi3EnterEventMode_Impl: fmi3Instance +> fmi3Status * fmi3Instance
	fmi3EnterEventMode_Impl(c) ==
		mk_(<fmi3OK>, mu(c,
			fmuState |-> <EVENT_MODE>))
	pre
		mk_(c.fmuKind, c.fmuState) in set STATES_fmi3EnterEventMode
	post
		RESULT.#2.fmuState = <EVENT_MODE>;

	fmi3EnterEventMode(
		 c					: fmi3Instance,
         -					: fmi3Boolean,
         -					: fmi3Boolean,
         rootsFound			: seq of int,
         nEventIndicators	: nat,
         -					: fmi3Boolean) result: fmi3Status * [fmi3Instance]
	==
		if mk_(c.fmuKind, c.fmuState) not in set STATES_fmi3EnterEventMode
		then logAPIError(c, "Cannot call fmi3EnterEventMode in this state", mk_(<fmi3Error>, nil))

		elseif nEventIndicators <> len rootsFound
		then logAPIError(c, "nEventIndicators does not match rootsFound", mk_(<fmi3Error>, nil))

		else fmi3EnterEventMode_Impl(c)
	 post
	 	result.#1 <> <fmi3OK> <=> result.#2 = nil;


	/**
	 * Implementation and API for fmi3NewDiscreteStates.
	 */
	fmi3NewDiscreteStates_Impl: fmi3Instance +> fmi3Status * [fmi3EventInfo] * [fmi3Instance]
	fmi3NewDiscreteStates_Impl(c) ==
    	c.instanceEnvironment.calculateEvents(c)
	pre
		mk_(c.fmuKind, c.fmuState) in set STATES_fmi3NewDiscreteStates;

	fmi3NewDiscreteStates: fmi3Instance +> fmi3Status * [fmi3EventInfo] * [fmi3Instance]
	fmi3NewDiscreteStates(c) ==
		if mk_(c.fmuKind, c.fmuState) not in set STATES_fmi3NewDiscreteStates
		then logAPIError(c, "Cannot call fmi3NewDiscreteStates in this state", mk_(<fmi3Error>, nil, nil))
	 	else fmi3NewDiscreteStates_Impl(c);


	/**
	 * Implementation and API for fmi3EnterContinuousTimeMode.
	 */
	fmi3EnterContinuousTimeMode_Impl: fmi3Instance +> fmi3Status * fmi3Instance
	fmi3EnterContinuousTimeMode_Impl(c) ==
		mk_(<fmi3OK>, mu(c,
			fmuState |-> <CONTINUOUS_TIME_MODE>))
	pre
		mk_(c.fmuKind, c.fmuState) in set STATES_fmi3EnterContinuousTimeMode
	post
		RESULT.#2.fmuState = <CONTINUOUS_TIME_MODE>;

	fmi3EnterContinuousTimeMode: fmi3Instance +> fmi3Status * [fmi3Instance]
	fmi3EnterContinuousTimeMode(c) ==
		if mk_(c.fmuKind, c.fmuState) not in set STATES_fmi3EnterContinuousTimeMode
		then logAPIError(c, "Cannot call fmi3EnterContinuousTimeMode in this state", mk_(<fmi3Error>, nil))
		else fmi3EnterContinuousTimeMode_Impl(c)
	 post
	 	RESULT.#1 <> <fmi3OK> <=> RESULT.#2 = nil;


	/**
	 * Implementation and API for fmi3EnterStepMode.
	 */
	fmi3EnterStepMode_Impl: fmi3Instance +> fmi3Status * fmi3Instance
	fmi3EnterStepMode_Impl(c) ==
		mk_(<fmi3OK>, mu(c,
			fmuState |-> <STEP_MODE>))
	pre
		mk_(c.fmuKind, c.fmuState) in set STATES_fmi3EnterStepMode
	post
		RESULT.#2.fmuState = <STEP_MODE>;

	fmi3EnterStepMode: fmi3Instance +> fmi3Status * [fmi3Instance]
	fmi3EnterStepMode(c) ==
		if mk_(c.fmuKind, c.fmuState) not in set STATES_fmi3EnterStepMode
		then logAPIError(c, "Cannot call fmi3EnterStepMode in this state", mk_(<fmi3Error>, nil))
		else fmi3EnterStepMode_Impl(c)
	 post
	 	RESULT.#1 <> <fmi3OK> <=> RESULT.#2 = nil;


	/**
	 * Implementation and API for fmi3CompletedIntegratorStep.
	 */
	fmi3CompletedIntegratorStep_Impl: fmi3Instance * fmi3Boolean +> fmi3Status * fmi3Boolean * fmi3Boolean
    fmi3CompletedIntegratorStep_Impl(c, -) ==
		let mk_(s, enterEventMode, terminateSimulation) = mk_(<fmi3OK>, false, false) in	-- TODO calculate?
			mk_(s, enterEventMode, terminateSimulation)
	pre
		mk_(c.fmuKind, c.fmuState) in set STATES_fmi3CompletedIntegratorStep;
		-- and not nilBool(c.xmlModel.modelExchange.completedIntegratorStepNotNeeded);

	fmi3CompletedIntegratorStep(
		c									: fmi3Instance,
        noSetFMUStatePriorToCurrentPoint	: fmi3Boolean) result : fmi3Status * [fmi3Boolean] * [fmi3Boolean]
	==
		if mk_(c.fmuKind, c.fmuState) not in set STATES_fmi3CompletedIntegratorStep
		then logAPIError(c, "Cannot call fmi3CompletedIntegratorStep in this state", mk_(<fmi3Error>, nil, nil))

--		elseif nilBool(c.xmlModel.modelExchange.completedIntegratorStepNotNeeded)
--		then logAPIError(c, "fmi3CompletedIntegratorStep not needed", mk_(<fmi3Error>, nil, nil))

	 	else fmi3CompletedIntegratorStep_Impl(c, noSetFMUStatePriorToCurrentPoint)
	 post
	 	result.#1 = <fmi3OK> <=> result.#2 <> nil and result.#3 <> nil;


	/**
	 * Implementation and API for fmi3GetDerivatives.
	 */
	fmi3GetDerivatives_Impl: fmi3Instance * nat +> fmi3Status * seq of fmi3Float
	fmi3GetDerivatives_Impl(c, nx) ==
		-- Compute derivatives TODO
		mk_(<fmi3OK>, conc [ c.variables(vref) | vref in seq c.continuousTimeStates ])
	pre
		mk_(c.fmuKind, c.fmuState) in set STATES_fmi3GetDerivatives
		and nx = len c.continuousTimeStates;

	fmi3GetDerivatives: fmi3Instance * nat +> fmi3Status * [seq of fmi3Float]
	fmi3GetDerivatives(c, nx) ==
		if mk_(c.fmuKind, c.fmuState) not in set STATES_fmi3GetDerivatives
		then logAPIError(c, "Cannot call fmi3GetDerivatives in this state", mk_(<fmi3Error>, nil))

		elseif nx <> len c.continuousTimeStates
		then logAPIError(c, "Wrong number of derivates requested", mk_(<fmi3Error>, nil))

	 	else fmi3GetDerivatives_Impl(c, nx)
	 post
	 	RESULT.#1 = <fmi3OK> <=> RESULT.#2 <> nil;


	/**
	 * Implementation and API for fmi3GetEventIndicators.
	 */
	fmi3GetEventIndicators_Impl: fmi3Instance * nat +> fmi3Status * seq of fmi3Float
	fmi3GetEventIndicators_Impl(c, ni) ==
		-- Compute event indicators TODO
		mk_(<fmi3OK>, [ 0 | - in set {1, ..., ni} ])
	pre
		mk_(c.fmuKind, c.fmuState) in set STATES_fmi3GetEventIndicators
		and ni = len nilSeq[EventIndicator](c.xmlModel.modelStructure.eventIndicator);

	fmi3GetEventIndicators: fmi3Instance * nat +> fmi3Status * [seq of fmi3Float]
	fmi3GetEventIndicators(c, ni) ==
		if mk_(c.fmuKind, c.fmuState) not in set STATES_fmi3GetEventIndicators
		then logAPIError(c, "Cannot call fmi3GetEventIndicators in this state", mk_(<fmi3Error>, nil))

		elseif ni <> len nilSeq[EventIndicator](c.xmlModel.modelStructure.eventIndicator)
		then logAPIError(c, "Wrong number of event indicators passed", mk_(<fmi3Error>, nil))

	 	else fmi3GetEventIndicators_Impl(c, ni)
	 post
	 	RESULT.#1 = <fmi3OK> <=> RESULT.#2 <> nil;


	/**
	 * Implementation and API for fmi3GetContinuousStates.
	 */
	fmi3GetContinuousStates_Impl: fmi3Instance * nat +> fmi3Status * seq of fmi3Float
	fmi3GetContinuousStates_Impl(c, nx) ==
		mk_(<fmi3OK>, conc [ c.variables(vref) | vref in seq c.continuousTimeStates ])
	pre
		mk_(c.fmuKind, c.fmuState) in set STATES_fmi3GetContinuousStates
		and nx = len c.continuousTimeStates;

	fmi3GetContinuousStates: fmi3Instance * nat +> fmi3Status * [seq of fmi3Float]
	fmi3GetContinuousStates(c, nx) ==
		if mk_(c.fmuKind, c.fmuState) not in set STATES_fmi3GetContinuousStates
		then logAPIError(c, "Cannot call fmi3GetContinuousStates in this state", mk_(<fmi3Error>, nil))

		elseif nx <> len c.continuousTimeStates
		then logAPIError(c, "Wrong number of derivates requested", mk_(<fmi3Error>, nil))

	 	else fmi3GetContinuousStates_Impl(c, nx)
	 post
	 	RESULT.#1 = <fmi3OK> <=> RESULT.#2 <> nil;


	/**
	 * Implementation and API for fmi3GetNominalsOfContinuousStates.
	 */
	fmi3GetNominalsOfContinuousStates_Impl: fmi3Instance * nat +> fmi3Status * seq of fmi3Float
	fmi3GetNominalsOfContinuousStates_Impl(c, nx) ==
		mk_(<fmi3OK>, [ 1.0 | - in set {1, ..., nx}])	-- TODO via calculate function?
	pre
		mk_(c.fmuKind, c.fmuState) in set STATES_fmi3GetNominalsOfContinuousStates
		and nx = len c.continuousTimeStates;

	fmi3GetNominalsOfContinuousStates: fmi3Instance * nat +> fmi3Status * [seq of fmi3Float]
	fmi3GetNominalsOfContinuousStates(c, nx) ==
		if mk_(c.fmuKind, c.fmuState) not in set STATES_fmi3GetNominalsOfContinuousStates
		then logAPIError(c, "Cannot call fmi3GetNominalsOfContinuousStates in this state", mk_(<fmi3Error>, nil))

		elseif nx <> len c.continuousTimeStates
		then logAPIError(c, "Wrong number of derivates requested", mk_(<fmi3Error>, nil))

	 	else fmi3GetNominalsOfContinuousStates_Impl(c, nx)
	 post
	 	RESULT.#1 = <fmi3OK> <=> RESULT.#2 <> nil;

/**
 * This file is part of the INTO-CPS toolchain.
 *
 * Copyright (c) 2017-2019, INTO-CPS Association,
 * c/o Professor Peter Gorm Larsen, Department of Engineering
 * Finlandsgade 22, 8200 Aarhus N.
 *
 * All rights reserved.
 *
 * THIS PROGRAM IS PROVIDED UNDER THE TERMS OF GPL VERSION 3 LICENSE OR
 * THIS INTO-CPS ASSOCIATION PUBLIC LICENSE VERSION 1.0.
 * ANY USE, REPRODUCTION OR DISTRIBUTION OF THIS PROGRAM CONSTITUTES
 * RECIPIENT'S ACCEPTANCE OF THE OSMC PUBLIC LICENSE OR THE GPL 
 * VERSION 3, ACCORDING TO RECIPIENTS CHOICE.
 *
 * The INTO-CPS toolchain  and the INTO-CPS Association Public License are
 * obtained from the INTO-CPS Association, either from the above address, from
 * the URLs: http://www.into-cps.org, and in the INTO-CPS toolchain distribution.
 * GNU version 3 is obtained from: http://www.gnu.org/copyleft/gpl.html.
 *
 * This program is distributed WITHOUT ANY WARRANTY; without
 * even the implied warranty of  MERCHANTABILITY or FITNESS FOR
 * A PARTICULAR PURPOSE, EXCEPT AS EXPRESSLY SET FORTH IN THE
 * BY RECIPIENT SELECTED SUBSIDIARY LICENSE CONDITIONS OF
 * THE INTO-CPS ASSOCIATION.
 *
 * See the full INTO-CPS Association Public License conditions for more details.
 */

/**
 * 2.2.8 Definition of Model Variables (ModelVariables)
 */
types
	Variable =
		Float64 | Float32 |
		Int64 | Int32 | Int16 | Int8 |
		UInt64 | UInt32 | UInt16 | UInt8 |
		Boolean | String | Binary | Enumeration | Clock;

	-- Possible type for a Variable
	FloatKind = <Float64> | <Float32>;
	IntegerKind = <Int64> | <Int32> | <Int16> | <Int8> | <UInt64> | <UInt32> | <UInt16> | <UInt8>;
	Kind = FloatKind | IntegerKind | <Boolean> | <String> | <Binary> | <Enumeration> | <Clock>;

functions
	/**
	 * Turn a Variable into a Kind.
	 */
	kindOf: Variable | VariableType +> Kind
	kindOf(sv) ==
		if is_(sv, Float32) then <Float32>
		else if is_(sv, Float64) then <Float64>
		else if is_(sv, Int8) then <Int8>
		else if is_(sv, UInt8) then  <UInt8>
		else if is_(sv, Int16) then<Int16>
		else if is_(sv, UInt16) then <UInt16>
		else if is_(sv, Int32) then <Int32>
		else if is_(sv, UInt32) then <UInt32>
		else if is_(sv, Int64) then <Int64>
		else if is_(sv, UInt64) then <UInt64>
		else if is_(sv, Boolean) then <Boolean>
		else if is_(sv, String) then <String>
		else if is_(sv, Binary) then <Binary>
		else if is_(sv, Enumeration) then <Enumeration>
		else if is_(sv, Clock) then <Clock>

		else if is_(sv, Float32Type) then <Float32>
		else if is_(sv, Float64Type) then <Float64>
		else if is_(sv, Int8Type) then <Int8>
		else if is_(sv, UInt8Type) then  <UInt8>
		else if is_(sv, Int16Type) then<Int16>
		else if is_(sv, UInt16Type) then <UInt16>
		else if is_(sv, Int32Type) then <Int32>
		else if is_(sv, UInt32Type) then <UInt32>
		else if is_(sv, Int64Type) then <Int64>
		else if is_(sv, UInt64Type) then <UInt64>
		else if is_(sv, BooleanType) then <Boolean>
		else if is_(sv, StringType) then <String>
		else if is_(sv, BinaryType) then <Binary>
		else if is_(sv, EnumerationType) then <Enumeration>
		else if is_(sv, ClockType) then <Clock>

		else undefined;

	/**
	 * Ordering for comparison of Variability values.
	 */
	orderOf: Variability +> fmi3ValueReference
	orderOf(value) ==
		cases value:
			<constant>   -> 1,
			<fixed>      -> 2,
			<tunable>    -> 3,
			<discrete>   -> 4,
			<continuous> -> 5
		end;

	/**
	 * Lookup a valueReference in the effective variable list.
	 */
	lookup: seq1 of Variable * fmi3ValueReference +> [Variable]
	lookup(svs, vr) ==
		let found = { v | v in seq svs & v.valueReference = vr } in
			if found = {}
			then nil
			else let {v} = found in v;

	/**
	 * Invariant helper for min/max/start checks on Real, Integer and Enumeration.
	 */
	isInRange[@T]: [@T] * [@T] * [seq1 of @T] * (Variable | VariableType) * [Initial] +> bool
	isInRange(min, max, start, kind, initial) ==
		let mk_(kmin, kmax) = minMaxKind(kindOf(kind)) in
		{
			max <> nil =>
				--@OnFail(1450, "2.2.8 max %s is not a valid %s", max, kind)
				( max <= kmax and max >= kmin ),

			min <> nil =>
				--@OnFail(1451, "2.2.8 min %s is not a valid %s", min, kind)
				( min <= kmax and min >= kmin ),

			start <> nil =>
				--@OnFail(1452, "2.2.8 start %s is not a valid %s", start, kind)
				( forall s in seq start & s <= kmax and s >= kmin ),

			max <> nil and min <> nil =>
				--@OnFail(1453, "2.2.8 min %s is not <= max %s", min, max)
				( max >= min ),

			start <> nil =>
				--@OnFail(1454, "2.2.8 start %s is not within min %s/max %s", start, min, max)
				( (min <> nil => forall s in seq start & min <= s) and
				  (max <> nil => forall s in seq start & max >= s) ),

			start = nil and initial not in set {<calculated>, <independent>} =>
				--@OnFail(1455, "2.2.8 Warning: implicit start of 0 is not within min %s/max %s", min, max)
				( (min <> nil => min <= 0) and
				  (max <> nil => max >= 0) ) or true	-- NOTE warning
		} = {true}
	pre is_(min, [real]) and is_(max, [real]) and is_(start, [seq1 of real]);	-- ie. numerics

	/**
	 * Return the MININT-MAXINT values for a particular numeric type
	 */
	minMaxKind: Kind +> real * real
	minMaxKind(kind) ==
		cases kind:
			<Float32>		-> mk_(-3.402823466385288598e+38, 3.402823466385288598e+38),
			<Float64>		-> mk_(-1.797693134862315708e+308, 1.797693134862315708e+308),
			<Int8>			-> mk_(-128, 127),
			<UInt8>			-> mk_(0, 255),
			<Int16>			-> mk_(-32768, 32767),
			<UInt16>		-> mk_(0, 65535),
			<Int32>			-> mk_(-2147483648, 2147483647),
			<UInt32>		-> mk_(0, 4294967295),
			<Int64>			-> mk_(-9.223372036854775808e+18, 9.223372036854775807e+18),
			<UInt64>		-> mk_(0, 1.8446744073709551615e+19),
			<Enumeration>	-> mk_(0, 4294967295),	-- UInt32?
			others			-> undefined
		end;

	/**
	 * Check whether a type is a "real" type.
	 */
	isReal: Variable +> bool
	isReal(var) ==
		is_Float64(var) or is_Float32(var);

	/**
	 * Check whether a type is an "integer" type.
	 */
	isInteger: Variable +> bool
	isInteger(var) ==
		is_Int64(var) or is_Int32(var) or is_Int16(var) or is_Int8(var) or
		is_UInt64(var) or is_UInt32(var) or is_UInt16(var) or is_UInt8(var);

	/**
	 * Verify a sequence of raw (NOT effective) Variables.
	 */
	isValidModelVariables: seq1 of Variable +> bool
	isValidModelVariables(svs) ==
		{
			/* @OnFail(1456, "2.2.8 Variables define more than one independent variable: %s",
				{ sv.name | sv in seq svs & sv.causality = <independent> }) */
			( card { sv | sv in seq svs & sv.causality = <independent> } <= 1 ),

			/* @OnFail(1457, "2.2.8 Variable names are not unique: %s",
				{ svs(a).name | a, b in set inds svs &
					a <> b and svs(a).name = svs(b).name } ) */
			( card { sv.name | sv in seq svs } = len svs ),

			isValidVariableDimension(svs)
		}
		union
		{
			-- @OnFail(1459, "2.2.8 Variable %s invalid at %s", sv.name, loc2str(sv.location))
			( isValidVariableAttributes(sv) )

			| sv in seq svs
		} = {true};

	/**
	 * Variable attribute rules, defined in 2.2.8.
	 */
	isValidVariableAttributes: Variable +> bool
	isValidVariableAttributes(sv) ==
		let eCausality   = effectiveCausality(sv.causality),
			eVariability = effectiveVariability(sv),
			eInitial     = effectiveInitial(sv, eCausality, eVariability)
		in
		{
			/* @OnFail(1460, "2.2.8 Variable %s causality/variability/initial/start %s/%s/%s/%s invalid at %s",
				sv.name, eCausality, eVariability, eInitial, sv.start, loc2str(sv.location)) */
			(
				cases eCausality:
					<structuralParameter> ->
						eVariability in set {<fixed>, <tunable>}
						and eInitial = <exact>,	-- (A)

					<parameter> ->
						eVariability in set {<fixed>, <tunable>}
						and eInitial = <exact>,	-- (A)

					<calculatedParameter> ->
						eVariability in set {<fixed>, <tunable>}
						and eInitial in set {<approx>, <calculated>},	-- (B)

					<input> ->
						(eVariability in set {<discrete>, <continuous>}
						 -- and eInitial = nil
						 and sv.start <> nil),	-- (D)

					<independent> ->
						eVariability = <continuous>
						and eInitial = nil		-- (D)
						and sv.start = nil
						-- @OnFail(1461, "2.2.8 Independent variable must be Float at %s", loc2str(sv.location))
						and (is_Float64(sv) or is_Float32(sv)),

					<output> ->
						cases eVariability:
							<constant> ->
								eInitial in set {<exact>},	-- (A)

							<discrete>,
							<continuous> ->
								eInitial in set { <exact>, <approx>, <calculated> }, -- (C)

							others -> false
						end,

					<local> ->
						cases eVariability:
							<constant> ->
								eInitial = <exact>,	-- (A)

							<fixed>,
							<tunable> ->
								eInitial in set { <calculated>, <approx> },	-- (B)

							<discrete>,
							<continuous> ->
								eInitial in set { <exact>, <approx>, <calculated> }, -- (C)

							others -> false
						end
				end
			),

			/* @OnFail(1462, "2.2.8 Variable %s variability/causality %s/%s invalid at %s",
				sv.name, eVariability, eCausality, loc2str(sv.location)) */
			(
				cases eVariability:
					<constant> ->
						eCausality in set {<output>, <local>},

					<fixed>, <tunable> ->
						eCausality in set {<structuralParameter>, <parameter>, <calculatedParameter>, <local>},

					<discrete> ->
						eCausality in set {<input>, <output>, <local>},

					<continuous> ->
						eCausality in set {<input>, <output>, <local>, <independent>}
						-- @OnFail(1463, "2.2.8 Continuous variable must be Real at %s", loc2str(sv.location))
						and (is_Float64(sv) or is_Float32(sv))
				end
			),

			/* @OnFail(1464, "2.2.8 Variable %s initial/causality %s/%s invalid at %s",
				sv.name, sv.initial, eCausality, loc2str(sv.location)) */
			(
				sv.initial <> nil =>
					eCausality <> <independent>
			),

			/* @OnFail(1499, "2.2.8 Clock variable %s cannot define initial or start at %s",
				sv.name, loc2str(sv.location)) */
			(
				is_Clock(sv) => sv.initial = nil and sv.start = nil
			),

			/* @OnFail(1465, "2.2.8 Variable %s initial/variability/start %s/%s/%s invalid at %s",
				sv.name, eInitial, eVariability, sv.start, loc2str(sv.location)) */
			(
				cases eInitial:
					<exact> ->
						sv.start <> nil,

					<approx> ->
						sv.start <> nil
						and eVariability <> <constant>,

					<calculated> ->
						sv.start = nil
						and eVariability <> <constant>,

					nil ->		-- Note that eInitial can be nil
						true	-- Tests on eInitial above are sufficient
				end
			),

			-- @OnFail(1466, "2.2.8 Variable %s VendorAnnotations invalid", sv.name)
			( isValidVendorAnnotation(sv.annotations) )
		} = {true};

	/**
	 * Check (effective) Clock attributes and references.
	 */
	isValidClockAttributes: Variable * seq1 of Variable +> bool
	isValidClockAttributes(sv, svs) ==
		if is_(sv, Clock)
		then
			-- @OnFail(1488, "Clock variable cannot have previous field set at %s", loc2str(sv.location))
			( sv.previous = nil )

		else
			{
				sv.clocks <> nil =>
					-- @OnFail(1486, "Clocked vars can only be discrete or tunable at %s", loc2str(sv.location))
					( sv.variability = <discrete> or sv.variability = <tunable> ),

				sv.previous <> nil =>
					let p = lookup(svs, sv.previous) in
					{
						-- @OnFail(1489, "Variable previous setting unknown at %s", loc2str(sv.location))
						( p <> nil ),

						-- @OnFail(1490, "Variable previous setting wrong kind at %s", loc2str(sv.location))
						( kindOf(p) = kindOf(sv) )
					} = {true}
			}
			union
			{
				let cvar = lookup(svs, cref) in
					-- @OnFail(1487, "Clock reference %s not found at %s", cvar.valueReference, loc2str(sv.location))
					( cvar <> nil )

					-- @OnFail(1488, "Clock reference %s is not a Clock at %s", cvar.valueReference, loc2str(sv.location))
					and ( is_(cvar, Clock) )

				| cref in seq nilSeq[nat](sv.clocks)
			} = {true};

	/**
	 * Check Dimension structures.
	 */
	isValidVariableDimension: [seq1 of Variable] +> bool
	isValidVariableDimension(vars) ==
		vars <> nil => dunion
		{
			{
				-- @OnFail(1467, "2.2.8 Dimension of %s cannot define start and valueReference", v.name)
				( v.dimension <> nil =>
					forall dim in seq v.dimension &
						dim.start = nil <=> dim.valueReference <> nil ),

				-- @OnFail(1468, "2.2.8 Dimension of %s cannot reference itself", v.name)
				( v.dimension <> nil =>
					forall dim in seq v.dimension &
						dim.valueReference <> nil =>
							dim.valueReference <> v.valueReference ),

				-- @OnFail(1469, "2.2.8 Dimension of %s, valueReference is not Integer, constant or structuralParameter, with start", v.name)
				( v.dimension <> nil =>
					forall dim in seq v.dimension &
						dim.valueReference <> nil =>
							exists r in seq vars &
								isInteger(r) and
								r.valueReference = dim.valueReference
								and ( r.causality = <structuralParameter> or r.variability = <constant> )
								and r.start <> nil and len r.start = 1 and r.start(1) >= 0 ),

				-- @OnFail(1470, "2.2.8 Structural parameter %s cannot have Dimension", v.name)
				( v.dimension <> nil =>
					v.causality <> <structuralParameter> )
			}

			| v in seq vars
		}
		= {true};

	/**
	 * Vaidity checks that depend on inherited values from any declaredType - ie. passed
	 * effective Variables.
	 */
	isValidEffectiveVariables: seq1 of Variable +> bool
	isValidEffectiveVariables(svs) ==
	{
		-- @OnFail(1471, "2.2.8 Variable %s min/max/start/nominal invalid at %s", sv.name, loc2str(sv.location))
		(
			cases sv:
				mk_Float64(-, -, -, -, -, -, -, -, -, -, -, -, -, -, -, -, -, min, max, nominal, start, -, -, -, -, -),
				mk_Float32(-, -, -, -, -, -, -, -, -, -, -, -, -, -, -, -, -, min, max, nominal, start, -, -, -, -, -) ->
					isInRange[real](min, max, start, sv, sv.initial)
					-- @OnFail(1472, "2.2.8 Real nominal must be >0.0 at %s", loc2str(sv.location))
					and (nominal <> nil => nominal > 0.0),

				mk_Int64(-, -, -, -, -, -, -, -, -, -, -, -, -, min, max, start, -, -, -),
				mk_Int32(-, -, -, -, -, -, -, -, -, -, -, -, -, min, max, start, -, -, -),
				mk_Int16(-, -, -, -, -, -, -, -, -, -, -, -, -, min, max, start, -, -, -),
				mk_Int8(-, -, -, -, -, -, -, -, -, -, -, -, -, min, max, start, -, -, -),
				mk_UInt64(-, -, -, -, -, -, -, -, -, -, -, -, -, min, max, start, -, -, -),
				mk_UInt32(-, -, -, -, -, -, -, -, -, -, -, -, -, min, max, start, -, -, -),
				mk_UInt16(-, -, -, -, -, -, -, -, -, -, -, -, -, min, max, start, -, -, -),
				mk_UInt8(-, -, -, -, -, -, -, -, -, -, -, -, -, min, max, start, -, -, -) ->
					isInRange[int](min, max, start, sv, sv.initial),

				mk_Enumeration(-, -, -, -, -, -, -, -, -, -, -, -, -, min, max, start, -, -, -) ->
					isInRange[nat](min, max, start, sv, sv.initial),	-- TODO kind of enum?

				mk_Binary(-, -, -, -, -, -, -, -, -, -, -, -, -, -, start, -, -, -) ->
					-- @OnFail(1473, "2.2.8 Binary %s start is not hex-binary at %s", sv.name, loc2str(sv.location))
					( start = nil or forall s in seq start &
						forall c in seq s &
							c in set elems "ABCDEFabcdef0123456789" ),

				others -> true		-- String, Boolean, Clock
			end
		)

		| sv in seq svs
	}
	union
	{
		-- @OnFail(1474, "2.2.8 Dimension of %s do not match start vector at %s", sv.name, loc2str(sv.location))
		( sv.start <> nil =>
			let size = dimensionProduct(svs, sv.dimension) in
				size > 0 => size = len sv.start )

		| sv in seq svs
	}
	union
	{
		/* @OnFail(1475, "2.2.8 Invalid Variable aliasing") */
		(
			let refmap = getAliasRefMap(svs) in
			card dom refmap < len svs => dunion	-- Must be some aliases
			{
				let aliases = refmap(ref) in
				{
					/* @OnFail(1476, "2.2.8 Multiple aliases of reference %s are settable: %s", ref,
							{ a.name | a in set aliases & isSettable(a) }) */
					( card { a | a in set aliases & isSettable(a) } <= 1 ),

					/* @OnFail(1477, "2.2.8 Aliases of reference %s are settable and independent: %s", ref,
							{ {a.name, b.name} | a, b in set aliases &
								a <> b and isSettable(a) and b.causality = <independent> } ) */
					( not exists a, b in set aliases & a <> b and isSettable(a) and b.causality = <independent> ),

					-- @OnFail(1478, "2.2.8 Too many aliases of reference %s have start set", ref)
					( card { a.start | a in set aliases & a.variability <> <constant> and a.start <> nil } <= 1 ),

					-- @OnFail(1479, "2.2.8 Constant aliases of reference %s have different start values", ref)
					( card { a.start | a in set aliases & a.variability = <constant> and a.start <> nil } <= 1 ),

					-- @OnFail(1480, "2.2.8 Aliases of reference %s must all be constant or variable", ref)
					( card { a | a in set aliases & a.variability = <constant> } in set {0, card aliases} ),

					-- @OnFail(1481, "2.2.8 Aliases of reference %s must all have same unit/baseUnits", ref)
					( card { a.unit | a in set aliases & isReal(a) } <= 1 ),

					/*
					 * In case of different variability among the set of alias variables, and if that set of aliases
					 * does not contain an input or parameter, the variability should be the highest of the variables
					 * in the set, e.g. continuous > discrete > tunable > fixed. If the set includes a parameter or
					 * input the aliases will have the stated variability of that parameter or input.
					 */
					 let vars = { a.variability | a in set aliases } in
					 	if exists a in set aliases & a.causality in set {<input>, <parameter>}
					 	then
					 		let a in set aliases be st a.causality in set {<input>, <parameter>} in
				 				/* @OnFail(1482, "2.2.8 Warning: aliases of reference %s must all be %s, because of %s",
				 						ref, a.variability, a.name) */
				 				( vars = { a.variability } ) or true
					 	else
					 		let highest in set vars be st not exists v in set vars & orderOf(v) > orderOf(highest) in
					 			-- @OnFail(1483, "2.2.8 Warning: aliases of reference %s must all be %s", ref, highest)
					 			( vars = { highest } ) or true
				}

				| ref in set dom refmap & card refmap(ref) > 1
			}
			= {true}
		)
	}
	union
	{
		-- @OnFail(1459, "2.2.9 Variable %s invalid at %s", sv.name, loc2str(sv.location))
		( isValidClockAttributes(sv, svs) )

		| sv in seq svs
	} = {true};


	/**
	 * Calculate the overall number of start values expected from dimension. If a variable is referenced,
	 * and we do not know its value, we return 0.
	 */
	dimensionProduct: seq1 of Variable * [seq of Dimension] +> nat
	dimensionProduct(svs, dims) ==
		if dims = nil or dims = []
		then 1
		else dimensionize(svs, hd dims) * dimensionProduct(svs, tl dims)
	measure if dims <> nil then len dims else 0;

	dimensionize: seq1 of Variable * Dimension +> nat
	dimensionize(svs, dim) ==
		if dim.start <> nil
		then dim.start
		else if dim.valueReference = nil
		then 0
		else let s = lookup(svs, dim.valueReference) in
			if s <> nil and s.start <> nil and len s.start > 0
			then hd s.start
			else 0;

	/**
	 * The default value for initial, defined by the (effective) values of causality and
	 * variability, via the table on p48/49.
	 */
	effectiveInitial: Variable * Causality * Variability -> [Initial]
	effectiveInitial(v, eCausality, eVariability) ==
		let i = v.initial in
			if i <> nil	then i
		else if is_Clock(v)
		then
			cases eCausality:
				<input> -> <exact>,
				<output> -> <calculated>,
				<local> -> <calculated>,
				others -> nil
			end
		else cases eCausality:
				<structuralParameter> ->
					cases eVariability:
						<fixed>, <tunable> -> <exact>,
						others -> nil
					end,

				<parameter> ->
					cases eVariability:
						<fixed>, <tunable> -> <exact>,
						others -> nil
					end,

				<calculatedParameter> ->
					cases eVariability:
						<fixed>, <tunable> -> <calculated>,
						others -> nil
					end,

				<input> ->
					cases eVariability:
						<discrete>, <continuous> -> nil,
						others -> nil
					end,

				<output> ->
					cases eVariability:
						<constant> -> <exact>,
						<discrete>, <continuous> -> <calculated>,
						others -> nil
					end,

				<local> ->
					cases eVariability:
						<constant> -> <exact>,
						<fixed>, <tunable> -> <calculated>,
						<discrete>, <continuous> -> <calculated>,
						others -> nil
					end,

				<independent> ->
					cases eVariability:
						<continuous> -> nil,
						others -> nil
					end
			end;

	effectiveCausality: [Causality] -> Causality
	effectiveCausality(c) ==
		if c = nil
		then <local>
		else c;

	effectiveVariability: Variable -> Variability
	effectiveVariability(v) ==
		if v.variability = nil
		then if isReal(v) then <continuous> else <discrete>
		else v.variability;

	/**
	 * Generate all of the effective Variables. Note that an effective "time"
	 * variable is created if there are no independent variables defined.
	 */
	effectiveScalarVariables: FmiModelDescription +> seq1 of Variable
	effectiveScalarVariables(model) ==
		let evs =
		[
			effectiveVariable(sv, model.typeDefinitions)
			| sv in seq model.modelVariables
		]
		in
			if not exists esv in seq evs & esv.causality = <independent>
			then evs ^
				[
					-- If no variable is defined as "independent", it is implicitly
					-- present with name = "time" and unit = "s"

					mk_Float64
					(
						mk_Location("<implicit variable>", 1),
						"time",
						let allvrs = { sv.valueReference | sv in seq evs } in
							let vr in set {1, ..., len evs + 1} be st vr not in set allvrs in vr,
						"Implicit independent time variable",
						<independent>,
						<continuous>,
						nil,
						nil,
						nil,
						nil,
						nil,
						nil,
						nil,
						nil,
						nil,
						nil, nil,
						nil, nil, nil, nil, nil,
						nil, nil, nil,
						nil
					)
				]
			else evs;

	/**
	 * Generate the effective value of a Variable, accounting for fields inherited from
	 * its declaredType.
	 */
	effectiveVariable: Variable * [seq of VariableType] +> Variable
	effectiveVariable(v, typedefs) ==
		let causality	= effectiveCausality(v.causality),
			variability	= effectiveVariability(v),
			initial		= effectiveInitial(v, causality, variability),
			newv		= mu(v,
				causality	|-> causality,
				variability	|-> variability,
				initial		|-> initial
			)
		in
		if v.declaredType = nil or typedefs = nil
		then newv
		else let dtypes = { td | td in seq typedefs & td.name = v.declaredType } in
			cases dtypes:
				{ dt } ->
					cases dt:
						mk_Float32Type(-, -, -, quantity, unit, dunit, relq, unbounded, min, max, nominal, -),
						mk_Float64Type(-, -, -, quantity, unit, dunit, relq, unbounded, min, max, nominal, -) ->
							if isReal(v) then
								mu(newv,
									quantity			|-> override[NormalizedString](v.quantity, quantity),
									unit				|-> override[NormalizedString](v.unit, unit),
									displayUnit			|-> override[NormalizedString](v.displayUnit, dunit),
									relativeQuantity	|-> override[bool](v.relativeQuantity, relq),
									unbounded			|-> override[bool](v.unbounded, unbounded),
									min					|-> override[real](v.min, min),
									max					|-> override[real](v.max, max),
									nominal				|-> override[real](v.nominal, nominal)
								)
							else
								newv,	-- Error reported elsewhere

						mk_Int64Type(-, -, -, quantity, min, max, -),
						mk_Int32Type(-, -, -, quantity, min, max, -),
						mk_Int16Type(-, -, -, quantity, min, max, -),
						mk_Int8Type(-, -, -, quantity, min, max, -),
						mk_UInt64Type(-, -, -, quantity, min, max, -),
						mk_UInt32Type(-, -, -, quantity, min, max, -),
						mk_UInt16Type(-, -, -, quantity, min, max, -),
						mk_UInt8Type(-, -, -, quantity, min, max, -) ->
							if isInteger(v) then
								mu(newv,
									quantity	|-> override[NormalizedString](v.quantity, quantity),
									min			|-> override[real](v.min, min),
									max			|-> override[real](v.max, max)
								)
							else
								newv,	-- Error reported elsewhere

						mk_EnumerationType(-, -, -, quantity, -, -) ->
							if is_Enumeration(v) then
								mu(newv,
									quantity	|-> override[NormalizedString](v.quantity, quantity)
								)
							else
								newv,	-- Error reported elsewhere

						mk_ClockType(-, -, -, canBeDeactivated,	priority, interval, intervalDecimal,
									shiftDecimal, supportsFraction, resolution, intervalCounter,
									shiftCounter, -) ->
							mu(newv,
								canBeDeactivated  |-> override[bool](v.canBeDeactivated, canBeDeactivated),
								priority          |-> override[nat](v.priority, priority),
								interval          |-> override[Interval](v.interval, interval),
								intervalDecimal   |-> override[real](v.intervalDecimal, intervalDecimal),
								shiftDecimal      |-> override[real](v.shiftDecimal, shiftDecimal),
								supportsFraction  |-> override[bool](v.supportsFraction, supportsFraction),
								resolution        |-> override[nat](v.resolution, resolution),
								intervalCounter   |-> override[nat](v.intervalCounter, intervalCounter),
								shiftCounter      |-> override[nat](v.shiftCounter, shiftCounter)),

						others ->
							newv
					end,

				others -> newv		-- Duplicate or missing typedefs caught elsewhere
			end;

	/**
	 * Helper to take an overriding non-nil value
	 */
	override[@T]: [@T] * [@T] +> [@T]
	override(default, value) ==
		if value <> nil then value else default;

	/**
	 * Test whether a Variable is settable with fmi2SetXXX.
	 */
	isSettable: Variable +> bool
	isSettable(sv) ==
		sv.variability <> <constant> and sv.initial in set { <exact>, <approx> }
		or
		sv.causality = <input>
		or
		sv.causality = <parameter> and sv.variability = <tunable>;

	/**
	 * Return the Variables from a list which are continuous-time states, defined to be
	 * Real variables which are continuous and have a derivative.
	 */
	continuousTimeStates: seq of Variable -> set of Variable
	continuousTimeStates(svs) ==
		{ svs(i) | i in set inds svs & isContinuousTimeState(i, svs) };

	/**
	 * Test ith index variable as a continuous time state.
	 */
	isContinuousTimeState: nat1 * seq of Variable +> bool
	isContinuousTimeState(i, svs) ==
		isReal(svs(i))
		and svs(i).variability = <continuous> 
		and exists j in seq svs &
			isReal(j) and j.derivative = svs(i).valueReference;

	/**
	 * Return the Variables from a list which are state derivatives, defined to be
	 * variables which are the derivative of some other variable.
	 */
	stateDerivatives: seq of Variable -> set of Variable
	stateDerivatives(svs) ==
		{ sv | sv in seq svs & isStateDerivative(sv) };

	/**
	 * Test Variable as a state derivative.
	 */
	isStateDerivative: Variable +> bool
	isStateDerivative(sv) ==
		isReal(sv) and sv.derivative <> nil;

	/**
	 * Get derivative order of a variable.
	 */
	getDerivativeBase: fmi3ValueReference * seq of Variable +> nat * fmi3ValueReference
	getDerivativeBase(vr, svs) ==
		if not isReal(svs(vr)) or svs(vr).derivative = nil
		then mk_(0, vr)
		else let mk_(o, r) = getDerivativeBase(svs(vr).derivative, svs) in
			mk_(o+1, r)
	measure is not yet specified;

	/**
	 * Get the alias refmap, from valueReferences to sets of SVs
	 */
	getAliasRefMap: seq1 of Variable -> map fmi3ValueReference to set of Variable
	getAliasRefMap(svs) ==
		let vrefs = { svi |-> svs(svi).valueReference | svi in set inds svs } in
		{
			vrefs(svi) |-> { svs(v) | v in set inds svs & vrefs(v) = vrefs(svi) }
			| svi in set inds svs
		};

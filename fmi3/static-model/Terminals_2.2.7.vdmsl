/**
 * This file is part of the INTO-CPS toolchain.
 *
 * Copyright (c) 2017-2019, INTO-CPS Association,
 * c/o Professor Peter Gorm Larsen, Department of Engineering
 * Finlandsgade 22, 8200 Aarhus N.
 *
 * All rights reserved.
 *
 * THIS PROGRAM IS PROVIDED UNDER THE TERMS OF GPL VERSION 3 LICENSE OR
 * THIS INTO-CPS ASSOCIATION PUBLIC LICENSE VERSION 1.0.
 * ANY USE, REPRODUCTION OR DISTRIBUTION OF THIS PROGRAM CONSTITUTES
 * RECIPIENT'S ACCEPTANCE OF THE OSMC PUBLIC LICENSE OR THE GPL 
 * VERSION 3, ACCORDING TO RECIPIENTS CHOICE.
 *
 * The INTO-CPS toolchain  and the INTO-CPS Association Public License are
 * obtained from the INTO-CPS Association, either from the above address, from
 * the URLs: http://www.into-cps.org, and in the INTO-CPS toolchain distribution.
 * GNU version 3 is obtained from: http://www.gnu.org/copyleft/gpl.html.
 *
 * This program is distributed WITHOUT ANY WARRANTY; without
 * even the implied warranty of  MERCHANTABILITY or FITNESS FOR
 * A PARTICULAR PURPOSE, EXCEPT AS EXPRESSLY SET FORTH IN THE
 * BY RECIPIENT SELECTED SUBSIDIARY LICENSE CONDITIONS OF
 * THE INTO-CPS ASSOCIATION.
 *
 * See the full INTO-CPS Association Public License conditions for more details.
 */

/**
 * 2.2.6.2 Definition of Terminals
 */
types
	FMITerminalsAndIcons ::
		location					: Location
		fmiVersion					: NormalizedString
		graphicalRepresentation		: GraphicalRepresentation
		terminals					: [seq1 of Terminal]
		vendorAnnotations			: [fmi3Annotation];

	Terminal ::
		location					: Location
		name						: NormalizedString
		matchingRule				: NormalizedString
		terminalKind				: [NormalizedString]
		description					: [AnyString]
		graphicalRepresentation		: [TerminalGraphicalRepresentation]
		variables					: [seq1 of TerminalMemberVariable]
		streams						: [seq1 of TerminalStreamMemberVariable]
		subTerminals				: [seq1 of Terminal];

	TerminalGraphicalRepresentation ::
		location					: Location
		defaultConnectionColor		: [seq of nat]
		defaultConnectionStrokeSize	: [real]
		x1							: real
		y1							: real
		x2							: real
		y2							: real
		iconBase					: [NormalizedString]
		vendorAnnotations			: [fmi3Annotation];

	TerminalMemberVariable ::
		location					: Location
		variableKind				: NormalizedString
		memberName					: NormalizedString
		variableName				: NormalizedString;

	TerminalStreamMemberVariable ::
		location					: Location
		inStreamMemberName			: NormalizedString
		outStreamMemberName			: NormalizedString
		inStreamVariableName		: NormalizedString
		outStreamVariableName		: NormalizedString;

functions
	isValidFMITerminalsAndIcons: FMITerminalsAndIcons +> bool
	isValidFMITerminalsAndIcons(tandi) ==
	{
		isValidGraphicalRepresentation(tandi.graphicalRepresentation),
		isValidTerminals(tandi.terminals)
	}
	= {true};

	isValidTerminals: [seq1 of Terminal] +> bool
	isValidTerminals(terminals) ==
		terminals <> nil => dunion
		{
			{
				-- @OnFail(1300, "2.2.6.2 Terminal invalid at line %s", t.location)
				( isValidTerminal(t) ),

				isValidTerminals(t.subTerminals),
				isValidTerminalGraphicalRepresentation(t.graphicalRepresentation)
			}

			| t in seq terminals
		}
		= {true}
	measure is not yet specified;

	isValidTerminal: Terminal +> bool
	isValidTerminal(term) ==
		term.variables <> nil =>
			-- @OnFail(1301, "2.2.6.2 Terminal member variables not unique at line %s", term.location)
			( card { v | v in seq term.variables } = len term.variables );


	isValidTerminalGraphicalRepresentation: [TerminalGraphicalRepresentation] +> bool
	isValidTerminalGraphicalRepresentation(gt) ==
		gt <> nil =>
			{
				-- @OnFail(1302, "2.2.6.2 Terminal area is not bottom left to top right at line %s", gt.location)
				( gt.x1 < gt.x2 and gt.y1 < gt.y2 ),

				-- @OnFail(1303, "2.2.6.2 Colour attributes must be octets (<=255) at line %s", gt.location)
				( gt.defaultConnectionColor <> nil =>
					forall c in seq gt.defaultConnectionColor & c <= 255 ),

				-- @OnFail(1304, "2.2.6.2 defaultConnectionStrokeSize must be >0 at line %s", gt.location)
				( gt.defaultConnectionStrokeSize <> nil =>
					gt.defaultConnectionStrokeSize > 0 ),

				-- @OnFail(1305, "2.2.6.2 Terminal vendor annotations invalid at line %s", gt.location)
				( isValidVendorAnnotation(gt.vendorAnnotations) )
			}
			= {true};

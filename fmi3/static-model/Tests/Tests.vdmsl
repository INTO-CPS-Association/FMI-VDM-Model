/*********************************************************************************
 *
 *	Copyright (c) 2017-2022, INTO-CPS Association,
 *	c/o Professor Peter Gorm Larsen, Department of Engineering
 *	Finlandsgade 22, 8200 Aarhus N.
 *
 *	MIT Licence:
 *
 *	Permission is hereby granted, free of charge, to any person obtaining a copy of
 *	this software and associated documentation files (the "Software"), to deal in
 *	the Software without restriction, including without limitation the rights to use,
 *	copy, modify, merge, publish, distribute, sublicense, and/or sell copies of the
 *	Software, and to permit persons to whom the Software is furnished to do so,
 *	subject to the following conditions:
 *
 *	The above copyright notice and this permission notice shall be included in all
 *	copies or substantial portions of the Software.
 *
 *	THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED,
 *	INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A
 *	PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT
 *	HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION
 *	OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE
 *	SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
 *
 *	SPDX-License-Identifier: MIT
 *
 *********************************************************************************/

/**
 * Various model tests
 */
traces
	/**
	 * Test the ScalarInvariant invariant function with all possible inputs - should return true/false (total)
	 * for every combination. Variable types are tested with "start" both defined and undefined.
	 */
	SVInvariantTotal:
		let c : [Causality] in
		let v : [Variability] in
		let i : [Initial] in
		let a : Annotations = [ mk_Annotation(mk_Location("modelDescription.xml", 1), "A", [mk_token("?")]) ] in
		let t in set
		{
            mk_Float64
            (
                mk_Location("modelDescription.xml", 6),
                "name",
                1234,
                nil,  -- description
                c,    -- causality
                v,    -- variabilty
                i,    -- initial
                nil,  -- canHandleMultipleSetPerTimeInstant
                nil,  -- intermediateUpdate
                nil,  -- previous
                nil,  -- clocks
                nil,  -- declaredType
                nil,  -- quantity
                nil,  -- unit
                nil,  -- displayUnit
                nil,  -- relativeQuantity
                nil,  -- unbounded
                nil,  -- min
                nil,  -- max
                nil,  -- nominal
                -- start
                [
                    1.23
                ],
                nil,  -- derivative
                nil,  -- reinit
                a,    -- annotations
                nil,  -- dimension
                -- alias
                [
                    mk_Alias
                    (
                        mk_Location("modelDescription.xml", 7),
                        "alias",
                        "desc",
                        nil   -- displayUnit
                    )
                ]
            ),

           mk_Int64
            (
                mk_Location("modelDescription.xml", 9),
                "name2",
                1234,
                nil,  -- description
                c,    -- causality
                v,    -- variability
                i,    -- initial
                nil,  -- canHandleMultipleSetPerTimeInstant
                nil,  -- intermediateUpdate
                nil,  -- previous
                nil,  -- clocks
                nil,  -- declaredType
                nil,  -- quantity
                nil,  -- min
                nil,  -- max
                -- start
                [
                    123
                ],
                a,    -- annotations
                nil,  -- dimension
                -- alias
                [
                    mk_Alias
                    (
                        mk_Location("modelDescription.xml", 10),
                        "alias",
                        "desc",
                        nil   -- displayUnit
                    )
                ] 
            )
		}
		in
			isValidVariableAttributes(t);

functions
	/**
	 * Helper to make Reals for test below.
	 */
	mkVariable: Causality * Variability * [Initial] * [seq1 of real] +> Float64
	mkVariable(c, v, i, start) ==
            mk_Float64
            (
                mk_Location("modelDescription.xml", 6),
                "name",
                1234,
                nil,  -- description
                c,    -- causality
                v,    -- variabilty
                nil,  -- canHandleMultipleSetPerTimeInstant
                nil,  -- intermediateUpdate
                nil,  -- previous
                nil,  -- clocks
                nil,  -- declaredType
                i,    -- initial
                nil,  -- quantity
                nil,  -- unit
                nil,  -- displayUnit
                nil,  -- relativeQuantity
                nil,  -- unbounded
                nil,  -- min
                nil,  -- max
                nil,  -- nominal
                -- start
                start,
                nil,  -- derivative
                nil,  -- reinit
                nil,  -- dimension
                nil,  -- annotations
                -- alias
                [
                    mk_Alias
                    (
                        mk_Location("modelDescription.xml", 7),
                        "alias",
                        "desc",
                        nil   -- displayUnit
                    )
                ] 
            );

	/**
	 * Test the ScalarVariable invariant for the table on p48, where the SV is valid or not
	 * for particular "initial" values, given a combination of causality and variability.
	 */
	testCausalityVariabilityInitial: () +> bool
	testCausalityVariabilityInitial() ==
		let nilv   = nil,
		    startv = [1.0]
		in
			forall causality : Causality &
				forall variability : Variability &
					-- @Printf("Testing %s/%s ...\n", causality, variability)
					-- @OnFail("Test %s/%s failed", causality, variability)
					(cases causality:
						<structuralParameter> ->
							cases variability:
								<fixed>, <tunable> ->
									isValidVariableAttributes(mkVariable(causality, variability, <exact>, startv)) and
									not isValidVariableAttributes(mkVariable(causality, variability, <approx>, startv)) and
									not isValidVariableAttributes(mkVariable(causality, variability, <calculated>, nilv)),

								<constant>, <discrete>, <continuous> ->
									forall initial : Initial &
										not isValidVariableAttributes(mkVariable(causality, variability, initial, nilv))
							end,

						<parameter> ->
							cases variability:
								<fixed>, <tunable> ->
									isValidVariableAttributes(mkVariable(causality, variability, <exact>, startv)) and
									not isValidVariableAttributes(mkVariable(causality, variability, <approx>, startv)) and
									not isValidVariableAttributes(mkVariable(causality, variability, <calculated>, nilv)),

								<constant>, <discrete>, <continuous> ->
									forall initial : Initial &
										not isValidVariableAttributes(mkVariable(causality, variability, initial, nilv))
							end,

						<calculatedParameter> ->
							cases variability:
								<fixed>, <tunable> ->
									isValidVariableAttributes(mkVariable(causality, variability, <calculated>, nilv)) and
									isValidVariableAttributes(mkVariable(causality, variability, <approx>, startv)) and
									not isValidVariableAttributes(mkVariable(causality, variability, <exact>, nilv)),

								<constant>, <discrete>, <continuous> ->
									forall initial : Initial &
										not isValidVariableAttributes(mkVariable(causality, variability, initial, nilv))
							end,

						<input> ->
							cases variability:
								<discrete>, <continuous> ->
									isValidVariableAttributes(mkVariable(causality, variability, nil, startv)) and
									not isValidVariableAttributes(mkVariable(causality, variability, <calculated>, nilv)) and
									-- not isValidVariableAttributes(mkVariable(causality, variability, <approx>, startv)) and
									not isValidVariableAttributes(mkVariable(causality, variability, <exact>, nilv)),

								<constant>, <fixed>, <tunable> ->
									forall initial : Initial &
										not isValidVariableAttributes(mkVariable(causality, variability, initial, nilv))
							end,

						<output> ->
							cases variability:
								<constant> ->
									isValidVariableAttributes(mkVariable(causality, variability, <exact>, startv)) and
									not isValidVariableAttributes(mkVariable(causality, variability, <calculated>, nilv)) and
									not isValidVariableAttributes(mkVariable(causality, variability, <approx>, startv)),

								<discrete>, <continuous> ->
									isValidVariableAttributes(mkVariable(causality, variability, <calculated>, nilv)) and
									isValidVariableAttributes(mkVariable(causality, variability, <exact>, startv))
									and isValidVariableAttributes(mkVariable(causality, variability, <approx>, startv)),

								<fixed>, <tunable> ->
									forall initial : Initial &
										not isValidVariableAttributes(mkVariable(causality, variability, initial, nilv))
							end,

						<local> ->
							cases variability:
								<constant> ->
									isValidVariableAttributes(mkVariable(causality, variability, <exact>, startv)) and
									not isValidVariableAttributes(mkVariable(causality, variability, <calculated>, nilv)) and
									not isValidVariableAttributes(mkVariable(causality, variability, <approx>, startv)),

								<fixed>, <tunable> ->
									isValidVariableAttributes(mkVariable(causality, variability, <calculated>, nilv)) and
									isValidVariableAttributes(mkVariable(causality, variability, <approx>, startv)) and
									not isValidVariableAttributes(mkVariable(causality, variability, <exact>, startv)),

								<discrete>, <continuous> ->
									isValidVariableAttributes(mkVariable(causality, variability, <calculated>, nilv)) and
									isValidVariableAttributes(mkVariable(causality, variability, <approx>, startv)) and
									isValidVariableAttributes(mkVariable(causality, variability, <exact>, startv))
							end,

						<independent> ->
							forall initial : Initial &
								not isValidVariableAttributes(mkVariable(causality, variability, initial, nilv))
					end);

	testStructuredNames: () +> seq of bool
	testStructuredNames() ==
	[
		isValidStructuredName("der(a.b.c,3)"),
		isValidStructuredName("der(a)"),
		isValidStructuredName("a"),
		isValidStructuredName("'!#$'"),
		isValidStructuredName("a[1]"),
		isValidStructuredName("_abc[1,2,3]"),
		isValidStructuredName("xyz.abc.def[1,2,3]"),
		isValidStructuredName("'!!!\\?'.a10[1,2,3]"),
		isValidStructuredName("a20[1,2,3].def[1]"),
		isValidStructuredName("'\\\"\\?\\\\'"),
		not isValidStructuredName("a[1,]"),
		not isValidStructuredName("[1]"),
		not isValidStructuredName("'1@"),
		not isValidStructuredName("ABC..DEF"),
		not isValidStructuredName("ABC[1]..DEF")
	];

functions
	/**
	 * Run all tests and check that they pass.
	 */
	tests: () +> seq of bool
	tests() ==
	[
		testCausalityVariabilityInitial()
		-- isValidFMIModelDescription(example)
	]
	^	testStructuredNames()

	post elems RESULT = {true};

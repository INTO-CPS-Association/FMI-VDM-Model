/**
 * This file is part of the INTO-CPS toolchain.
 *
 * Copyright (c) 2017-2019, INTO-CPS Association,
 * c/o Professor Peter Gorm Larsen, Department of Engineering
 * Finlandsgade 22, 8200 Aarhus N.
 *
 * All rights reserved.
 *
 * THIS PROGRAM IS PROVIDED UNDER THE TERMS OF GPL VERSION 3 LICENSE OR
 * THIS INTO-CPS ASSOCIATION PUBLIC LICENSE VERSION 1.0.
 * ANY USE, REPRODUCTION OR DISTRIBUTION OF THIS PROGRAM CONSTITUTES
 * RECIPIENT'S ACCEPTANCE OF THE OSMC PUBLIC LICENSE OR THE GPL 
 * VERSION 3, ACCORDING TO RECIPIENTS CHOICE.
 *
 * The INTO-CPS toolchain  and the INTO-CPS Association Public License are
 * obtained from the INTO-CPS Association, either from the above address, from
 * the URLs: http://www.into-cps.org, and in the INTO-CPS toolchain distribution.
 * GNU version 3 is obtained from: http://www.gnu.org/copyleft/gpl.html.
 *
 * This program is distributed WITHOUT ANY WARRANTY; without
 * even the implied warranty of  MERCHANTABILITY or FITNESS FOR
 * A PARTICULAR PURPOSE, EXCEPT AS EXPRESSLY SET FORTH IN THE
 * BY RECIPIENT SELECTED SUBSIDIARY LICENSE CONDITIONS OF
 * THE INTO-CPS ASSOCIATION.
 *
 * See the full INTO-CPS Association Public License conditions for more details.
 */

/**
 * 2.2.9 Definition of the Model Structure (ModelStructure)
 */
functions
	/**
	 * Validation of a ModelStructure.
	 */
	isValidModelStructure: ModelStructure +> bool
	isValidModelStructure(ms) ==
	{
		-- @OnFail(1500, "2.2.9 ModelStructure has invalid initialUnknowns")
		( ms.initialUnknown <> nil =>
			forall u in seq ms.initialUnknown &
				isValidUnknown(u.valueReference, u.dependencies, u.dependenciesKind, u.location) ),

		-- @OnFail(1501, "2.2.9 ModelStructure has invalid continuousStateDerivative")
		( ms.continuousStateDerivative <> nil =>
			forall u in seq ms.continuousStateDerivative &
				isValidUnknown(u.valueReference, u.dependencies, u.dependenciesKind, u.location) ),

		-- @OnFail(1502, "2.2.9 ModelStructure has invalid clockedState")
		( ms.clockedState <> nil =>
			forall u in seq ms.clockedState &
				isValidUnknown(u.valueReference, u.dependencies, u.dependenciesKind, u.location) ),

		-- @OnFail(1503, "2.2.9 ModelStructure has invalid eventIndicator")
		( ms.eventIndicator <> nil =>
			forall u in seq ms.eventIndicator &
				isValidUnknown(u.valueReference, u.dependencies, u.dependenciesKind, u.location) ),

		-- @OnFail(1504, "2.2.9 InitialUnknowns are not of kind dependent or constant")
		(
			ms.initialUnknown <> nil =>
				forall iu in seq ms.initialUnknown &
					iu.dependenciesKind <> nil =>
						forall dk in seq iu.dependenciesKind &
							 dk in set { <dependent>, <constant>, nil }
		)
	} = {true};

	/**
	 * Validate an Unknown structure in isolation.
	 */
	isValidUnknown: fmi3ValueReference * [seq of (nat)] * [seq of (DependenciesKind)] * Location +> bool
	isValidUnknown(valueReference, dependencies, dependenciesKind, location) ==
	-- @OnFail(1502, "2.2.11 Unknown %s has invalid dependencies/kinds at %s", valueReference, loc2str(location))
	(
		if dependencies <> nil
		then dependenciesKind <> nil =>
			{
				-- @OnFail(1505, "2.2.9 Dependencies list does not match dependenciesKind at %s", loc2str(location))
				( len dependencies = len dependenciesKind ),

				/* @OnFail(1506, "2.2.9 Unknown has duplicate indexes %s at %s",
					{ dependencies(a) | a, b in set inds dependencies &
						a <> b and dependencies(a) = dependencies(b) }, loc2str(location) ) */
				( len dependencies = card elems dependencies),

				-- @OnFail(1507, "2.2.9 Unknown cannot depend on itself at %s", loc2str(location))
				(true) -- CHECK THIS! ( u.index not in set elems u.dependencies )
			} = {true}
		else
			dependenciesKind = nil
	);

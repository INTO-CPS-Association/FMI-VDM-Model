/**
 * This file is part of the INTO-CPS toolchain.
 *
 * Copyright (c) 2017-2019, INTO-CPS Association,
 * c/o Professor Peter Gorm Larsen, Department of Engineering
 * Finlandsgade 22, 8200 Aarhus N.
 *
 * All rights reserved.
 *
 * THIS PROGRAM IS PROVIDED UNDER THE TERMS OF GPL VERSION 3 LICENSE OR
 * THIS INTO-CPS ASSOCIATION PUBLIC LICENSE VERSION 1.0.
 * ANY USE, REPRODUCTION OR DISTRIBUTION OF THIS PROGRAM CONSTITUTES
 * RECIPIENT'S ACCEPTANCE OF THE OSMC PUBLIC LICENSE OR THE GPL 
 * VERSION 3, ACCORDING TO RECIPIENTS CHOICE.
 *
 * The INTO-CPS toolchain  and the INTO-CPS Association Public License are
 * obtained from the INTO-CPS Association, either from the above address, from
 * the URLs: http://www.into-cps.org, and in the INTO-CPS toolchain distribution.
 * GNU version 3 is obtained from: http://www.gnu.org/copyleft/gpl.html.
 *
 * This program is distributed WITHOUT ANY WARRANTY; without
 * even the implied warranty of  MERCHANTABILITY or FITNESS FOR
 * A PARTICULAR PURPOSE, EXCEPT AS EXPRESSLY SET FORTH IN THE
 * BY RECIPIENT SELECTED SUBSIDIARY LICENSE CONDITIONS OF
 * THE INTO-CPS ASSOCIATION.
 *
 * See the full INTO-CPS Association Public License conditions for more details.
 */

/**
 * 2.2.10 Definition of Model Variables (ModelVariables)
 */
types
	-- XSD p45, description p46
	Causality	= <parameter> | <calculatedParameter> | <input> | <output> | <local> | <independent>;

	-- XSD p45, description p46, ordering p53 (2.0.1)
	Variability	= <constant> | <fixed> | <tunable> | <discrete> | <continuous>
	ord a < b ==
		let value =
		{
			<constant>   |-> 1,
			<fixed>      |-> 2,
			<tunable>    |-> 3,
			<discrete>   |-> 4,
			<continuous> |-> 5
		}
		in value(a) < value(b);

	-- XSD p45, description p46
	Initial		= <exact> | <approx> | <calculated>;


	-- Common variable attributes
	fmi3RealVariableAttributes ::
		derivative			: [nat]
		reinit				: [bool];

	Dimension ::
		start				: nat		-- ???
		valueReference		: nat;

	fmi3VariableBase ::
		location							: Location
		dimension							: [seq1 of Dimension]
		annotations							: [fmi3Annotation]
		name								: NormalizedString1
		valueReference						: nat
		description							: [AnyString]
		causality							: [Causality]
		variability							: [Variability]
		initial								: [Initial]
		canHandleMultipleSetPerTimeInstant	: [bool]
		declaredType						: [NormalizedString1]
		clockReference						: [nat]
		intermediateAccess					: [bool];

	Real ::
		base				: fmi3VariableBase
		varAtts				: fmi3RealVariableAttributes
		realAtts			: fmi3RealAttributes
		floatAtts			: fmi3FloatXXAttributes
		start				: [real];

	Integer ::
		base				: fmi3VariableBase
		integerAtts			: fmi3IntegerAttributes
		intAtts				: fmi3IntXXAttributes
		start				: [int];

	Boolean ::
		base				: fmi3VariableBase
		start				: [bool];

	String ::
		base				: fmi3VariableBase
		start				: [AnyString];

	Binary ::
		base				: fmi3VariableBase
		mimeType			: [NormalizedString1]
		maxSize				: [nat]
		start				: [AnyString];	-- HexBinary

	Enumeration ::
		base				: fmi3VariableBase
		quantity			: [NormalizedString1]
		min					: [int]
		max					: [int]
		start				: [seq of int];

	Clock ::
		base				: fmi3VariableBase
		start				: [bool];

	Variable = Real | Integer | Boolean | String | Binary | Enumeration | Clock;

	VarName = NormalizedString1;	-- Has a syntax defined in section 2.2.12


functions
	/**
	 * Invariant helper for min/max/start checks on Real, Integer and Enumeration.
	 */
	isInRange[@T]: [@T] * [@T] * [@T] +> bool
	isInRange(min, max, start) ==
	{
		max <> nil and min <> nil =>
			--@OnFail("2.2.10 min %s is not <= max %s", min, max)
			( max >= min ),

		start <> nil =>
			--@OnFail("2.2.10 start %s is not within min %s/max %s", start, min, max)
			( (min <> nil => min <= start) and
			  (max <> nil => max >= start) ),

		start = nil =>
			--@OnFail("2.2.10 Warning: implicit start of 0 is not within min %s/max %s", min, max)
			( (min <> nil => min <= 0) and
			  (max <> nil => max >= 0) ) or true	-- NOTE warning
	} = {true}
	pre is_(min, [real]) and is_(max, [real]) and is_(start, [real]);	-- ie. numerics

	/**
	 * Verify a sequence of raw Variables.
	 */
	isValidScalarVariables: seq1 of Variable +> bool
	isValidScalarVariables(svs) ==
		{
			/* @OnFail("2.2.10 Variables define more than one independent variable: %s",
				{ sv.base.name | sv in seq svs & sv.base.causality = <independent> }) */
			( card { sv | sv in seq svs & sv.base.causality = <independent> } <= 1 ),

			/* @OnFail("2.2.10 Variable names are not unique: %s",
				{ svs(a).base.name | a, b in set inds svs &
					a <> b and svs(a).base.name = svs(b).base.name } ) */
			( card { sv.base.name | sv in seq svs } = len svs )
		}
		union
		{
			-- @OnFail("2.2.10 Variable %s invalid at line %s", sv.base.name, sv.base.location)
			( isValidScalarVariable(sv) )

			| sv in seq svs
		} = {true};

	/**
	 * Raw Variable invariant. Rules defined in the table on p49.
	 */
	isValidScalarVariable: Variable +> bool
	isValidScalarVariable(sv) ==
		let eCausality   = effectiveCausality(sv.base.causality),
			eVariability = effectiveVariability(sv.base.variability),
			eInitial     = effectiveInitial(sv.base.initial, eCausality, eVariability)
		in
		{
			-- Table on p46 defining causality, and p48/49 defining combinations
			/* @OnFail("2.2.10 Variable %s causality/variability/initial/start %s/%s/%s/%s invalid at line %s",
				sv.base.name, eCausality, eVariability, eInitial, sv.start, sv.base.location) */
			(
				cases eCausality:
					<parameter> ->
						eVariability in set {<fixed>, <tunable>}
						and eInitial = <exact>,		-- (A)

					<calculatedParameter> ->
						eVariability in set {<fixed>, <tunable>}
						and eInitial in set {<approx>, <calculated>},	-- (B)

					<input> ->
						eVariability in set {<discrete>, <continuous>}
						and eInitial = nil
						and sv.start <> nil,		-- (D)

					<independent> ->
						eVariability = <continuous>
						and eInitial = nil		-- (D)
						and sv.start = nil
						-- @OnFail("2.2.10 Independent variable must be Real at line %s", sv.base.location)
						and (is_Real(sv)),

					<output> ->
						cases eVariability:
							<constant> ->
								eInitial in set {<exact>},	-- (A)

							<discrete>,
							<continuous> ->
								eInitial in set { <exact>, <approx>, <calculated> }, -- (C)

							others -> false
						end,

					<local> ->
						cases eVariability:
							<constant> ->
								eInitial = <exact>,	-- (A)

							<fixed>,
							<tunable> ->
								eInitial in set { <calculated>, <approx> },	-- (B)

							<discrete>,
							<continuous> ->
								eInitial in set { <exact>, <approx>, <calculated> } -- (C)
						end
				end
			),

			-- Table on p46 defining variability, and p49 defining combinations
			/* @OnFail("2.2.10 Variable %s variability/causality %s/%s invalid at line %s",
				sv.base.name, eVariability, eCausality, sv.base.location) */
			(
				cases eVariability:
					<constant> ->
						eCausality in set {<output>, <local>},

					<fixed>, <tunable> ->
						eCausality in set {<parameter>, <calculatedParameter>, <local>},

					<discrete> ->
						eCausality in set {<input>, <output>, <local>},

					<continuous> ->
						eCausality in set {<input>, <output>, <local>, <independent>}
						-- @OnFail("2.2.10 Continuous variable must be Real at line %s", sv.base.location)
						and (is_Real(sv))
				end
			),

			-- Table on p47 defining initial
			/* @OnFail("2.2.10 Variable %s initial/causality %s/%s invalid at %s",
				sv.base.name, sv.base.initial, eCausality, sv.base.location) */
			(
				sv.base.initial <> nil =>
					(eCausality not in set {<input>, <independent>})
			),

			-- Table on p47 defining initial
			/* @OnFail("2.2.10 Variable %s initial/variability/start %s/%s/%s invalid at line %s",
				sv.base.name, eInitial, eVariability, sv.start, sv.base.location) */
			(
				cases eInitial:
					<exact> ->
						sv.start <> nil,

					<approx> ->
						sv.start <> nil
						and eVariability <> <constant>,

					<calculated> ->
						sv.start = nil
						and eVariability <> <constant>,

					nil ->		-- Note that eInitial can be nil (undefined in table on p48)
						true	-- Tests on eInitial above are sufficient
				end
			),

			-- @OnFail("2.2.10 VendorAnnotations invalid")
			( isValidVendorAnnotations(sv.base.annotations) )
		} = {true};


	/**
	 * Vaidity checks that depend on inherited values from any declaredType - ie. passed
	 * effective Variables.
	 */
	isValidEffectiveVariables: seq1 of Variable +> bool
	isValidEffectiveVariables(svs) ==
	{
		-- @OnFail("2.2.10 Variable %s min/max/start/nominal invalid at line %s", sv.base.name, sv.base.location)
		(
			cases sv:
				mk_Real(-,
					mk_fmi3RealVariableAttributes(-, -),
					mk_fmi3RealAttributes(-, -, -, -, -),
					mk_fmi3FloatXXAttributes(min, max, nominal), start) ->
					isInRange[real](min, max, start)
					-- @OnFail("2.2.10 Real nominal must be >0.0 at line %s", sv.base.location)
					and (nominal <> nil => nominal > 0.0),

				mk_Integer(-, -, mk_fmi3IntXXAttributes(min, max), start) ->
					isInRange[real](min, max, start),

				mk_Enumeration(-, -, min, max, start) ->
					isInRange[real](min, max, start),

				others -> true		-- String, Boolean, Binary, Clock
			end
		)

		| sv in seq svs
	}
	union
	{
		/* @OnFail("2.2.10 Invalid Variable aliasing") */
		(
			let refmap = getAliasRefMap(svs) in
			card dom refmap < len svs => dunion	-- Must be some aliases
			{
				let aliases = refmap(ref) in
				{
					/* @OnFail("2.2.10 Multiple aliases of reference %s/%s are settable: %s", ref.#1, ref.#2,
							{ a.base.name | a in set aliases & isSettable(a) }) */
					( card { a | a in set aliases & isSettable(a) } <= 1 ),

					/* @OnFail("2.2.10 Aliases of reference %s/%s are settable and independent: %s", ref.#1, ref.#2,
							{ {a.base.name, b.base.name} | a, b in set aliases &
								a <> b and isSettable(a) and b.base.causality = <independent> } ) */
					( not exists a, b in set aliases & a <> b and isSettable(a) and b.base.causality = <independent> ),

					-- @OnFail("2.2.10 Too many aliases of reference %s/%s have start set", ref.#1, ref.#2)
					( card { a.start | a in set aliases & a.base.variability <> <constant> and a.start <> nil } <= 1 ),

					-- @OnFail("2.2.10 Constant aliases of reference %s/%s have different start values", ref.#1, ref.#2)
					( card { a.start | a in set aliases & a.base.variability = <constant> and a.start <> nil } <= 1 ),

					-- @OnFail("2.2.10 Aliases of reference %s/%s must all be constant or variable", ref.#1, ref.#2)
					( card { a | a in set aliases & a.base.variability = <constant> } in set {0, card aliases} ),

					-- @OnFail("2.2.10 Aliases of reference %s/%s must all have same unit/baseUnits", ref.#1, ref.#2)
					( card { a.realAtts.unit | a in set aliases & is_Real(a) } <= 1 ),

					/*
					 * In case of different variability among the set of alias variables, and if that set of aliases
					 * does not contain an input or parameter, the variability should be the highest of the variables
					 * in the set, e.g. continuous > discrete > tunable > fixed. If the set includes a parameter or
					 * input the aliases will have the stated variability of that parameter or input.
					 */
					 let vars = { a.base.variability | a in set aliases } in
					 	if exists a in set aliases & a.base.causality in set {<input>, <parameter>}
					 	then
					 		let a in set aliases be st a.base.causality in set {<input>, <parameter>} in
				 				/* @OnFail("2.2.10 Warning: aliases of reference %s/%s must all be %s, because of %s",
				 						ref.#1, ref.#2, a.base.variability, a.base.name) */
				 				( vars = { a.base.variability } ) or true
					 	else
					 		let highest in set vars be st not exists v in set vars & v > highest in
					 			-- @OnFail("2.2.10 Warning: aliases of reference %s/%s must all be %s", ref.#1, ref.#2, highest)
					 			( vars = { highest } ) or true
				}

				| ref in set dom refmap & card refmap(ref) > 1
			}
			= {true}
		)
	} = {true};

	/**
	 * The default value for initial, defined by the (effective) values of causality and
	 * variability, via the table on p48/49.
	 */ 
	effectiveInitial: [Initial] * Causality * Variability -> [Initial]
	effectiveInitial(i, eCausality, eVariability) ==
		if i <> nil
		then i
		else cases eCausality:
			<parameter> ->
				cases eVariability:
					<fixed>, <tunable> -> <exact>,
					others -> nil
				end,

			<calculatedParameter> ->
				cases eVariability:
					<fixed>, <tunable> -> <calculated>,
					others -> nil
				end,

			<input> ->
				cases eVariability:
					<discrete>, <continuous> -> nil,
					others -> nil
				end,

			<output> ->
				cases eVariability:
					<constant> -> <exact>,
					<discrete>, <continuous> -> <calculated>,
					others -> nil
				end,

			<local> ->
				cases eVariability:
					<constant> -> <exact>,
					<fixed>, <tunable> -> <calculated>,
					<discrete>, <continuous> -> <calculated>
				end,

			<independent> ->
				cases eVariability:
					<continuous> -> nil,
					others -> nil
				end
		end;

	effectiveCausality: [Causality] -> Causality
	effectiveCausality(c) ==
		if c = nil
		then <local>
		else c;

	effectiveVariability: [Variability] -> Variability
	effectiveVariability(v) ==
		if v = nil
		then <continuous>
		else v;

	/**
	 * Generate all of the effective Variables. Note that an effective "time"
	 * variable is created if there are no independent variables defined.
	 */
	effectiveScalarVariables: FMIModelDescription +> seq1 of Variable
	effectiveScalarVariables(model) ==
		let evs =
		[
			effectiveVariable(sv, model.typeDefinitions)
			| sv in seq model.modelVariables
		]
		in
			if not exists esv in seq evs & esv.base.causality = <independent>
			then evs ^
				[
					-- If no variable is defined as "independent", it is implicitly
					-- present with name = "time" and unit = "s"

					mk_Real
					(
						mk_fmi3VariableBase(
							0,		-- Line
							nil,
							nil,
							"time",
							let allvrs = { sv.base.valueReference | sv in seq evs } in
								let vr in set {1, ..., len evs + 1} be st vr not in set allvrs in vr,
							"Implicit independent time variable",
							<independent>,
							<continuous>,
							nil,
							nil,
							nil,
							nil,
							nil),
						mk_fmi3RealVariableAttributes(nil, nil),
						mk_fmi3RealAttributes(nil, nil, nil, nil, nil),
						mk_fmi3FloatXXAttributes(0, nil, nil),
						nil
					)
				]
			else evs;

	/**
	 * Generate the effective value of a Variable, accounting for fields inherited from
	 * its declaredType.
	 */
	effectiveVariable: Variable * [set1 of VariableType] +> Variable
	effectiveVariable(v, typedefs) ==
		let causality	= effectiveCausality(v.base.causality),
			variability	= effectiveVariability(v.base.variability),
			initial		= effectiveInitial(v.base.initial, causality, variability),
			newbase		= mu(v.base,
				causality	|-> causality,
				variability	|-> variability,
				initial		|-> initial
			)
		in
		if v.base.declaredType = nil or typedefs = nil
		then v
		else let dtypes = { td | td in set typedefs & td.base.name = v.base.declaredType } in
			cases dtypes:
				{ dt } ->
					cases dt:
						mk_RealType(-, -,
							mk_fmi3RealAttributes(dquantity, dunit, ddunit,drelq, dunbounded),
							mk_fmi3FloatXXAttributes(dmin, dmax, dnominal)) ->
							if is_Real(v) then
								mu(v,
									base |-> newbase,

									realAtts |-> mu(v.realAtts,
										quantity			|-> override[NormalizedString1](v.realAtts.quantity, dquantity),
										unit				|-> override[NormalizedString1](v.realAtts.unit, dunit),
										displayUnit			|-> override[NormalizedString1](v.realAtts.displayUnit, ddunit),
										relativeQuantity	|-> override[bool](v.realAtts.relativeQuantity, drelq),
										unbounded			|-> override[bool](v.realAtts.unbounded, dunbounded)
									),

									floatAtts |-> mu(v.floatAtts,
										min					|-> override[real](v.floatAtts.min, dmin),
										max					|-> override[real](v.floatAtts.max, dmax),
										nominal				|-> override[real](v.floatAtts.nominal, dnominal)
									)
								)
							else
								v,	-- Error reported elsewhere

						mk_IntegerType(-, -,
							mk_fmi3IntegerAttributes(dquantity),
							mk_fmi3IntXXAttributes(dmin, dmax)) ->
							if is_Integer(v) then
								mu(v,
									base |-> newbase,

									integerAtts |-> mu(v.integerAtts,
										quantity	|-> override[NormalizedString1](v.integerAtts.quantity, dquantity)
									),

									intAtts |-> mu(v.intAtts,
										min			|-> override[real](v.intAtts.min, dmin),
										max			|-> override[real](v.intAtts.max, dmax)
									)
								)
							else
								v,	-- Error reported elsewhere

						mk_EnumerationType(-, dquantity, -) ->
							if is_Enumeration(v) then
								mu(v,
									base		|-> newbase,
									quantity	|-> override[NormalizedString1](v.quantity, dquantity)
								)
							else
								v,	-- Error reported elsewhere

						others ->
							mu(v, base |-> newbase)
					end,

				others -> v		-- Duplicate or missing typedefs caught elsewhere
			end;

	/**
	 * Helper to take an overriding non-nil value
	 */
	override[@T]: [@T] * [@T] +> [@T]
	override(default, value) ==
		if value <> nil then value else default;

	/**
	 * Test whether a Variable is settable with fmi2SetXXX. See sections 3.2.3 and 4.2.4.
	 */
	isSettable: Variable +> bool
	isSettable(sv) ==
		sv.base.variability <> <constant> and sv.base.initial in set { <exact>, <approx> }
		or
		sv.base.causality = <input>
		or
		sv.base.causality = <parameter> and sv.base.variability = <tunable>;

	/**
	 * Test whether two Variables are the same basic type.
	 */
	sameTypes: Variable * Variable +> bool
	sameTypes(a, b) ==
		typeName(a) = typeName(b);

	/**
	 * Get the string name of a Variable type.
	 */
	typeName: Variable +> seq1 of char
	typeName(v) ==
		if is_Real(v) then "Real"
		else if is_Integer(v) then "Integer"
		else if is_Boolean(v) then "Boolean"
		else if is_String(v) then "String"
		else if is_Enumeration(v) then "Enumeration_" ^ v.base.declaredType
		else "?";

	/**
	 * Return the Variables from a list which are continuous-time states, defined to be
	 * Real variables which are continuous and have a derivative.
	 */
	continuousTimeStates: seq of Variable -> set of Variable
	continuousTimeStates(svs) ==
		{ svs(i) | i in set inds svs & isContinuousTimeState(i, svs) };

	/**
	 * Test ith index variable as a continuous time state.
	 */
	isContinuousTimeState: nat1 * seq of Variable +> bool
	isContinuousTimeState(i, svs) ==
		is_Real(svs(i))
		and svs(i).base.variability = <continuous> 
		and exists j in seq svs &
			is_Real(j) and j.varAtts.derivative = i;

	/**
	 * Return the Variables from a list which are state derivatives, defined to be
	 * variables which are the derivative of some other variable.
	 */
	stateDerivatives: seq of Variable -> set of Variable
	stateDerivatives(svs) ==
		{ sv | sv in seq svs & isStateDerivative(sv) };

	/**
	 * Test Variable as a state derivative.
	 */
	isStateDerivative: Variable +> bool
	isStateDerivative(sv) ==
		is_Real(sv) and sv.varAtts.derivative <> nil;

	/**
	 * Get derivative order of a variable.
	 */
	getDerivativeBase: nat1 * seq of Variable +> nat * nat1
	getDerivativeBase(vr, svs) ==
		if not is_Real(svs(vr)) or svs(vr).varAtts.derivative = nil
		then mk_(0, vr)
		else let mk_(o, r) = getDerivativeBase(svs(vr).varAtts.derivative, svs) in
			mk_(o+1, r)
	measure is not yet specified;

	/**
	 * Get the alias refmap, from valueReference/type pairs to sets of SVs
	 */
	getAliasRefMap: seq1 of Variable -> map nat * seq of char to set of Variable
	getAliasRefMap(svs) ==
		let tags = { svi |-> aliasTag(svs(svi)) | svi in set inds svs } in
		{
			tags(svi) |-> { svs(v) | v in set inds svs & tags(v) = tags(svi) }
			| svi in set inds svs
		};

	/**
	 * Generate an alias "tag" for a Variable, made of the valueReference and
	 * the type of the variable. All aliases of a variable have the same tag.
	 */
	aliasTag: Variable +> nat * seq1 of char
	aliasTag(sv) ==
		mk_(sv.base.valueReference, typeName(sv));
